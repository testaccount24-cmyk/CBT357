//-YOUR-USERID-M2 JOB (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  GEN SUPERC COMPARES FOR PDSMATCH <UNEQUAL> MEMBERS
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 PDSMATC:  PROC OPTIONS(MAIN) REORDER;

        DCL  REC        CHAR(32760) STATIC VARYING;
        DCL  (II,JJ,KK) FIXED BIN(31) STATIC;
        DCL  CARD       CHAR(00080) STATIC;
        DCL  DSN1       CHAR(00044) STATIC VARYING;
        DCL  DSN2       CHAR(00044) STATIC VARYING;
        DCL  MEM        CHAR(00008) STATIC VARYING;
        DCL  I_EOF_IND  CHAR(01) STATIC INIT('N');
        DCL  UNEQU_IND  CHAR(01) STATIC INIT('N');

        ON ENDFILE(I) I_EOF_IND = 'Y';
        READ FILE(I) INTO(REC);

        DO WHILE ( I_EOF_IND  = 'N' );
           IF SUBSTR(REC,2,11) = 'LIBRARY1 - '
              THEN DO;
                 DO JJ = 14 TO 80 WHILE(SUBSTR(REC,JJ,1) ¬= ' ');
                 END;
                 DSN1 = SUBSTR(REC,13,JJ-13);
              END;
           ELSE IF SUBSTR(REC,2,11) = 'LIBRARY2 - '
              THEN DO;
                 DO JJ = 14 TO 80 WHILE(SUBSTR(REC,JJ,1) ¬= ' ');
                 END;
                 DSN2 = SUBSTR(REC,13,JJ-13);
              END;
           ELSE IF SUBSTR(REC,12,9) = '<UNEQUAL>'
              THEN DO;
                 IF UNEQU_IND = 'N'
                    THEN DO;
                       UNEQU_IND = 'Y';
                       CARD = '//-YOUR-USERID-S    JOB      (-YOUR-ACCT-INFO-),'
                          || '''-YOUR-NAME-'',';
                       WRITE FILE(O) FROM(CARD);
                       CARD = '//             CLASS=2,MSGCLASS=X,'
                          || 'NOTIFY=-YOUR-USERID-';
                       WRITE FILE(O) FROM(CARD);
                       CARD = '//***';
                       WRITE FILE(O) FROM(CARD);
                    END;
                 CARD = '//' || SUBSTR(REC,2,8)
                    || '   EXEC     PGM=ISRSUPC,REGION=8M,';
                 WRITE FILE(O) FROM(CARD);
                 CARD = '//             PARM=(DELTAL,LINECMP,'
                   || ''' SEQ'','''')';
                 WRITE FILE(O) FROM(CARD);
                 CARD = '//NEWDD        DD       DISP=SHR,';
                 WRITE FILE(O) FROM(CARD);
                 DO JJ = 2 TO 9 WHILE(SUBSTR(REC,JJ,1) ¬= ' ');
                 END;
                 MEM = SUBSTR(REC,2,JJ-2);
                 CARD = '//             DSN=' || DSN1
                    || '(' || MEM || ')';
                 WRITE FILE(O) FROM(CARD);
                 CARD = '//OLDDD        DD       DISP=SHR,';
                 WRITE FILE(O) FROM(CARD);
                 CARD = '//             DSN=' || DSN2
                    || '(' || MEM || ')';
                 WRITE FILE(O) FROM(CARD);
                 CARD = '//OUTDD        DD       SYSOUT=*';
                 WRITE FILE(O) FROM(CARD);
                 CARD = '//***';
                 WRITE FILE(O) FROM(CARD);
              END;
           READ FILE(I) INTO(REC);
           END;

        END PDSMATC;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//L          EXEC     PGM=HEWLKED,COND=(5,LT),
//             PARM='XREF,LIST,LET,DCBS,AMODE=31,RMODE=ANY',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DISP=SHR,DSN=-YOUR-LOADLIB-
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD       *
  SETSSI   04022400
  ENTRY    PLISTART
  NAME     PDSMATC(R)
//***
