/** REXX - SPLITAFT - SPLIT COLUMNS BASED ON ONE OR MORE CHARACTERS **/

   ADDRESS ISREDIT
   "MACRO (STRING INDENT) NOPROCESS"
   IF STRING = "" | STRING = "?" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      TOP = 1
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END
   IF SUBSTR(STRING,1,1) = '"' | SUBSTR(STRING,1,1) = "'",
      THEN STRING = TRANSLATE(SUBSTR(STRING,2,LENGTH(STRING)-2))
      ELSE STRING = TRANSLATE(STRING)
   IF INDENT = "" | Â¬DATATYPE(INDENT,'W') THEN INDENT = 4

   DO NN = BOT TO TOP BY -1
      "(REC) = LINE" NN
      II = POS(STRING,REC)
      IF II > 0 & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D" THEN DO
               DO JJ = 8 TO 72 WHILE(SUBSTR(REC,JJ,1) = " ")
               END JJ
               DO KK = (II+LENGTH(STRING)) TO 72,
                  WHILE(SUBSTR(REC,KK,1) = " ")
               END KK
               KK = KK - II - LENGTH(STRING)
               SPLITREC = COPIES(" ",JJ+INDENT-1-KK),
                  || SUBSTR(REC,II+LENGTH(STRING))
               "LINE_AFTER (NN) = (SPLITREC)"
               REC = SUBSTR(REC,1,II+LENGTH(STRING)-1)
               "LINE " NN " = (REC)"
            END
   END

   "LOCATE" TOP
   RETURN(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SPLITAFT - SPLIT COLUMNS AFTER STRING                  "
   SAY
   SAY " FORMAT "
   SAY "    SPLITAFT STRING  ( INDENT )                       "
   SAY "       WILL SPLIT THE SPECIFIED LINES AFTER THE STRING"
   SAY "       PROVIDED. A RANGE OF LINES MAY BE SPECIFIED BY "
   SAY "       C/CC/C## OR THE ENTIRE FILE WILL BE PROCESSED. "
   SAY "       IF NO STRING IS SPECIFIED, THIS HELP WILL BE   "
   SAY "       SHOWN. QUOTES MAY BE USED AROUND THE STRING.   "
   SAY "       INDENT DEFAULTS TO 4.                          "
   SAY
   SAY " EXAMPLE "
   SAY "    SPLITAFT ' TO '                            "
   SAY "       ON THE FOLLOWING:                       "
   SAY "          MOVE      7   TO WS-SHORT-FIELD      "
   SAY "          MOVE     16   TO WS-MIDDLE-FIELD     "
   SAY "          MOVE    427   TO WS-REAL-BIG-FIELD   "
   SAY "       WILL RESULT IN:                         "
   SAY "          MOVE      7   TO                     "
   SAY "              WS-SHORT-FIELD                   "
   SAY "          MOVE     16   TO                     "
   SAY "              WS-MIDDLE-FIELD                  "
   SAY "          MOVE    427   TO                     "
   SAY "              WS-REAL-BIG-FIELD                "
   EXIT(1)

