/**  REXX -- Display and allow changes to PDS Member's statistics   **/
/*********************************************************************/
   ARG PARMS
   Do ii = 2 to length(PARMS) while(substr(PARMS,ii,1) <> '(')
   End ii
   jj  = length(PARMS) - 2
   PDS = substr(PARMS,2,ii-2)
   MEM = substr(PARMS,ii+1,length(PARMS)-ii-2)
   Call setup_libdefs
   quit = 'NO'
   Call get_stats
   Call display_panel
   If quit <> 'YES' then do
      Call update_stats
      Address ispexec 'SETMSG MSG(MSGN001)'
   End
   Else do
      Address ispexec
      "LMCLOSE   DATAID(&TTID)"
      "LMFREE    DATAID(&TTID)"
      Address ispexec 'SETMSG MSG(MSGN003)'
   End
   Call destroy_libdefs
   Return 1
/******************* setup_libdefs ********************************/
setup_libdefs: Procedure Expose ddname

   ddname = '$'right(time(s),7,'0')
   Address tso 'ALLOC NEW DEL F('ddname') DIR(1) SP(1) TR RECF(F B)
                BLKS(0) LRECL(80) REU UNIT(SYSDA)'
   Address ispexec
   'LMINIT DATAID(DID) DDNAME('ddname') ENQ(EXCLU)'
   'LMOPEN DATAID(&DID) OPTION(OUTPUT)'
   a=1
   Do 2
     Do a=a to 999 Until substr(line,1,8)='/*MEMBER'
       line = sourceline(a)
     End
     Parse Var line . memname .
     Do a=a+1 to 999 While substr(line,1,2) \= '*/'
       line = sourceline(a)
       'LMPUT DATAID(&DID) MODE(INVAR) DATALOC(LINE) DATALEN(80)'
     End
     'LMMADD DATAID(&DID) MEMBER(&MEMNAME)'
   End
   'LMFREE DATAID(&DID)'
   'LIBDEF ISPPLIB LIBRARY ID('ddname') STACK'
   'LIBDEF ISPMLIB LIBRARY ID('ddname') STACK'
   Return
/********************* get_stats **********************************/
get_stats:
   Address ispexec
   "LMINIT  DATAID(TTID) DATASET('"PDS"') ENQ(SHRW)"
   "LMOPEN  DATAID(&TTID) OPTION(INPUT)"
   "LMMFIND DATAID(&TTID) MEMBER("MEM") STATS(YES)"
   VV      = ZLVERS
   CREDATE = ZLCDATE
   CHGDATE = ZLMDATE
   CHGTM   = ZLMTIME
   INISZ   = ZLINORC
   CURSZ   = ZLCNORC
   MD      = ZLMOD
   USERID  = ZLUSER
   Return
/********************* update_stats **********************************/
update_stats:
   Address ispexec
   "LMMSTATS   DATAID(&TTID) MEMBER("MEM")",
              "VERSION("VV") MODLEVEL("MD")",
              "CREATED("CREDATE") MODDATE("CHGDATE")",
              "MODTIME("CHGTM") CURSIZE("CURSZ")",
              "INITSIZE("INISZ") USER("USERID")"
   "LMCLOSE   DATAID(&TTID)"
   "LMFREE    DATAID(&TTID)"
   Return
/********************* DISPLAY PANEL ******************************/
display_panel:
   Address ispexec
   'ADDPOP'
   'DISPLAY PANEL(POPUP)'
   If rc = 8 then quit = 'YES'
   'REMPOP'
   Return
/********************* DESTROY_LIBDEFS ***************************/
destroy_libdefs:
   Address ispexec 'LIBDEF ISPPLIB '
   Address ispexec 'LIBDEF ISPMLIB '
   Address tso 'FREE F('ddname')'
   Return

/*------------------------------------------------------------------*/
/* In-line panels and messages are defined below                    */
/*------------------------------------------------------------------*/
/*MEMBER POPUP  This panel prompts for XREF input
)ATTR DEFAULT(%+_)
     % TYPE(TEXT)  INTENS (HIGH)
     + TYPE(TEXT)  INTENS (LOW)  SKIP(ON)
     _ TYPE(INPUT) INTENS (HIGH) CAPS(ON) JUST(LEFT)
     $ TYPE(INPUT) INTENS (HIGH) CAPS(ON) JUST(LEFT)
     @ TYPE(TEXT)  INTENS (HIGH) COLOR(TURQ)
     ! TYPE(TEXT)  INTENS (LOW)  CAPS(ON)
)BODY WINDOW(60,16)
@  -- PDS Statistics Display/Update                   -- +

%PDS:+$PDS                                          +
%MEM:+$MEM     +

%Version:   +$VV+          ** 01 to 99               +
%Mod Count: +$MD+          ** 00 to 99               +
%Create Dt: +$CREDATE +    ** YY/MM/DD               +
%Change Dt: +$CHGDATE +    ** YY/MM/DD               +
%Change Tm: +$CHGTM +      ** HH:MM                  +
%Initial Sz:+$INISZ+       ** 0 to 32767             +
%Current Sz:+$CURSZ+       ** 0 to 32767             +
%UserID:    +$USERID +     ** 1 to 7 characters      +

@  -- Press <ENTER> to update or <PF3> (END) to Quit  -- +
)INIT
   .CURSOR  = VV
)PROC
)END
*/
/*MEMBER MSGN00  This is a message member. Can be in same lib as panel.
MSGN001 'Processed &MEM member...'
'Data set produced is &PDS for member &MEM...'
MSGN003 'No changes made...'
'Terminated with no changes entered...'
*/
