/**  REXX -- INSERT COLUMNS DELETING ORIGINAL COLUMNS  **/
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'MOVEINSD MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (FRMLEFT,FRMRIGHT,TOLEFT,LBL1,LBL2)"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY 'MOVEINSD EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT',
          'WITH NO PENDING EDIT COMMANDS!'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG = ''
   IF FRMLEFT = ''  |  FRMRIGHT = ''  |  TOLEFT = ''  THEN DO
      MESG='PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMLEFT,'W')  |  FRMLEFT = 0  THEN DO
      MESG="'"FRMLEFT"' VALUE OF FROM-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMRIGHT,'W')  |  FRMRIGHT = 0  THEN DO
      MESG="'"FRMRIGHT"' VALUE OF FROM-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF FRMLEFT>FRMRIGHT THEN DO
      MESG = "FROM-LEFT-COL "FRMLEFT" IS GREATER THAN FROM-RIGHT-COL " +
         FRMRIGHT
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TOLEFT,'W')  |  TOLEFT = 0  THEN DO
      MESG="'"TOLEFT"' VALUE OF TO-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF TOLEFT >= FRMLEFT & TOLEFT <= FRMRIGHT THEN DO
      MESG="'"TOLEFT"' IS BETWEEN FROM COLUMNS!"
      SIGNAL ERROR
   END
   LEN = FRMRIGHT - FRMLEFT + 1
   TORIGHT = TOLEFT + LEN - 1
   IF TOLEFT > TORIGHT THEN DO
      MESG="TO-LEFT-COL "TOLEFT" IS GREATER THAN TO-RIGHT-COL "TORIGHT
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   IF TORIGHT > LRECL THEN DO
      MESG = "TO-RIGHT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
   END
   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
        END
   END
   "ISREDIT ("LRECL") = LRECL "
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR     "
   "ISREDIT ("ANUM") = AUTONUM              "
   "ISREDIT (END) = LINENUM .ZLAST"
   START = 1
   IF LBL1 <> '' THEN
      "ISREDIT (START) = LINENUM &LBL1"

   IF LBL2 <> '' THEN
       "ISREDIT (END) = LINENUM &LBL2"
   ELSE
       LBL2 = LBL1

   IF LBL1 > LBL2 THEN DO
      WST   = START
      START = END
      END   = WST
   END

   DO PNTR =  START  TO  END  BY 1
      "ISREDIT (RECORD) = LINE &PNTR"
      IF FRMLEFT > TOLEFT
         THEN RECORD = SUBSTR(RECORD,1,TOLEFT-1),
               || SUBSTR(RECORD,FRMLEFT,LEN),
               || SUBSTR(RECORD,TOLEFT,FRMLEFT-TOLEFT),
               || SUBSTR(RECORD,FRMRIGHT+1)
         ELSE RECORD = SUBSTR(RECORD,1,FRMLEFT-1),
               || SUBSTR(RECORD,FRMRIGHT+1,TOLEFT-FRMRIGHT-1),
               || SUBSTR(RECORD,FRMLEFT,LEN),
               || SUBSTR(RECORD,TOLEFT)
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END
   "ISREDIT LOCATE" START
   "ISREDIT AUTONUM = "ANUM
    SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   SIGNAL DISPDOC

EOJ:
   X=MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "MOVEINSD - INSERT COLUMNS BEFORE A SPECIFIC COLUMN"
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY
   SAY "    MOVEINSD FROM-LEFT-COL  FROM-RIGHT-COL  TO-LEFT-COL  .A .B "
   SAY
   SAY "       COLUMN PARAMETERS ARE REQUIRED. LABEL RANGE IS OPTIONAL."
   SAY "       MOVES AND INSERTS DATA, DELETING ORIGINAL COLUMNS.      "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    MOVEINSD  21 28  1 "
   SAY "       WILL MOVE THE CONTENTS OF COLS 21      "
   SAY "       THRU 28 TO COLUMNS 1 THRU 8 SHIFTING   "
   SAY "       COLUMNS 1 ONWARD TO THE RIGHT 8 PLACES "
   SAY "       DELETING THE ORIGINAL SOURCE COLUMNS.  "
   SAY
   EXIT(1)
