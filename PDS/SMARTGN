//-YOUR-USERID-S JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  GENERATE CONTROL CARDS FOR A LIST OF DATA SETS
//***    CONTROL CARDS TO BE GENERATED ARE IN FILE SYSCTL
//***    INPUT DATA SET NAMES ARE IN FILE SYSIN
//***    OUTPUT CONTROL CARD COPIES ARE IN FILE SYSOUT
//***
//***  ? IN THE CONTROL CARDS WILL BE REPLACED BY THE DATA SET NAME
//***  # (UP TO 8 IN A ROW) WILL BE REPLACED BY THE INPUT NUMBER
//***  ! IS REPLACED BY TODAY AS YYMMDD
//***  % IS REPLACED BY TODAY AS CCYYMMDD
//***  @ IS REPLACED BY CURRENT TIME AS HHMMSS
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 SMARTGN: PROC OPTIONS(MAIN) REORDER;

        DCL  REC               CHAR(00080) STATIC;
        DCL  REC_ARRAY(01000)  CHAR(00080) STATIC INIT('');
        DCL  NAME              CHAR(00050) STATIC VARYING INIT('');
        DCL  CCYYMMDD          CHAR(00008) STATIC;
        DCL  YYMMDD            CHAR(00006) STATIC;
        DCL  HHMMSS            CHAR(00006) STATIC;
        DCL  PIC99999999       PIC '99999999' STATIC INIT(0);
        DCL  PIC_OFFSET        FIXED BIN(31) STATIC;
        DCL  REC_COUNT         FIXED BIN(31) STATIC INIT(0);
        DCL  INPUT_COUNT       FIXED BIN(31) STATIC INIT(0);
        DCL  (II,JJ,KK)        FIXED BIN(31) STATIC INIT(0);
        DCL  SYSCTL_EOF_IND    CHAR(01) STATIC INIT('N');
        DCL  SYSIN_EOF_IND     CHAR(01) STATIC INIT('N');

        DCL  DATE      BUILTIN;
        DCL  TIME      BUILTIN;


        YYMMDD   = DATE;
        IF SUBSTR(YYMMDD,1,2) < '50'
           THEN CCYYMMDD = '20' || YYMMDD;
           ELSE CCYYMMDD = '19' || YYMMDD;
        HHMMSS   = SUBSTR(TIME,01,06);

        ON ENDFILE(SYSCTL) SYSCTL_EOF_IND = 'Y';
        READ FILE(SYSCTL) INTO(REC);
        DO WHILE ( SYSCTL_EOF_IND  ¬= 'Y' );
           CALL FILL_CTL_ARRAY;
           END;

        ON ENDFILE(SYSIN) SYSIN_EOF_IND = 'Y';
        READ FILE(SYSIN) INTO(REC);
        DO WHILE ( SYSIN_EOF_IND   ¬= 'Y' );
           CALL GEN_CTL_FOR_EACH_INPUT;
           END;


 FILL_CTL_ARRAY: PROC;
        REC_COUNT = REC_COUNT + 1;
        REC_ARRAY(REC_COUNT) = REC;
        IF REC_COUNT >= HBOUND(REC_ARRAY,1)
           THEN SYSCTL_EOF_IND = 'Y';
           ELSE READ FILE(SYSCTL) INTO(REC);
        END FILL_CTL_ARRAY;

 GEN_CTL_FOR_EACH_INPUT: PROC;
        INPUT_COUNT = INPUT_COUNT + 1;
        DO II = 1 TO LENGTH(REC)
              WHILE ( SUBSTR(REC,II,1) ¬= ' ' );
           END;
        IF II = 1
           THEN NAME = 'BLANK INPUT';
           ELSE NAME = SUBSTR(REC,1,II-1);
        DO II = 1 TO REC_COUNT;
           REC = REC_ARRAY(II);
           JJ = INDEX(REC,'?');
           DO WHILE ( JJ > 0 );
              REC = SUBSTR(REC,1,JJ-1)
                 || NAME
                 || SUBSTR(REC,JJ+1);
              JJ = INDEX(REC,'?');
              END;
           JJ = INDEX(REC,'#');
           DO WHILE ( JJ > 0 );
              PIC99999999 = INPUT_COUNT;
              DO KK = JJ TO 80
                    WHILE ( SUBSTR(REC,KK,1) = '#' );
                 END;
              REC = SUBSTR(REC,1,JJ-1)
                 || SUBSTR(PIC99999999,9-KK+JJ)
                 || SUBSTR(REC,KK);
              JJ = INDEX(REC,'#');
              END;
           JJ = INDEX(REC,'!');
           DO WHILE ( JJ > 0 );
              REC = SUBSTR(REC,1,JJ-1)
                 || YYMMDD
                 || SUBSTR(REC,JJ+1);
              JJ = INDEX(REC,'!');
              END;
           JJ = INDEX(REC,'%');
           DO WHILE ( JJ > 0 );
              REC = SUBSTR(REC,1,JJ-1)
                 || CCYYMMDD
                 || SUBSTR(REC,JJ+1);
              JJ = INDEX(REC,'%');
              END;
           JJ = INDEX(REC,'@');
           DO WHILE ( JJ > 0 );
              REC = SUBSTR(REC,1,JJ-1)
                 || HHMMSS
                 || SUBSTR(REC,JJ+1);
              JJ = INDEX(REC,'@');
              END;
           WRITE FILE(SYSOUT) FROM(REC);
           END;
        READ FILE(SYSIN) INTO(REC);
        END GEN_CTL_FOR_EACH_INPUT;

        END SMARTGN;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//L          EXEC     PGM=HEWLKED,
//             PARM='XREF,LIST,LET,DCBS,AMODE(31),RMODE(ANY)',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD       *
  SETSSI   99111403
  ENTRY    PLISTART
  NAME     SMARTGN(R)
//***
