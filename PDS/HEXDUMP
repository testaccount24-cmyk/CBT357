//-YOUR-USERID-H JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 /***  BASIC INPUT & PRINT W/ HEX PL/I PROGRAM  ***/

 HEXDUMP: PROC OPTIONS(MAIN) REORDER;

        DCL  REC       CHAR(32700)   STATIC VARYING;
        DCL  I_EOF_IND CHAR(01)      STATIC INIT('N');
        DCL  II        FIXED BIN(31) STATIC INIT(0);
        DCL  READ#     FIXED BIN(31) STATIC INIT(0);
        DCL  PRINTED#  FIXED BIN(31) STATIC INIT(0);
        DCL  DATE      BUILTIN;
        DCL  TIME      BUILTIN;
        DCL  TO_HEX1   CHAR(256)     STATIC;
        DCL  TO_HEX2   CHAR(256)     STATIC;
        DCL  MASK      CHAR(41)      STATIC INIT(' ');
        DCL  FROM      CHAR(44)      STATIC INIT(' ');
        DCL  REC#      PIC'ZZZZ9'    STATIC INIT(0);
        DCL  1  DATAOUT STATIC,
            10  RECNO  CHAR(05)      INIT(' '),
            10  HEX_OP CHAR(44)      INIT(' '),
            10  FILL10 CHAR(02)      INIT(' *'),
            10  CHR_OP CHAR(20)      INIT(' '),
            10  FILL20 CHAR(01)      INIT('*');
        DCL  1  NULLOUT STATIC,
            10  REC##  CHAR(05)      INIT(' '),
            10  NULMSG CHAR(44)
                INIT('       --- NULL (0 LENGTH) RECORD ---       '),
            10  NULFIL CHAR(02)      INIT(' *'),
            10  NULCHR CHAR(20)      INIT(' '),
            10  NULTRM CHAR(01)      INIT('*');

        ON ENDFILE(SYSUT1) I_EOF_IND = 'Y';
        TO_HEX1 = (16)'0' || (16)'1' || (16)'2' || (16)'3'
               || (16)'4' || (16)'5' || (16)'6' || (16)'7'
               || (16)'8' || (16)'9' || (16)'A' || (16)'B'
               || (16)'C' || (16)'D' || (16)'E' || (16)'F';
        TO_HEX2 = (16)'0123456789ABCDEF';

        READ FILE(SYSUT1) INTO(REC);

        DO WHILE ( I_EOF_IND  = 'N' );
           READ# = READ# + 1;
           IF LENGTH(REC) > 0
              THEN DO;
                 II    = LENGTH(REC);
                 REC#  = READ#;
                 RECNO = REC#;
                 DO WHILE ( II > 0 );
                    CHR_OP = SUBSTR(REC,1);
                    MASK = TRANSLATE(CHR_OP,TO_HEX1)
                       ||  TRANSLATE(CHR_OP,TO_HEX2);
                    FROM
                       = '&0K1L2M3N4O&5P6Q7R8S9T&AUBVCWDXEY&FZG@H#I$J%';
                    IF II < 20
                       THEN SUBSTR(FROM,(II*2)+(((II*2)/10)+2))
                          = (44)'&';
                    HEX_OP = TRANSLATE(
                       FROM,
                       MASK,
                       '0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZ@#$%&');
                    WRITE FILE(SYSUT2) FROM(DATAOUT);
                    RECNO = ' ';
                    REC = SUBSTR(REC,21);
                    II = II - 20;
                    END;        /**  DO WHILE ( II > 0 )  **/
                 END;           /**  IF LENGTH(REC) > 0  THEN...  **/
              ELSE DO;
                 REC#  = READ#;
                 REC## = REC#;
                 WRITE FILE(SYSUT2) FROM(NULLOUT);
                 END;           /**  IF LENGTH(REC) > 0  ELSE...  **/
           PRINTED# = PRINTED# + 1;
           READ FILE(SYSUT1) INTO(REC);
           END;                 /**  DO WHILE ( I_EOF_IND  = 'N' )  **/

        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  ', DATE, ' STATISTICS ', TIME, '  ****' )
                 ( SKIP(1), A, A, A, A, A );
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  NUMBER RECORDS READ:    ', READ# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS PRINTED: ', PRINTED# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        END HEXDUMP;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(2960,(111,12)),DCB=BLKSIZE=2960
//***
//L          EXEC     PGM=HEWLKED,
//             PARM='XREF,LIST,LET,DCBS',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD       *
  SETSSI   98121300
  ENTRY    PLISTART
  NAME     HEXDUMP(R)
//***
