/**  REXX -- Place lines from the default clipboard after multiple   **/
/***         lines in the current file                                */

   Address ISPEXEC
   'ISREDIT MACRO (PARM1) NOPROCESS'
   Parm1 = translate(Parm1)
   If Parm1 = '?' then signal DispDoc

   If  datatype(Parm1,'W')  then  Skip_Value = Parm1
                            else  Skip_Value = 1

   'ISREDIT PROCESS RANGE C'
   Select
       When rc = 0 then do
            'ISREDIT (ZDEST1) = LINENUM .ZFRANGE'
            'ISREDIT (ZDEST2) = LINENUM .ZLRANGE'
       End
       When rc <= 8 then do
            zedsmsg = 'Enter "C/CC" line cmds'
            zedlmsg = 'PA requires a "C" or "CC" line command'
            'SETMSG MSG(ISRZ001)'
            Exit(12)
       End
       When rc < 20 then
           Exit 12
       When rc = 20 then
           zdest = 0
       Otherwise Exit 12
   End

   if  ( ZDest2 - ZDest1 ) // Skip_Value <> 0
      then ZDest2 = ZDest2 - ( ( ZDest2 - ZDest1 ) // Skip_Value )
   DATA        = "..."
   CutCount    = -1
   TimesPasted = 0
   Do ZDest = ZDest2 to ZDest1 by -Skip_Value
      "ISREDIT LINE_AFTER &ZDEST = (DATA)"
      ii = ZDest + 1
      "ISREDIT LABEL" ii "= .HERE"
      If CutCount = -1 then do
         "ISREDIT LINE_AFTER" ii "= (DATA)"
         jj = ii + 1
         "ISREDIT LABEL" jj "= .THERE"
      End
      Address ISREDIT "PASTE AFTER .HERE KEEP"
      If RC = 12 then do
         zedsmsg = 'No "CUT" data'
         zedlmsg = 'There is no data to "PASTE" from the ISPF' ,
                   'Default Clipboard.'
         Address ISPEXEC "SETMSG MSG(ISRZ001)"  /* msg - with alarm */
         Exit 12
      End
      TimesPasted = TimesPasted + 1
      If CutCount = -1 then do
         "ISREDIT (ii) = LINENUM .HERE"
         "ISREDIT (jj) = LINENUM .THERE"
         CutCount = jj - ii - 1
         "ISREDIT DELETE .THERE .THERE"
      End
      "ISREDIT DELETE .HERE .HERE"
   End

   zedsmsg = CutCount 'lines pasted' TimesPasted 'times'
   zedlmsg = CutCount 'lines were pasted into the current file',
      TimesPasted 'times'
   'SETMSG MSG(ISRZ000)'
   'ISREDIT CURSOR =' ZDest1 0
   Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
   Say
   Say ' PA will place cut lines from the PROFILE pool after           '
   Say ' one or more lines in the current file.                        '
   Say
   Say '     PA  ( Skip#Lines )                                        '
   Say
   Say ' Enter PA on the COMMAND line and use C or CC range line       '
   Say ' command to specify where the lines are to be pasted. A number '
   Say ' of lines to skip may also be specified, 1 is the default.     '
   Say
   Exit(0)
