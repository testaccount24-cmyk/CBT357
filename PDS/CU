/**  REXX -- CREATE CLEAN UP JCL AFTER CURSOR POSITION  **/
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'CU EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS) NOPROCESS"
      PARMS = TRANSLATE(PARMS)
      IF PARMS = "?" THEN SIGNAL DISPDOC
   END
   ELSE DO
      SAY 'CU EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END
   IF DATATYPE(PARMS,'W') THEN NUMDELS = PARMS
                          ELSE NUMDELS = 1

   ADDRESS ISREDIT
   "(LINE,COL) = CURSOR"
   IF LINE = 1  THEN DO
       ADDRESS ISPEXEC "VGET (ZSCREENC,ZSCREENI) SHARED"
       II = INDEX(ZSCREENI,'* Top of Data *')
       IF ( ZSCREENC % 80 )  <=  ( II % 80 )  THEN LINE = 0
       ELSE LINE = 1
   END
   DDNUM = NUMDELS
   CARD1 = "//***"
   "LINE_AFTER (LINE) = (CARD1)"
   CARD2 = "//             DISP=(MOD,DELETE,DELETE),",
      || "UNIT=SYSDA,SPACE=(TRK,0)"
   CARD1 = "//DEL001       DD       DSN=...,"
   DO II = 1 TO NUMDELS
      "LINE_AFTER (LINE) = (CARD2)"
      PATCH = RIGHT(DDNUM,3,'0')
      DDNUM = DDNUM - 1
      CARD1 = OVERLAY(PATCH,CARD1,6,3)
      "LINE_AFTER (LINE) = (CARD1)"
   END II
   CARD1 = "//CLEANUP    EXEC     PGM=IEFBR14"
   "LINE_AFTER (LINE) = (CARD1)"
   CARD1 = "//***"
   "LINE_AFTER (LINE) = (CARD1)"
   LINE = LINE + 3
   COL  = 29
   "CURSOR = " LINE COL
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "CU  -  COPY CLEAN UP JCL AFTER CURSOR POSITION            "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    CU  ( ### ) ( ? )                                     "
   SAY
   SAY "       CREATE AN IEFBR14 WITH ### MOD/DELETE CARDS AFTER  "
   SAY "       THE CURSOR POSITION. ### DEFAULTS TO 1.            "
   SAY
   SAY "       A QUESTION MARK ALONE CAUSES THIS HELP TO DISPLAY. "
   SAY
   EXIT(1)

