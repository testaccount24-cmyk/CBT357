MACRO 5 PATTERN PATLEN TARGET TRGLEN POSN MAXPAT 64 MAXTRG 4096
************************************************************************
*                                                                      *
*  MACRO NAME: STRSTCH - Fast string search using the Boyer-Moore      *
*                        algorithm                                     *
*                                                                      *
*  AUTHOR: Russ Jones                                                  *
*          HCA Wesley Medical Center                                   *
*          550 N Hillside                                              *
*          Wichita KS 67212                                            *
*                                                                      *
*  DATE WRITTEN: 06/15/87                                              *
*                                                                      *
*  REFERENCE: Robert S. Boyer and J. Strother Moore                    *
*             "A Fast String Searching Algorithm"                      *
*             Communications of the ACM, vol. 20, No. 10               *
*             (Oct 1977):762-772                                       *
*                                                                      *
*             Richard Wiggins and Paul Wolberg                         *
*             "Searching for Strings with Boyer-Moore"                 *
*             Computer Language, vol. 3, No. 11                        *
*             (Nov 1986):28-42                                         *
*                                                                      *
************************************************************************
DEFINE SS_TARGET_         W     &MAXTRG A
  DEFINE SS_TRG_CHAR_      SS_TARGET_ 1 A OCCURS &MAXTRG
DEFINE SS_TRGLEN_         W           4 P
DEFINE SS_PATTERN_        W     &MAXPAT A
DEFINE   SS_PAT_CHAR_     SS_PATTERN_ 1 A OCCURS &MAXPAT
DEFINE SS_PATLEN_         W           4 P
DEFINE SS_DELTA_          W           4 P OCCURS 256
DEFINE SS_SUB_            W           2 B VALUE 0
DEFINE   SS_SUB_C_        SS_SUB_  +1 1 A
DEFINE SS_I_              W           4 P
DEFINE SS_OLDI_           W           4 P
DEFINE SS_J_              W           4 P
DEFINE SS_DL_             W           4 P
DEFINE SS_IDL_            W           4 P
DEFINE SS_LARGE_          W           4 P VALUE 1000000
DEFINE SS_PREV_PAT_       W     &MAXPAT A
*
SS_TARGET_ = &TARGET
SS_TRGLEN_ = &TRGLEN
SS_PATTERN_ = &PATTERN
SS_PATLEN_ = &PATLEN
IF SS_PATTERN_ NE SS_PREV_PAT_
  SS_PREV_PAT_ = SS_PATTERN_
  SS_I_ = 1
  DO WHILE SS_I_ LE 256
    SS_DELTA_ (SS_I_) = SS_PATLEN_
    SS_I_ = SS_I_ + 1
  END-DO
  SS_I_ = 1
  DO WHILE SS_I_ LE SS_PATLEN_
    SS_SUB_ = 0
    SS_SUB_C_ = SS_PAT_CHAR_ (SS_I_)
    SS_SUB_ = SS_SUB_ + 1
    IF SS_I_ LT SS_PATLEN_
      SS_DELTA_ (SS_SUB_) = SS_PATLEN_ - SS_I_
    ELSE
      SS_DELTA_ (SS_SUB_) = SS_LARGE_
    END-IF
    SS_I_ = SS_I_ + 1
  END-DO
END-IF
IF SS_PATLEN_ GT SS_TRGLEN_
  &POSN = 0
  GO TO SS_EXIT_
END-IF
SS_I_ = SS_PATLEN_
SS_FAST_
SS_SUB_ = 0
SS_SUB_C_ = SS_TRG_CHAR_ (SS_I_)
SS_SUB_ = SS_SUB_ + 1
SS_I_ = SS_I_ + SS_DELTA_ (SS_SUB_)
IF SS_I_ LE SS_TRGLEN_
  GO TO SS_FAST_
END-IF
IF SS_I_ LE SS_LARGE_
  &POSN = 0
  GO TO SS_EXIT_
END-IF
SS_I_ = SS_I_ - SS_LARGE_ - 1
SS_OLDI_ = SS_I_ + 1
SS_J_ = SS_PATLEN_ - 1
SS_SLOW_
IF SS_J_ = 0
  &POSN = SS_I_ + 1
  GO TO SS_EXIT_
END-IF
IF SS_TRG_CHAR_ (SS_I_) = SS_PAT_CHAR_ (SS_J_)
  SS_I_ = SS_I_ - 1
  SS_J_ = SS_J_ - 1
  GO TO SS_SLOW_
END-IF
SS_SUB_ = 0
SS_SUB_C_ = SS_TRG_CHAR_ (SS_I_)
SS_SUB_ = SS_SUB_ + 1
SS_DL_ = SS_DELTA_ (SS_SUB_)
SS_IDL_ = SS_I_ + SS_DL_
IF (SS_DL_ NE SS_LARGE_) AND (SS_IDL_ GT SS_OLDI_)
  SS_I_ = SS_I_ + SS_DL_
ELSE
  SS_I_ = SS_OLDI_ + 2
END-IF
IF SS_I_ LE SS_TRGLEN_
  GO TO SS_FAST_
ELSE
  &POSN = 0
END-IF
SS_EXIT_
