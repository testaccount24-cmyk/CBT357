/** REXX - Put lines from the user profile after the cursor **/

   Address ISPEXEC
   'ISREDIT MACRO (PARM1)   PROCESS'
   Parm1 = translate(Parm1)
   If Parm1 = '?' then signal DispDoc

   'ISREDIT (Line,Col) = CURSOR'
   'ISREDIT (ZDest)    = CURSOR'
   If ZDest = 1  then do
       'VGET (ZSCREENC,ZSCREENI) SHARED'
       ii = INDEX(ZScreenI,'* Top of Data *')
       If ( ZScreenC % 80 )  <=  ( ii % 80 )  then ZDest = 0
       Else ZDest = 1
   End

   'VGET (CUTCNT) PROFILE'
   If rc Â¬= 0 | CutCnt <= 0 then do
       zedsmsg = 'Use CUTX before PASTY'
       zedlmsg = 'Use CUTX before attempting to use PASTY'
       'SETMSG MSG(ISRZ001)'
       Exit(12)
   End

   TruncCnt = 0
   CutCount = CutCnt
   Do i = CutCnt to 1 by -1
       'VGET (CL'i') PROFILE'
       'ISREDIT LINE_AFTER' ZDest '= DATALINE (CL'i')'
       If rc = 4 then TruncCnt = TruncCnt + 1
   End

   If TruncCnt > 0 then do
       zedsmsg = TruncCnt 'lines truncated'
       msg     = 'Current record length shorter than origin'
       zedlmsg = msg '-' TruncCnt 'of' CutCount 'records truncated'
       'SETMSG MSG(ISRZ001)'
   End
   Else do
       zedsmsg = CutCount 'lines pasted'
       zedlmsg = CutCount 'lines were pasted into the current file'
       'SETMSG MSG(ISRZ000)'
   End

   If Line = 0 & Col = 0 then Exit(1)
   Else do
      'ISREDIT CURSOR = ' Line Col
      Exit
   End

DispDoc:
   ADDRESS TSO "CLEAR"
   Say
   Say ' PASTY will insert previously cut lines from the PROFILE pool '
   Say ' into the current file after the cursor position.             '
   Say ' would have been cut with the CUTX macro.                     '
   Say
   Say '     PASTY                                                    '
   Say
   Say ' Enter PASTY on the COMMAND line and position the cursor on   '
   Say ' the line after which the cut data is to be inserted. Assign  '
   Say ' PASTY to a PF Key and you can just position the cursor and   '
   Say ' press the PF Key, as often as desired and across files.      '
   Say
   Exit(1)
