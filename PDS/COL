/************************* REXX ***********************************
 *
 * THIS REXX WAS WRITTEN SO THAT YOU CAN TYPE COLS ON THE COMMAND
 * LINE IN AN EDIT SESSION JUST LIKE YOU DO IN A BROWSE SESSION,
 * INSTEAD OF HAVING TO PUT COLS ON A LINE NUMBER.
 * WRITTEN 02/1993 BY JEFFREY A. RICE...
 *
 ******************************************************************/
TRACE O
DEFINE = MSG("OFF")
ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
ADDRESS ISREDIT
"MACRO"
"(MACLVL) = MACRO_LEVEL"
IF MACLVL  =  001  THEN DO
   COUNTER = 1
   "(WIDTH) = DATA_WIDTH"
/*
 * CHECK FOR NUMBER ON COB MODE  -  IF SO THEN NUMBERS ARE TO BE
 * REDUCED BY 6 BECAUSE THE EDITOR IGNORES COLUMNS 1-6
 */
   "(NUM1,NUM2) = NUMBER"
   IF NUM1  =  "ON"  &  POS(" COBOL ",NUM2)  >  0  THEN DO
      COUNTER = COUNTER + 6
      WIDTH = WIDTH + 6
   END
/*
 * BUILD A COLUMNS LINE FOR THE WIDTH OF THE CURRENT EDIT SESSION.
 */
   COLUMNS = ""
   DO WHILE COUNTER <= WIDTH
      IF RIGHT(COUNTER,1) = "0" THEN DO
         COLUMNS = COLUMNS""LEFT(RIGHT(COUNTER,2),1)
      END
      ELSE DO
         IF RIGHT(COUNTER,1) = "5" THEN
            COLUMNS = COLUMNS"+"
         ELSE
            COLUMNS = COLUMNS"-"
      END
      COUNTER = COUNTER + 1
   END
/*
 * DISPLAY COLUMNS LINE BUILT
 */
   "LINE_BEFORE .ZCSR = INFOLINE '"COLUMNS"'"
   "UP 1"
END
ELSE DO
   ZEDLMSG = "%COLS CAN ONLY BE EXECUTED AS AN EDIT MACRO FROM",
             "AN EDIT SESSION."
   ADDRESS ISPEXEC "SETMSG MSG(ISRZ000)"
END
EXIT

