//-YOUR-USERID-D JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//A          EXEC     PGM=ASMA90,REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=SYS1.MACLIB
//             DD       DISP=SHR,DSN=-YOUR-SYSLIB-
DYNA     TITLE 'DYNAMICALLY ALLOCATE A DSN USING DDNAME AND SHR'
**********************************************************************
*  INPUT PARAMETERS ARE:
*    DATA SET NAME       44 BYTES - ALPHANUMERIC  POINTED TO IN R2
*    MEMBER               8 BYTES - ALPHANUMERIC                R3
*    DDNAME               8 BYTES - ALPHANUMERIC                R4
*    DYNALLOC R15 R/C     2 BYTES - BINARY                      R5
*    DYNALLOC RETURN CODE 2 BYTES - BINARY                      R6
*    DYNALLOC REASON CODE 2 BYTES - BINARY                      R7
**********************************************************************
         COPY  MACSDYNA
DYNA     RENTER
         LM    R2,R7,0(R1)
         ST    R2,DSADDR          DSNAME ADDR FOR ALLOC MACRO
         ST    R3,MNADDR          MEMBER ADDR
         ST    R4,DDADDR          DDNAME ADDR

         CLC   0(8,R3),BLANKS
         BE    NONMEM
         ALLOC DISP=SHR,DSN=DSADDR,MEMBER=MNADDR,DDN=DDADDR
         B     CHECKOUT
NONMEM   ALLOC DISP=SHR,DSN=DSADDR,DDN=DDADDR

         DROP  R14
         USING S99RB,R14
CHECKOUT STH   R15,0(R5)          SEND BACK DYNALLOC R15 VALUE
         MVC   0(2,R6),S99ERROR   SEND ERROR REASON CODE BACK
         MVC   0(2,R7),S99INFO    SEND INFO  REASON CODE BACK
         REXIT

**       ALLOC MACRO PARAMETERS

DSADDR   DS    A
DSLEN    DC    H'44'              LENGTH  OF DSNAME (44)
MNADDR   DS    A
MNLEN    DC    H'8'               LENGTH  OF MEMBER (08)
DDADDR   DS    A
DDLEN    DC    H'8'               LENGTH  OF DDNAME (08)

BLANKS   DC    CL08'        '
         LTORG
         DYNSPACE
         IEFZB4D0
         IEFZB4D2
         END
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*,
//             DCB=(RECFM=FM,LRECL=121,BLKSIZE=121)
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(2960,(111,12)),DCB=BLKSIZE=2960
//***
//L          EXEC     PGM=IEWL,
//             PARM='XREF,LIST,LET,DCBS',
//             REGION=4M
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,
//             DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   99070900
  ENTRY    DYNA
  NAME     DYNA(R)
//***
