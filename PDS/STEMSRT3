/* REXX */
NN    = 50000
A.0   = NN
XX    = RANDOM(1,99999,13579)
DO II = 1 TO NN
   A.II = RIGHT(RANDOM(1,99999),5,'0') || COPIES(" ",35)
END II

   START_CPU = TIME('R')
   START_CPU = SYSVAR(SYSCPU)

CALL SORTSRT

   ENDING_CPU = SYSVAR(SYSCPU) - START_CPU
   SAY '** ELAPSED TIME IS' TIME('E') 'AND CPU TIME IS' ENDING_CPU

   DO II = 1 TO 10
      SAY A.II
   END II
   DO II = 24996 TO 25005
      SAY A.II
   END II
   DO II = 49991 TO 50000
      SAY A.II
   END II

EXIT(0)

SORTSRT:
   ADDRESS TSO
   SAVEMSG = MSG("OFF")
   "FREE  DD(SORTIN SORTOUT SYSIN SYSPRINT) ATTR(FB40)"
   "DEL '-YOUR-USERID-.STEMSRT3.DATA'"
   "ATTR FB40 RECFM(F B) LRECL(40) BLKSIZE(27960)"
   "ALLOC DSN('-YOUR-USERID-.STEMSRT3.DATA') DD(SORTIN) NEW SP(10 10) CYL",
      "USING(FB40) UNIT(SYSDA)"
   "EXECIO 0 DISKW SORTIN (OPEN"
   "EXECIO * DISKW SORTIN(STEM A. FINIS"
   "ALLOC DSN('-YOUR-USERID-.STEMSRT3.DATA') DD(SORTOUT) SHR USING(FB40)"
   "ALLOC DD(SYSPRINT) DSN(*)"
   "ALLOC DD(SYSIN) NEW TRACKS SPACE(1) UNIT(SYSDA)",
        "LRECL(80) BLKSIZE(800) RECFM(F B)"
   QUEUE " SORT FIELDS=(01,40,CH,A)"
   "EXECIO" QUEUED() "DISKW SYSIN (FINIS"
   "CALL *(SORT)"
   "EXECIO 0 DISKR SORTIN (OPEN"
   "EXECIO * DISKR SORTIN(STEM A. FINIS"
   "FREE  DD(SORTIN SORTOUT SYSIN SYSPRINT) ATTR(FB40)"
   "DEL '-YOUR-USERID-.STEMSRT3.DATA'"
   RETURN

