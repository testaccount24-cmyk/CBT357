/**  REXX -- REXX EDIT MACRO TO ENCRYPT/DECRYPT FILES **/
    address ISREDIT
    "MACRO (pswd)"
    if pswd = "" then signal DispDoc
    "(STATUS) = USER_STATE"
    "(lrecl) = LRECL"
    "AUTOSAVE OFF"
    "CAPS OFF"
    brkr = 0
    do i = 1 to length(pswd)
       brkr = brkr + index(xrange('00'x,'ff'x),substr(pswd,i,1))
    end i
    ovly = 'AAABACADAEAFA0A1A2A3A4A5A6A7A8A9'x,
        || 'BABBBCBDBEBFB0B1B2B3B4B5B6B7B8B9'x,
        || 'CACBCCCDCECFC0C1C2C3C4C5C6C7C8C9'x,
        || 'DADBDCDDDEDFD0D1D2D3D4D5D6D7D8D9'x,
        || 'EAEBECEDEEEFE0E1E2E3E4E5E6E7E8E9'x,
        || 'FAFBFCFDFEFFF0F1F2F3F4F5F6F7F8F9'x,
        || '0A0B0C0D0E0F00010203040506070809'x,
        || '1A1B1C1D1E1F10111213141516171819'x,
        || '2A2B2C2D2E2F20212223242526272829'x,
        || '3A3B3C3D3E3F30313233343536373839'x,
        || '4A4B4C4D4E4F40414243444546474849'x,
        || '5A5B5C5D5E5F50515253545556575859'x,
        || '6A6B6C6D6E6F60616263646566676869'x,
        || '7A7B7C7D7E7F70717273747576777879'x,
        || '8A8B8C8D8E8F80818283848586878889'x,
        || '9A9B9C9D9E9F90919293949596979899'x
    "(n) = LINENUM .ZLAST"
    offs = random(1,256,brkr)
    do i = 1 to n
       "(x) = LINE" i
       linesz = length(x)
       ovly = substr(ovly,offs) || substr(ovly,1,offs-1)
       tpswd  = substr(copies(pswd,length(pswd) % linesz + 1),1,linesz)
       tovly  = substr(copies(ovly,length(ovly) % linesz + 1),1,linesz)
       x      = bitxor(bitxor(x,tovly),tpswd)
       "LINE "i" = (x)"
       offs = random(1,256)
    end i
    "USER_STATE = (STATUS)"
    exit 0

DispDoc:
   ADDRESS TSO "CLEAR"
   SAY "ENC  -  Encrypt & Decrypt entire file of any RECFM     "
   SAY
   SAY "  ENC   password                                       "
   SAY
   SAY "  Passwords can be any length. When first invoked,     "
   SAY "  the password encrypts the file. When re-invoked      "
   SAY "  using the same password, the file is decrypted.      "
   SAY
   SAY "  Caution should be used to remember the password      "
   SAY "  since it may be difficult to recreate the file       "
   SAY "  without the correct password.                        "
   SAY
   EXIT(1)
