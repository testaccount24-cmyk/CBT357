/**  REXX -- ALIGN COLUMNS BASED ON ONE OR MORE CHARACTERS          **/

   ADDRESS ISREDIT
   "MACRO (PARM) NOPROCESS"
   IF PARM = "" | PARM = "?" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      TOP = 1
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END
   IF SUBSTR(PARM,1,1) = '"' | SUBSTR(PARM,1,1) = "'",
      THEN PARM = TRANSLATE(SUBSTR(PARM,2,LENGTH(PARM)-2))
      ELSE PARM = TRANSLATE(PARM)

   OFFSET     = 0
   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      II = POS(PARM,REC)
      IF II > OFFSET & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D",
         THEN OFFSET = II
   END NN

   NEG_OFFSET = 99999
   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      II = POS(PARM,REC)
      IF II > 0 & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D" THEN DO
         REC = SUBSTR(REC,1,II-1) || COPIES(" ",OFFSET-II),
            || PARM || SUBSTR(REC,II+LENGTH(PARM))
         "LINE " NN " = (REC)"
         II = POS(PARM,REC)
         DO JJ = (II-1) TO 1 BY -1 WHILE(SUBSTR(REC,JJ,1) = " ")
         END JJ
         IF NEG_OFFSET > (II-JJ-1) THEN NEG_OFFSET = (II-JJ-1)
      END
   END NN

   DO II = 1 TO LENGTH(PARM) WHILE(SUBSTR(PARM,II,1) = " ")
   END II
   IF II > 1 THEN NEG_OFFSET = NEG_OFFSET + II - 1

   IF NEG_OFFSET > 0 THEN NEG_OFFSET = NEG_OFFSET - 1
   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      II = POS(PARM,REC)
      IF II > 0 & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D" THEN DO
         REC = SUBSTR(REC,1,II-1-NEG_OFFSET) || SUBSTR(REC,II)
         "LINE " NN " = (REC)"
      END
   END NN

   "LOCATE" TOP
   RETURN(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "ALIGNS - ALIGN COLUMNS                                    "
   SAY
   SAY " FORMAT "
   SAY "    ALIGNS  CHAR                                          "
   SAY "       WILL ALIGN THE SPECIFIED LINES AT THE CHARACTER    "
   SAY "       OR CHARACTERS PROVIDED.  A RANGE OF LINES MAY BE   "
   SAY "       SPECIFIED BY C/CC/C## OR THE ENTIRE FILE WILL BE   "
   SAY "       PROCESSED.  NO CHAR SPECIFIED OR ONLY A ? WILL     "
   SAY "       CAUSE THIS HELP TO BE SHOWN. QUOTES MAY BE USED.   "
   SAY
   SAY " EXAMPLE "
   SAY "    ALIGNS  ' TO '                                        "
   SAY "       ON THE FOLLOWING:                                  "
   SAY "          MOVE 7 TO WS-SHORT-FIELD                        "
   SAY "          MOVE     16    TO WS-MIDDLE-FIELD               "
   SAY "          MOVE     427 TO WS-REAL-BIG-FIELD               "
   SAY "       WILL ALIGN AS:                                     "
   SAY "          MOVE 7       TO  WS-SHORT-FIELD                 "
   SAY "          MOVE     16  TO  WS-MIDDLE-FIELD                "
   SAY "          MOVE     427 TO  WS-REAL-BIG-FIELD              "
   EXIT(1)

