/**  REXX -- MOVE COLUMNS ONTO OTHER COLUMNS DELETING OLD COLUMNS  **/
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY MOVECOLD 'MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC=0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (FRMLEFT,FRMRIGHT,TOLEFT,LBL1,LBL2)"
      IF RC=0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY 'MOVECOLD EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT',
          'WITH NO PENDING EDIT COMMANDS!'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG = ''
   IF FRMLEFT = ''  |  FRMRIGHT = ''  |  TOLEFT = ''  THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMLEFT,'W')  |  FRMLEFT = 0  THEN DO
      MESG = "'"FRMLEFT"' VALUE OF FROM-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMRIGHT,'W')  |  FRMRIGHT = 0  THEN DO
      MESG = "'"FRMRIGHT"' VALUE OF FROM-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF FRMLEFT > FRMRIGHT THEN DO
      MESG = "FROM-LEFT-COL "FRMLEFT" IS GREATER THAN FROM-RIGHT-COL " +
         FRMRIGHT
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TOLEFT,'W')  |  TOLEFT = 0  THEN DO
      MESG = "'"TOLEFT"' VALUE OF TO-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   LEN = FRMRIGHT - FRMLEFT + 1
   TORIGHT = TOLEFT + LEN - 1
   IF TOLEFT > TORIGHT THEN DO
      MESG = "TO-LEFT-COL "TOLEFT" IS GREATER THAN TO-RIGHT-COL "TORIGHT
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   IF TORIGHT > LRECL THEN DO
      MESG="TO-RIGHT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
   END
   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
       END
   END
   "ISREDIT ("LRECL") = LRECL "
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR     "
   "ISREDIT ("ANUM") = AUTONUM              "
   "ISREDIT (END) = LINENUM .ZLAST"
   START = 1
   IF LBL1 <> '' THEN
       "ISREDIT (START) = LINENUM &LBL1"

   IF LBL2 <> '' THEN
        "ISREDIT (END) = LINENUM &LBL2"
   ELSE
        LBL2 = LBL1

    IF LBL1 > LBL2 THEN DO
       WST   = START
       START = END
       END   = WST
    END

   DO PNTR =  START  TO  END  BY 1
      "ISREDIT (RECORD) = LINE &PNTR"
      IF TOLEFT > FRMRIGHT THEN DO
         RECORD = SUBSTR(RECORD,1,FRMLEFT-1),
            || SUBSTR(RECORD,FRMRIGHT+1,TOLEFT-FRMRIGHT-1),
            || SUBSTR(RECORD,FRMLEFT,LEN),
            || SUBSTR(RECORD,TOLEFT+LEN)
      END
      ELSE DO
         RECORD = SUBSTR(RECORD,1,TOLEFT-1),
            || SUBSTR(RECORD,FRMLEFT,LEN),
            || SUBSTR(RECORD,TOLEFT+LEN,FRMLEFT-TOLEFT-LEN),
            || SUBSTR(RECORD,FRMRIGHT+1)
      END
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END
   "ISREDIT LOCATE" START
   "ISREDIT AUTONUM =" ANUM
    SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   SIGNAL DISPDOC

EOJ:
   X=MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "MOVECOLD - MOVE COLUMNS ONTO OTHER DATA DELETING FROM COLUMNS"
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY
   SAY "    MOVECOLD FROM-LEFT-COL FROM-RIGHT-COL TO-LEFT-COL (.A .B)"
   SAY
   SAY "       COLUMN PARAMETERS ARE REQUIRED. LABEL RANGE IS OPTIONAL"
   SAY
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    MOVECOLD  1 3  11  .A .B                   "
   SAY
   SAY "       WILL MOVE THE CONTENTS OF COLUMNS 1     "
   SAY "       THRU 3 TO COLUMNS 11 THRU 13 DELETING   "
   SAY "       1 THRU 3 IN THE LABEL RANGE .A THRU .B  "
   SAY
   EXIT(1)
