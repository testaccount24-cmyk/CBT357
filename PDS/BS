/**  REXX - DISPLAY MAX & HALF TRACK BLOCK SIZE FOR A GIVEN LRECL  **/
   NUMERIC DIGITS 20
   SAVEMSG = MSG("OFF")
   CALLED = SYSVAR(SYSNEST)
   X = ADDRESS()
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT "ISREDIT MACRO ("LRECL")"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      UMODE = 'ISPEXEC'
      ARG LRECL
   END
   ELSE DO
      ADDRESS ISREDIT "ISREDIT ("LIN","COL") = CURSOR"
   END
   IF LRECL = '?' THEN SIGNAL DISPDOC

 CHECK:
   DO WHILE LRECL = ''
      SAY "PLEASE ENTER LRECL:"
      PULL LRECL
      SAVMSG = ''
   END
   IF DATATYPE(LRECL)<>'NUM' | LRECL < 1 | LRECL > 32760 THEN DO
      SAY "'"LRECL"' IS INVALID "
      LRECL = ''
      SIGNAL CHECK
   END
   NUMRECS = 32760%LRECL
   MAXSZ   = NUMRECS*LRECL
   NUMRECS = 27998%LRECL
   OPTSZ   = NUMRECS*LRECL
   NUMRECS = 262144%LRECL
   SUPERSZ = NUMRECS*LRECL
   ANSW    = "LRECL "LRECL", MAX BLKSZ=",
             MAXSZ", OPT 3390 BLKSZ="OPTSZ,
             "& LARGE BLKSZ="SUPERSZ
   IF UMODE = 'ISREDIT' THEN
      ADDRESS ISREDIT "ISREDIT LINE_AFTER "LIN" = MSGLINE '"ANSW"'"
   ELSE
      SAY ANSW
   EXIT  0

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "BS -- CALCULATE OPTIMAL BLOCK SIZE FOR A GIVEN LRECL   "
   SAY
   SAY "  BS  ( RECORD-LENGTH )                                "
   SAY
   SAY "  IF NOT PROVIED ON THE INITIAL CALL, THE RECORD       "
   SAY "  LENGTH WILL BE REQUESTED FROM THE USER.              "
   SAY
   SAY "  THE OPTIMAL 3390 BLOCK IS BASED ON 27,998 HALF       "
   SAY "  TRACK BLOCKING.                                      "
   SAY
   SAY "  THE CALCULATED BLOCK SIZES ARE BASED ON 32,760 AS    "
   SAY "  THE NORMAL MAX SIZE AND 262,144 FOR THE NEW LARGE    "
   SAY "  BLOCK SIZE (256K IS THE NORM DEFAULT).               "
   SAY
   EXIT(1)
