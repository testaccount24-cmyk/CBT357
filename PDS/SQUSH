/**  REXX -- LEFT JUSTIFIES DATA AND TURNS MULTIPLE BLANKS INTO ONE **/

   ADDRESS ISREDIT
   "MACRO (LEFT,RIGHT) NOPROCESS"

   IF LEFT = "?" THEN SIGNAL DISPDOC
   IF LEFT <> "" AND RIGHT = "" THEN DO
      "ISREDIT (L2,RIGHT) = BOUNDS"
      END

   "PROCESS RANGE C"
   IF RC = 0 THEN DO
      "(FIRST) = LINENUM .ZFRANGE"
      "(LAST)  = LINENUM .ZLRANGE"
   END
   ELSE DO
      "(FIRST) = LINENUM .ZFIRST"
      "(LAST)  = LINENUM .ZLAST"
   END

   "ISREDIT ("LB","RB") = BOUNDS"
   IF LEFT  <> "" THEN LB = LEFT
   IF RIGHT <> "" THEN RB = RIGHT
   "ISREDIT ("LRECL")   = LRECL"

   DO II = FIRST TO LAST
      "(LINE) = LINE " II
      IF LB = 1 & RB = LRECL THEN DO
         SELECT
            WHEN WORDS(LINE) = 0 THEN NOP
            OTHERWISE LINE = SPACE(LINE)
         END  /*** END OF SELECT ***/
      END
      ELSE DO
         BLOCK = SUBSTR(LINE,LB,RB-LB+1)
         SELECT
            WHEN WORDS(BLOCK) = 0 THEN NOP
            OTHERWISE BLOCK = SPACE(BLOCK)
         END  /*** END OF SELECT ***/
         LINE = SUBSTR(LINE,1,LB-1) || BLOCK,
            || COPIES(' ',RB-LB+1-LENGTH(BLOCK)),
            || SUBSTR(LINE,RB+1)
      END
      "LINE "II"= (LINE)"
   END II

   "LOCATE" FIRST
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SQUSH - LEFT JUSTIFY AND REMOVE EXTRA BLANKS              "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    SQUSH  ( LEFT-COLUMN  RIGHT-COLUMN )                  "
   SAY
   SAY "       PROCESS ENTIRE FILE OR C/CC/C# DELIMITED RANGE AND "
   SAY "       LEFT JUSTIFY AND SQUEEZE OUT EXTRA BLANKS. EITHER  "
   SAY "       THE SPECIFIED COLUMNS OR THE CURRENT BOUNDS ARE    "
   SAY "       USED TO LIMIT PROCESSING.                          "
   SAY
   EXIT(1)

