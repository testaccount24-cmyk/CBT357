/**  REXX -- COPY A PDS MEMBER BEFORE OR AFTER EVERY OTHER MEMBER  **/
/*------------------------------------------------------------------*/
   SAVEMSG=MSG()
   X=MSG("OFF")
   ADDRESS 'ISPEXEC'
   'ISREDIT MACRO (MEMBER,LOCATION,PREFIX)'
   IF MEMBER = '' THEN SIGNAL DISPDOC
   'ISREDIT (DATA1)   = DATAID '
   'ISREDIT (THISONE) = MEMBER '
   PARSE UPPER VAR LOCATION LOCATION .
   PARSE UPPER VAR MEMBER   MEMBER   .
   IF LOCATION = '' | (LOCATION <> 'BEFORE' & LOCATION <> 'AFTER'),
      THEN DO
         PREFIX   = LOCATION
         LOCATION = 'BEFORE'
      END
   PARSE UPPER VAR PREFIX PREFIX .
   IF LOCATION = 'BEFORE',
      THEN DO
         ADDRESS 'ISREDIT' '(LINECNT) = LINENUM' .ZLAST
         ADDRESS 'ISREDIT' 'COPY' MEMBER 'BEFORE' .ZFIRST
         ADDRESS 'ISREDIT' '(LINENEW) = LINENUM' .ZLAST
         LINENEW = LINENEW - LINECNT
         ADDRESS 'ISREDIT' 'CREATE @#$@#$@# 1' LINENEW
         IF PREFIX = '' | SUBSTR(THISONE,1,LENGTH(PREFIX)) = PREFIX
            THEN NOP
            ELSE ADDRESS 'ISREDIT' 'DELETE 1' LINENEW
      END
      ELSE DO
         ADDRESS 'ISREDIT' '(LINECNT) = LINENUM' .ZLAST
         ADDRESS 'ISREDIT' 'COPY' MEMBER 'AFTER' .ZLAST
         ADDRESS 'ISREDIT' '(LINENEW) = LINENUM' .ZLAST
         LINECNT = LINECNT + 1
         ADDRESS 'ISREDIT' ' CREATE @#$@#$@#' LINECNT LINENEW
         IF PREFIX = '' | SUBSTR(THISONE,1,LENGTH(PREFIX)) = PREFIX
            THEN NOP
            ELSE ADDRESS 'ISREDIT' 'DELETE' LINECNT LINENEW
      END
   ADDRESS 'ISPEXEC' 'LMOPEN DATAID('DATA1') OPTION(INPUT)'
   MEMBER1 = ''
      DO UNTIL LMRC <> 0
        ADDRESS 'ISPEXEC' 'LMMLIST DATAID('DATA1') OPTION(LIST)',
                          'MEMBER(MEMBER1) STATS(YES)'
        LMRC = RC
     IF LMRC = 0              ,/* IF MEMBER NAME RETURNED           */
       & MEMBER1 <> THISONE   ,/*    AND IT ISN'T THIS MEMBER       */
       & MEMBER1 <> MEMBER    ,/*    AND IT ISN'T THE COPIED MEMBER */
       & MEMBER1 <> '@#$@#$@#',/*    AND IT ISN'T THE DUMMY MEMBER  */
       & (                    ,/*    AND PREFIX CHECK IS OK...      */
           PREFIX = ''        ,/*       AND NO PREFIX SPECIFIED     */
            | SUBSTR(MEMBER1,1,LENGTH(PREFIX))=PREFIX,/* | PREF MTCH*/
         ) THEN DO            /*  INVOKE EDIT WITH LOCATION MACRO   */
         ADDRESS 'ISPEXEC' 'CONTROL ERRORS CANCEL'
         IF LOCATION = 'BEFORE',
            THEN DO
               ADDRESS 'ISPEXEC' 'EDIT DATAID('DATA1')',
                       'MEMBER('MEMBER1') MACRO(ADDLINEB)'
            END
            ELSE DO
               ADDRESS 'ISPEXEC' 'EDIT DATAID('DATA1')',
                       'MEMBER('MEMBER1') MACRO(ADDLINEA)'
            END
         ADDRESS 'ISPEXEC' 'CONTROL ERRORS CANCEL'
       END
   END
   ADDRESS 'ISPEXEC' 'LMMLIST DATAID('DATA1') OPTION(FREE)'
   ADDRESS 'ISPEXEC' 'LMCLOSE DATAID('DATA1')'
   ADDRESS 'ISREDIT' '(DATASET) = DATASET'
   ADDRESS "TSO"     "DELETE '"DATASET"(@#$@#$@#)'"
   EXIT

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "ADDLINES - ADD A MEMBER TO ALL OTHER MEMBERS                "
   SAY
   SAY "    ADDLINES  MEMBER  ( LOCATION )  ( PREFIX )              "
   SAY
   SAY "       MEMBER IS THE NAME OF THE MEMBER TO BE COPIED INTO   "
   SAY "              THE OTHER MEMBERS.                            "
   SAY
   SAY "       LOCATION IS EITHER 'BEFORE' OR 'AFTER', INDICATING   "
   SAY "              WHETHER THE COPIED DATA WILL BE INSERTED      "
   SAY "              BEFORE THE MEMBER DATA OR APPENDED AT THE     "
   SAY "              END. 'BEFORE' IS THE DEFAULT.                 "
   SAY
   SAY "       PREFIX IS AN OPTIONAL PREFIX TO USE WHEN SELECTING   "
   SAY "              MEMBERS TO PROCESS.  FOR EXAMPLE, XX1 WILL    "
   SAY "              PROCESS ONLY THOSE MEMBERS BEGINNING W/ XX1.  "
   SAY
   EXIT(1)
