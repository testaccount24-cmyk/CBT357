/**  REXX -- COMPRESS THE PDS EDITED OR ONE SPECIFIED  **/

   ADDRESS 'ISPEXEC'
   "ISREDIT MACRO (DSN)"
   IF RC > 0 THEN EXIT(RC)
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR "
   SAVEMSG = MSG("OFF")

   UPPER DSN
   IF DSN = '?' THEN SIGNAL DISPDOC
   IF DSN = '' THEN DO
      "ISREDIT (DSN) = DATASET"
      DSN = "'" || DSN || "'"
   END

   ADDRESS TSO
   "FREE  DD(SYSUT1 SYSPRINT SYSIN)"
   "ALLOC DD(SYSUT1) DSN("DSN") SHR"
   "ALLOC DD(SYSPRINT) DUMMY"
   "ALLOC DD(SYSIN) NEW TRACKS SPACE(1) UNIT(SYSDA)",
        " LRECL(80) BLKSIZE(800) RECFM(F B)"
   QUEUE " COPY INDD=SYSUT1,OUTDD=SYSUT1"
   "EXECIO" QUEUED() "DISKW SYSIN (FINIS"
   "CALL *(IEBCOPY)"
   RETCODE = RC
   "FREE  DD(SYSUT1 SYSPRINT SYSIN)"
   "ALLOC DD(SYSPRINT) DSN(*)"

   STR = 'COMPRESSED RC='RETCODE
   CALL SETMSG '* NO ' STR ' DONE $PDS' DSN 'COMPRESSED...'
   "ISREDIT LOCATE" SAVLINE
   "ISREDIT CURSOR = "SAVLINE SAVCOL
   IF SAVEMSG = 'ON' THEN
      X = MSG("ON")
   RETURN RETCODE

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN


DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "COMPRESS - CURRENT PDS OR SPECIFIED ONE                   "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    COMPRESS  ( PDS-NAME )                                "
   SAY
   SAY "       INVOKE IEBCOPY COMPRESS AGAINST EITHER THE CURRENT "
   SAY "       (DEFAULTED WHEN NO PDS-NAME SPECIFIED) OR THE      "
   SAY "       DATA SET SPECIFIED.                                "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    COMPRESS  'USERID.TEST.MACLIB'                        "
   SAY
   SAY "       WILL COMPRESS 'USERID.TEST.MACLIB'                 "
   SAY
   EXIT(1)

