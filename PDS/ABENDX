//-YOUR-USERID-A JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//*** PROGRAM TO ABEND WITH A PARM DRIVEN RETURN CODE 0 TO 4095
//*** OR 0C1, 0C2, 0C3, 0C4, 0C6, 0C7, 0C8, 0C9, 0CB OR 806
//***    IF NO OR BAD PARM, ABEND WITH 4095
//***
//A          EXEC     PGM=ASMA90,REGION=4M,
//             PARM='OBJECT'
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*,
//             DCB=(RECFM=FM,LRECL=121,BLKSIZE=121)
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(2960,(111,12)),DCB=BLKSIZE=2960
//SYSLIB       DD       DISP=SHR,DSN=SYS1.MACLIB
         USING *,11
ABENDX   STM   14,12,12(13)
         LR    11,15
         LA    14,SAVEAREA
         ST    14,8(13)
         ST    13,4(14)
         LR    13,14
         L     3,0(1)           * GET PTR TO PARM
         LH    4,0(3)           * GET LENGTH OF PARM
         LTR   4,4
         BNZ   REALPARM
DEFAULT  LA    5,4095           * IF ZERO LEN, DEFAULT TO 4095 CODE
         B     DIE
REALPARM C     4,THREE          * CHECK IF S0C ABEND DESIRED
         BNE   NOTS0C
         CLC   2(3,3),STR0C1
         BE    S0C1
         CLC   2(3,3),STR0C2
         BE    S0C2
         CLC   2(3,3),STR0C3
         BE    S0C3
         CLC   2(3,3),STR0C4
         BE    S0C4
         CLC   2(3,3),STR0C6
         BE    S0C6
         CLC   2(3,3),STR0C7
         BE    S0C7
         CLC   2(3,3),STR0C8
         BE    S0C8
         CLC   2(3,3),STR0C9
         BE    S0C9
         CLC   2(3,3),STR0CB
         BE    S0CB
         CLC   2(3,3),STR806
         BE    S806
NOTS0C   BCTR  4,0
         STC   4,TRTINST+1      * CHECK FOR NUMERIC
TRTINST  TRT   2(1,3),NUMTBL
         BC    7,DEFAULT
         LA    5,112(4)
         STC   5,PACKINST+1
PACKINST PACK  DW,2(1,3)
         CVB   5,DW
         C     5,MAX            * CHECK FOR MAX (4095)
         BH    DEFAULT
DIE      ABEND (5)
S0C1     DC    H'0'
S0C2     DC    X'B20A0000'
S0C3     EX    0,*
S0C4     SR    15,15
         MVI   0(15),X'FF'
S0C6     LA    15,*
         LA    15,1(15)
         BR    15
S0C7     ZAP   *+6(3),BIGNUM
S0C8     L     15,MAXNUM
         A     15,MAXNUM
S0C9     SR    14,14
         LA    15,1
         SR    2,2
         DR    14,2
S0CB     DP    TO,BY
S806     LINK  EP=QWERTYUIO
DW       DS    D
SAVEAREA DS    9D
NUMTBL   DC    240X'FF',10X'00',6X'FF'
MAX      DC    F'4095'
THREE    DC    F'3'
BIGNUM   DC    F'1357902468'
MAXNUM   DC    X'7FFFFFFF'
STR0C1   DC    CL3'0C1'
STR0C2   DC    CL3'0C2'
STR0C3   DC    CL3'0C3'
STR0C4   DC    CL3'0C4'
STR0C6   DC    CL3'0C6'
STR0C7   DC    CL3'0C7'
STR0C8   DC    CL3'0C8'
STR0C9   DC    CL3'0C9'
STR0CB   DC    CL3'0CB'
STR806   DC    CL3'806'
TO       DC    PL3'99999'
BY       DC    PL1'0'
         END   ABENDX
//***
//L          EXEC     PGM=IEWL,
//             PARM='XREF,LIST,LET,DCBS',
//             REGION=4M
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,
//             DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   04031200
  NAME     ABENDX(R)
//***
