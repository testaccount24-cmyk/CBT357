/** REXX - SAVE command for VIEW **************************************/
/*                                                                    */
/* Provides the equivalent of the SAVE command in a View session.     */
/* It uses REPLACE to write the data and LMMSTATS to set the          */
/* statistics.                                                        */
/*                                                                    */
/**********************************************************************/
   ADDRESS 'ISPEXEC'
   ADDRESS ISPEXEC "ISREDIT MACRO (PARMS)"
   IF SUBSTR(PARMS,1,1) = '?' THEN SIGNAL DISPDOC
   IF RC > 0 THEN EXIT RC /* not an EDIT macro */
   "CONTROL ERRORS RETURN"
   "ISREDIT (zdsn) = DATASET"
   "ISREDIT (zmemb) = MEMBER"
   IF ZMEMB='' THEN CALL PROCESS_SEQUENTIAL_DATA_SET
/*------------------------------------------------------------------*/
/*     Fast-path processing when STATS=OFF                          */
/*------------------------------------------------------------------*/
   "ISREDIT (stats) = STATS"
   IF STATS='OFF' THEN DO
      "ISREDIT REPLACE" ZMEMB ".ZFIRST .ZLAST"
      CALL CHECK_RC RC,0,'REPLACE'
      "SETMSG MSG(ISRE016)"
      EXIT
   END
/*------------------------------------------------------------------*/
/*     Prevent LMMSTATS failure when VERSION=00                     */
/*------------------------------------------------------------------*/
   "ISREDIT (zlvers) = VERSION"
   IF ZLVERS = 0 THEN DO
      ZEDSMSG = "Invalid Version"
      ZEDLMSG = "Version 00 is not supported by the LMMSTATS service".
         "SETMSG MSG(ISRZ001)"
      EXIT 4             /* not supported in this release */
   END
/*------------------------------------------------------------------*/
/*     Retrieve current statistics from member's directory entry    */
/*------------------------------------------------------------------*/
   "ISREDIT (dataid) = DATAID"
   "LMOPEN DATAID("DATAID")"
   CALL CHECK_RC RC,0,'LMOPEN'
   "LMMFIND DATAID("DATAID") MEMBER("ZMEMB") STATS(YES)"
   LMMFIND_RC=RC
   CALL CHECK_RC RC,8,'LMMFIND'
/*------------------------------------------------------------------*/
/*     Rewrite member using the REPLACE command                     */
/*------------------------------------------------------------------*/
   "ISREDIT (n1,n2) = NUMBER"
   MODLINES=0 /* modified lines  */
   IF N1='ON' & LEFT(N2,1)='S' THEN DO  /* NUMBER ON STD   */
      "ISREDIT (ustat) = USER_STATE" /* push user_state */
      CALL COUNT_MODLINES /* Cound modified lines   */
      "ISREDIT REPLACE" ZMEMB ".ZF .ZL"
      XRC = RC
      "ISREDIT USER_STATE = (ustat)" /* pop user_state  */
      CALL CHECK_RC XRC,0,'REPLACE'
   END
   ELSE DO /* NUMBER OFF      */
      "ISREDIT REPLACE" ZMEMB ".ZF .ZL"
      CALL CHECK_RC RC,0,'REPLACE'
   END
/*------------------------------------------------------------------*/
/*     Update statistics using LMMSTATS                             */
/*------------------------------------------------------------------*/
   "ISREDIT (zlvers) = VERSION" /* NEW VERSION   */
   "ISREDIT (zlmod) = LEVEL" /* NEW LEVEL     */
   IF LMMFIND_RC=8 THEN /* new member    */
      "LMMSTATS DATAID("DATAID") MEMBER("ZMEMB")" "VERSION("ZLVERS") ,
      MODLEVEL("ZLMOD")" "MODRECS("MODLINES+0")"
   ELSE /* member existed before */
      "LMMSTATS DATAID("DATAID") MEMBER("ZMEMB")" "VERSION("ZLVERS") ,
      MODLEVEL("ZLMOD")" "CREATED("ZLCDATE") INITSIZE("ZLINORC")" ,
      "MODRECS("MODLINES+0")"
   CALL CHECK_RC RC,0,'LMMSTATS'
/*------------------------------------------------------------------*/
/*     Issue completion message and exit                            */
/*------------------------------------------------------------------*/
   "LMCLOSE DATAID("DATAID")"
   CALL CHECK_RC RC,0,'LMCLOSE'
   "SETMSG MSG(ISRE016)" /* member zmemb saved */
   EXIT
/*------------------------------------------------------------------*/
/*     Count_modlines internal function: Count modified lines       */
/*------------------------------------------------------------------*/
COUNT_MODLINES:
   "ISREDIT NUMBER OFF"
   "ISREDIT (recfm) = RECFM"
   IF RECFM='F' THEN
      "ISREDIT (lrecl) = LRECL"
   ELSE
      LRECL=8 /* RECFM=V        */
   "ISREDIT (modlines) = LINENUM .ZL" /* total number of lines*/
   "ISREDIT SEEK '00' ALL" LRECL-1 /*count unmodified lines*/
   IF RC=0 THEN
      DO
         "ISREDIT (c1,c2) = SEEK_COUNTS"
         MODLINES=MODLINES-C1 /* modified lines*/
      END
/*------------------------------------------------------------------*/
/*           Process a sequential data set                          */
/*------------------------------------------------------------------*/
PROCESS_SEQUENTIAL_DATA_SET:
   "ISREDIT REPLACE '"zdsn"'" .zfirst .zlast
   "SETMSG MSG(ISRE016)"
   EXIT
/*------------------------------------------------------------------*/
/*           Error routine                                          */
/*------------------------------------------------------------------*/
CHECK_RC:
   IF ARG(1) \> ARG(2) THEN RETURN /* RC=0 */
   "SETMSG MSG("ZERRMSG")"
   "LMCLOSE DATAID("DATAID")"
   EXIT 20
/*------------------------------------------------------------------*/
/*           Built-in Help                                          */
/*------------------------------------------------------------------*/
DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "VSAVE -- SAVES FILE/MEMBER FROM WITHIN VIEW            "
   SAY
   SAY "  VSAVE   ( ? )                                        "
   SAY
   SAY "  WILL SAVE EITHER A VIEWED MEMBER OR SEQUENTIAL FILE, "
   SAY "  WITHOUT HAVING TO DO A MANUAL REPLACE. THE ONLY      "
   SAY "  PARAMETER, A QUESTION MARK (?), CAUSES THIS VERY     "
   SAY "  MINIMAL HELP TO BE DISPLAYED.                        "
   SAY
   EXIT(1)
