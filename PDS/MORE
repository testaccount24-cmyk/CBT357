/**  REXX -- UNEXCLUDE LINES BEFORE/AFTER SHOWN LINE  **/
   ADDRESS "ISREDIT"
   "MACRO (PARMS) NOPROCESS"

   AFTER  = 1
   BEFORE = 1
   LABF   = ""
   LABL   = ""
   FIRST  = -1

   PARMS = TRANSLATE(PARMS)
   IF PARMS = '?' THEN SIGNAL DISPDOC
   DO II = 1 TO 10;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,6) = "AFTER(" THEN DO;
         PARSE VAR TOKEN  "AFTER(" AFTER ")" RIGHT
         ITERATE II;
      END
      IF "BEFORE(" = SUBSTR(TOKEN,1,7)  THEN DO;
         PARSE VAR TOKEN  "BEFORE(" BEFORE ")" RIGHT
         ITERATE II;
      END
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LABF = "" THEN LABF = TOKEN
         ELSE IF LABL = "" THEN LABL = TOKEN
         ELSE DO
            ZEDSMSG = ".LABEL RC=8"
            ZEDLMSG = "TOO MANY LABELS --" LABF LABL TOKEN
            ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
            EXIT 8
         END
      END
   END II
   IF LABF <> ""  &  LABL = ""  THEN LABL = LABF

   IF LABL = "" THEN DO
      "PROCESS RANGE C"
      IF RC = 0 THEN DO
         LABF = ".ZFRANGE"
         LABL = ".ZLRANGE"
      END
      ELSE DO
         LABF = ".ZFIRST"
         LABL = ".ZLAST"
      END
   END

   "(LABFNO) = LINENUM" LABF
   "(LABLNO) = LINENUM" LABL

   DO II = LABFNO TO LABLNO
      "(LINESTAT) = XSTATUS" II
      IF LINESTAT = "X" THEN DO
         RR = -1
         DO JJ = (II) TO (II+BEFORE-1)
            "(LINESTAT) = XSTATUS" JJ
            IF LINESTAT = "X" THEN DO
               "XSTATUS" JJ "= NX";
               IF FIRST = -1 THEN FIRST = JJ
            END
            ELSE DO
               RR = JJ
               JJ = II + BEFORE
            END
         END
         IF RR <> -1 THEN DO
            II = RR
         END
         ELSE DO
            RR = -1
            DO KK = JJ TO LABLNO
               "(LINESTAT) = XSTATUS" KK
               IF LINESTAT = "NX" THEN DO
                  RR = KK
                  KK = LABLNO + 1
               END
            END
            IF RR <> -1 THEN II = RR
                        ELSE II = LABLNO + 1
         END
      END
   END

   DO II = LABFNO TO LABLNO
      "(LINESTAT) = XSTATUS" II
      IF LINESTAT = "X" THEN DO
         RR = -1
         DO JJ = (II+1) TO LABLNO
            "(LINESTAT) = XSTATUS" JJ
            IF LINESTAT = "NX" THEN DO
               RR = JJ
               IF (JJ-II) >= AFTER THEN DO
                  DO KK = (JJ-1) TO (JJ-AFTER) BY -1
                     "XSTATUS" KK "= NX";
                  END
                  JJ = LABLNO + 1
               END
            END
         END
         IF RR = -1 THEN DO
            IF (JJ-II) >= AFTER THEN DO
               DO KK = (JJ-1) TO (JJ-AFTER) BY -1
                  "XSTATUS" KK "= NX";
               END
               II = LABLNO + 1
            END
         END
         ELSE DO
            II = RR
         END
      END
   END

   IF FIRST = -1 THEN DO
       "LOCATE" 0
   END
   ELSE DO
      IF FIRST > 1 THEN FIRST = FIRST - 1
      "(LINESTAT) = XSTATUS" FIRST
      "LOCATE" FIRST
      "XSTATUS" FIRST "=" LINESTAT
   END

   ZEDSMSG = "MORE" BEFORE"/"AFTER
   ZEDLMSG = "MORE COMPLETED BEFORE="BEFORE "AFTER="AFTER
   ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "MORE - UNEXCLUDE LINES BEFORE/AFTER CURRENTLY EXCLUDED LINES"
   SAY
   SAY " FORMAT "
   SAY
   SAY "    MORE    ( BEFORE(#) )   ( AFTER(#) )   ( .A .B )        "
   SAY
   SAY "       UNEXCLUDES (SHOWS) THE BEFORE AND AFTER NUMBER(S) OF "
   SAY "       LINES AROND CURRENTLY EXCLUDED LINES. LINE RANGE MAY "
   SAY "       BE SPECIFIED BY LABELS OR C/CC/C# PREFIX COMMENDS.   "
   SAY "       THE DEFAULT FOR BEFORE AND AFTER IS 1.               "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    MORE    BEFORE(2)  AFTER(0)                             "
   SAY
   SAY "       UNEXCLUDES 2 LINES BEFORE EACH EXCLUDED GROUP AND 0  "
   SAY "       LINES AFTER IN THE ENTIRE FILE.                      "
   SAY
   EXIT(1)

