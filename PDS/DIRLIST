//-YOUR-USERID-D JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//*** HEX LIST OF A DIRECTORY SEPARATING EACH MEMBER
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 DIRLIST:
      PROC OPTIONS(MAIN) REORDER;

      DCL  SELMEM       FILE INPUT STREAM
                        ENV(FB, RECSIZE(080), TOTAL);
      DCL  I            FILE INPUT RECORD SEQL
                        ENV(F, RECSIZE(256), BLKSIZE(256), TOTAL);
      DCL  O            FILE OUTPUT RECORD SEQL
                        ENV(VB, RECSIZE(076), TOTAL);

      DCL  1  DIR_BLK           BASED(DIR_BLK_PTR),
              2  LL             FIXED BIN(15,0),
              2  DATA           CHAR(254);
      DCL  1  MEM_OVLY          BASED(MEM_OVLY_PTR),
              2  MEMBER         CHAR(8),
              2  TTR            CHAR(3),
              2  C,
                 3  ALIAS       BIT(1),
                 3  TTR_CNT     BIT(2),
                 3  DATA_CNT    BIT(5),
              2  ORG_HWDS(39)   CHAR(2);
      DCL  DIR_DATA             CHAR(072) STATIC VARYING;
      DCL  DIR_DATA_OVLY        CHAR(100) BASED(MEM_OVLY_PTR);

      DCL  FIRST                FIXED BIN(31) STATIC INIT(00000001);
      DCL  LAST                 FIXED BIN(31) STATIC INIT(99999999);
      DCL  FIRSTMEM             CHAR(8) STATIC INIT(' ');
      DCL  LASTMEM              CHAR(8) STATIC INIT('99999999');

      DCL  ( MEM_OVLY_PTR, DIR_BLK_PTR )
                                PTR STATIC;
      DCL  ( MEMBER, MASK_CTR, LEN, ICTR, JCTR, I_ORG_CNT )
                                FIXED BIN(31) STATIC INIT(0);
      DCL  ( HBOUND, SUBSTR, HIGH, LENGTH, ADDR, INDEX )
                                BUILTIN;

      OPEN FILE(SELMEM), FILE(I), FILE(O);
      GET DATA FILE(SELMEM);
      ON ENDFILE(I) GOTO I_EOF;

 I_READ_LOOP:
      READ FILE(I) SET(DIR_BLK_PTR);
      LEN = 2;
      MEM_OVLY_PTR = ADDR(DIR_BLK.DATA);
 I_MEM_ROUTINE:
      IF MEM_OVLY.MEMBER = HIGH(8)
         THEN GOTO I_EOF;
      MEMBER = MEMBER + 1;
      IF MEMBER < FIRST | MEM_OVLY.MEMBER < FIRSTMEM
         THEN GOTO I_NEXT_MEM;
      IF MEMBER > LAST | MEM_OVLY.MEMBER > LASTMEM
         THEN GOTO I_EOF;
      DIR_DATA
         = SUBSTR(DIR_DATA_OVLY, 1, 12 + ( MEM_OVLY.C.DATA_CNT * 2 ));
      WRITE FILE(O) FROM(DIR_DATA);
 I_NEXT_MEM:
      I_ORG_CNT = MEM_OVLY.C.DATA_CNT + 1;
      MEM_OVLY_PTR = ADDR(MEM_OVLY.ORG_HWDS(I_ORG_CNT));
      LEN = LEN + (2 * I_ORG_CNT) + 10;
      IF DIR_BLK.LL > LEN
            THEN GOTO I_MEM_ROUTINE;
            ELSE GOTO I_READ_LOOP;

 I_EOF:
      CLOSE FILE(SELMEM), FILE(I), FILE(O);
      END DIRLIST;

//SYSUT1       DD       UNIT=VIO,SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(5,3))
//***
//L          EXEC     PGM=HEWLKED,COND=(5,LT),
//             PARM='XREF,LIST,LET,AMODE(31),RMODE(ANY)',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   99042100
  ENTRY    PLISTART
  NAME     DIRLIST(R)
//***
