//-YOUR-USERID-T JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  COMPILE FOR TOUCH WHICH RETRIEVES AND REPLACES A SPECIFIC
//***  DIRECTORY ENTRY'S STATISTICS. USES BLDLR AND STOWR.
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,COND=(5,LT),
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 TOUCH: PROC OPTIONS(MAIN) REORDER;

        DCL  SYSIN_EOF               CHAR(01) STATIC INIT('N');
        DCL  (ADDR, DATE, SUBSTR, TIME)
                                     BUILTIN;

        DCL  DSNAME                  CHAR(44)       STATIC;
        DCL  DDNAME                  CHAR(08)       STATIC;
        DCL  MEMBER                  CHAR(08)       STATIC;
        DCL  ALIAS                   CHAR(01)       STATIC;
        DCL  VERSIONNUMBER           PIC'999'       STATIC;
        DCL  MAINTNUMBER             PIC'999'       STATIC;
        DCL  CREATEDATE              PIC'99999999'  STATIC;
        DCL  TODAYDATE               PIC'99999999'  STATIC;
        DCL  CHANGEDATE              PIC'99999999'  STATIC;
        DCL  CHANGETIME              PIC'99999'     STATIC;
        DCL  CURRENTSIZE             PIC'999999'    STATIC;
        DCL  INITIALSIZE             PIC'999999'    STATIC;
        DCL  MODCOUNT                PIC'999999'    STATIC;
        DCL  USERID                  CHAR(07)       STATIC;

        DCL  01  CCYYMMDD                           STATIC,
                05  CCYY             PIC'9999',
                05  MM               PIC'99',
                05  DD               PIC'99';
        DCL  MMDD_TO_DDD_LEAP(12)    PIC'999'       STATIC
                                     INIT( 000, 031, 060,
                                           091, 121, 152,
                                           182, 213, 244,
                                           274, 305, 335 );
        DCL  MMDD_TO_DDD(12)         PIC'999'       STATIC
                                     INIT( 000, 031, 059,
                                           090, 120, 151,
                                           181, 212, 243,
                                           273, 304, 334 );

        DCL      DIR_DATA    CHAR(80) STATIC;
        DCL  01  DIRDATA     BASED(DIRDATA_PTR),
                05  MEMBER_NAME         CHAR(08),
                05  TT_R                CHAR(03),
                05  ALIAS_#TTRN_#HW     BIT(08),
                05  VERSION_NUMBER      CHAR(01),
                05  MAINTENANCE_NUMBER  CHAR(01),
                05  FILL_10             CHAR(01),
                05  FILL_20             CHAR(01),
                05  CREATE_DATE         CHAR(04),
                05  CHANGE_DATE         CHAR(04),
                05  CHANGE_TIME         CHAR(02),
                05  CURRENT_SIZE        CHAR(02),
                05  INITIAL_SIZE        CHAR(02),
                05  MODIFICATION_COUNT  FIXED BIN(15),
                05  USER_ID             CHAR(07),
                05  FILL_99             CHAR(41);
        DCL  DIRDATA_PTR                PTR;
        DIRDATA_PTR = ADDR(DIR_DATA);

        DCL  FW_SIZE                 FIXED BIN(31)  STATIC;
        DCL  01 CH_OVLY_FW_SIZE BASED(ADDR(FW_SIZE)),
                 05  CH_DUMMY        CHAR(02),
                 05  CH_SIZE         CHAR(02);
        DCL  PACKED7                 FIXED DEC(07) STATIC;
        DCL  TODAY                   CHAR(08) STATIC;
        DCL  HEX04OVLY               BIT(32) BASED(PACKED7_PTR);
        DCL  CHAR04OVLY              CHAR(04) BASED(PACKED7_PTR);
        DCL  HEX0000000F             BIT(32) STATIC
             INIT('00000000000000000000000000001111'B);
        DCL  PACKED7_PTR             PTR;
        PACKED7_PTR = ADDR(PACKED7);

        DECLARE BLDLR  ENTRY(CHAR(44),      /* DSNAME            */
                             CHAR(8),       /* MEMBER            */
                             CHAR(8),       /* DDNAME            */
                             FIXED BIN(15), /* DYNALLOC R15 R/C  */
                             FIXED BIN(15), /* INFO  ERROR  CODE */
                             FIXED BIN(15), /* INFO  REASON CODE */
                             CHAR(80)     ) /* RET'D DIRECTORY   */
                       OPTIONS(ASM);

        DECLARE STOWR  ENTRY(CHAR(44),      /* DSNAME            */
                             CHAR(8),       /* MEMBER            */
                             CHAR(8),       /* DDNAME            */
                             FIXED BIN(15), /* DYNALLOC R15 R/C  */
                             FIXED BIN(15), /* INFO  ERROR  CODE */
                             FIXED BIN(15), /* INFO  REASON CODE */
                             CHAR(80)     ) /* DIRECTORY UPDATE  */
                       OPTIONS(ASM);

        DCL  HWBIN     FIXED BIN(15) STATIC INIT(0);
        DCL  CHBIN     CHAR(02) BASED(HWBIN_PTR);
        DCL  HWBIN_PTR PTR;
        HWBIN_PTR = ADDR(HWBIN);

        DCL  THREEPACKED
                       FIXED DECIMAL(5) STATIC;
        DCL  TWOHEX    CHAR(02) BASED(THREEPACKED_PTR);
        DCL  NOW       CHAR(04) STATIC;
        DCL  NOWNUM    PIC'9999' BASED(ADDR(NOW));
        DCL  THREEPACKED_PTR    PTR;
        THREEPACKED_PTR = ADDR(THREEPACKED);

        DCL  R15       FIXED BIN(15) STATIC INIT(0);
        DCL  RC        FIXED BIN(15) STATIC INIT(0);
        DCL  WHY       FIXED BIN(15) STATIC INIT(0);

     ON ENDFILE(SYSIN) SYSIN_EOF = 'Y';

     DSNAME         = HIGH(44);
     DDNAME         = ' ';
     MEMBER         = HIGH(08);
     ALIAS          = HIGH(01);
     VERSIONNUMBER  = 999;
     MAINTNUMBER    = 999;
     CREATEDATE     = 99999999;
     CHANGEDATE     = 99999999;
     CHANGETIME     = 99999;
     CURRENTSIZE    = 999999;
     INITIALSIZE    = 999999;
     MODCOUNT       = 999999;
     USERID         = HIGH(07);

     GET DATA FILE(SYSIN) COPY(SYSPRINT);

     DO WHILE ( SYSIN_EOF = 'N' );

        IF DSNAME = HIGH(44)
           THEN DO;
              PUT EDIT ('**  ERROR  **  ',
                        'NO SUPPLIED ''DSNAME'' VALUE')
                       (SKIP(1),A,A);
              CALL PLIRETC(16);
              RETURN;
              END;

        IF MEMBER = HIGH(08)
           THEN DO;
              PUT EDIT ('**  ERROR  **  ',
                        'NO SUPPLIED ''MEMBER'' VALUE')
                       (SKIP(1),A,A);
              CALL PLIRETC(20);
              RETURN;
              END;

        CALL BLDLR(DSNAME,MEMBER,DDNAME,
           R15,RC,WHY,DIR_DATA);

        IF R15 ¬= 0
           THEN DO;
              PUT EDIT ('**  ERROR  **  ',
                        'BLDL ERROR = ',    R15,
                        ', REASON CODE = ', RC,
                        ', OTHER CODE = ',  WHY)
                       (SKIP(1),A,A,P'9999',A,P'-ZZ,ZZ9',
                        A,P'-ZZ,ZZ9');
              PUT EDIT ('               ',
                        'DSNAME = ', DSNAME)
                       (SKIP(1),A,A,A);
              PUT EDIT ('               ',
                        'MEMBER = ', MEMBER)
                       (SKIP(1),A,A,A);
              CALL PLIRETC(24);
              RETURN;
              END;

        IF ( ALIAS_#TTRN_#HW & '00011111'B ) < '00001111'B
           THEN DO;
              ALIAS_#TTRN_#HW
                 = ( ALIAS_#TTRN_#HW & '11100000'B ) | '00001111'B;
              VERSION_NUMBER     = 001;
              MAINTENANCE_NUMBER = 001;
              TODAY              = '  ' || DATE;
              IF SUBSTR(TODAY,3,2) < '50'
                 THEN SUBSTR(TODAY,1,2) = '20';
                 ELSE SUBSTR(TODAY,1,2) = '19';
              STRING(CCYYMMDD) = TODAY;
              IF MOD(CCYY,4) = 0
                 THEN TODAYDATE = ( CCYY * 1000 )
                       + MMDD_TO_DDD_LEAP(MM) + DD;
                 ELSE TODAYDATE = ( CCYY * 1000 )
                       + MMDD_TO_DDD(MM) + DD;
              PACKED7            = TODAYDATE - 1900000;
              HEX04OVLY          = HEX04OVLY | HEX0000000F;
              CREATE_DATE        = CHAR04OVLY;
              CHANGE_DATE        = CHAR04OVLY;
              NOW                = SUBSTR(TIME,1,4);
              THREEPACKED        = NOWNUM * 10;
              CHANGE_TIME        = TWOHEX;
              FILL_20            = LOW(1);
              FW_SIZE            = 65535;
              CURRENT_SIZE       = CH_SIZE;
              INITIAL_SIZE       = CH_SIZE;
              MODIFICATION_COUNT = 0;
              USER_ID            = 'DEFAULT';
              END;

        IF ALIAS ¬= HIGH(01)
           THEN DO;
              IF ALIAS = 'Y'
                 THEN DO;
                    ALIAS_#TTRN_#HW = ALIAS_#TTRN_#HW | '10000000'B;
                    END;
                 ELSE DO;
                    IF ALIAS = 'N'
                       THEN DO;
                          ALIAS_#TTRN_#HW = ALIAS_#TTRN_#HW
                             & '01111111'B;
                          END;
                       ELSE DO;
                          R15 = 60;
                          PUT EDIT ('**  ERROR  **  ',
                                    'ALIAS MUST BE ''Y'' OR ''N'' - ',
                                    ALIAS)
                                   (SKIP(1),A,A,A);
                          END;
                    END;
              END;

        IF VERSIONNUMBER ¬= 999
           THEN DO;
              IF VERSIONNUMBER < 100
                 THEN DO;
                    HWBIN = VERSIONNUMBER;
                    VERSION_NUMBER = SUBSTR(CHBIN,2,1);
                    END;
                 ELSE DO;
                    R15 = 64;
                    PUT EDIT ('**  ERROR  **  ',
                              'VERSION NUMBER MUST BE < 100 - ',
                              VERSIONNUMBER)
                             (SKIP(1),A,A,P'999');
                    END;
              END;

        IF MAINTNUMBER ¬= 999
           THEN DO;
              IF MAINTNUMBER < 100
                 THEN DO;
                    HWBIN = MAINTNUMBER;
                    MAINTENANCE_NUMBER = SUBSTR(CHBIN,2,1);
                    END;
                 ELSE DO;
                    R15 = 68;
                    PUT EDIT ('**  ERROR  **  ',
                              'MAINTENANCE NUMBER MUST BE < 100 - ',
                              MAINTNUMBER)
                             (SKIP(1),A,A,P'999');
                    END;
              END;

        IF CREATEDATE ¬= 99999999
           THEN DO;
              IF CREATEDATE > 9999999
                 THEN DO;
                    STRING(CCYYMMDD) = CREATEDATE;
                    IF MOD(CCYY,4) = 0
                       THEN DO;
                          CREATEDATE = ( CCYY * 1000 )
                             + MMDD_TO_DDD_LEAP(MM)
                             + DD;
                          END;
                       ELSE DO;
                          CREATEDATE = ( CCYY * 1000 )
                             + MMDD_TO_DDD(MM)
                             + DD;
                          END;
                    END;
              IF CREATEDATE > 1900000
                 THEN DO;
                    PACKED7     = CREATEDATE - 1900000;
                    HEX04OVLY   = HEX04OVLY | HEX0000000F;
                    CREATE_DATE = CHAR04OVLY;
                    END;
                 ELSE DO;
                    R15 = 72;
                    PUT EDIT ('**  ERROR  **  ',
                              'CREATE DATE SHOULD BE CCYYDDD OR ',
                              'CCYYMMDD - ',
                              CREATEDATE)
                             ( SKIP(1), A, A, A, P'99999999' );
                    END;
              END;

        IF CHANGEDATE ¬= 99999999
           THEN DO;
              IF CHANGEDATE > 9999999
                 THEN DO;
                    STRING(CCYYMMDD) = CHANGEDATE;
                    IF MOD(CCYY,4) = 0
                       THEN DO;
                          CHANGEDATE = ( CCYY * 1000 )
                             + MMDD_TO_DDD_LEAP(MM)
                             + DD;
                          END;
                       ELSE DO;
                          CHANGEDATE = ( CCYY * 1000 )
                             + MMDD_TO_DDD(MM)
                             + DD;
                          END;
                    END;
              IF CHANGEDATE > 1900000
                 THEN DO;
                    PACKED7     = CHANGEDATE - 1900000;
                    HEX04OVLY   = HEX04OVLY | HEX0000000F;
                    CHANGE_DATE = CHAR04OVLY;
                    END;
                 ELSE DO;
                    R15 = 76;
                 PUT EDIT ('**  ERROR  **  ',
                           'CHANGE DATE SHOULD BE CCYYDDD OR ',
                           'CCYYMMDD - ',
                           CHANGEDATE)
                          ( SKIP(1), A, A, A, P'99999999' );
                    END;
            END;

        IF CHANGETIME ¬= 99999
           THEN DO;
              IF CHANGETIME < 2400
                 THEN DO;
                    THREEPACKED = CHANGETIME * 10;
                    CHANGE_TIME = TWOHEX;
                    FILL_20     = LOW(1);
                    END;
                 ELSE DO;
                    R15 = 80;
                    PUT EDIT ('**  ERROR  **  ',
                              'CHANGE TIME SHOULD BE HHMM ',
                              '(FROM 00:00 TO 23:59) - ',
                              CHANGETIME)
                             (SKIP(1),A,A,A,P'99999');
                    END;
              END;

        IF CURRENTSIZE ¬= 999999
           THEN DO;
              IF CURRENTSIZE <= 65535 & CURRENTSIZE > 0
                 THEN DO;
                    FW_SIZE      = CURRENTSIZE;
                    CURRENT_SIZE = CH_SIZE;
                    END;
                 ELSE DO;
                    R15 = 84;
                    PUT EDIT ('**  ERROR  **  ',
                              'CURRENT SIZE MUST BE > 0 AND < 65535 - ',
                              CURRENTSIZE)
                             (SKIP(1),A,A,P'999999');
                    END;
              END;

        IF INITIALSIZE ¬= 999999
           THEN DO;
              IF INITIALSIZE <= 65535 & INITIALSIZE > 0
                 THEN DO;
                    FW_SIZE      = INITIALSIZE;
                    INITIAL_SIZE = CH_SIZE;
                    END;
                 ELSE DO;
                    R15 = 88;
                    PUT EDIT ('**  ERROR  **  ',
                              'INITIAL SIZE MUST BE > 0 AND < 65535 - ',
                              INITIALSIZE)
                             (SKIP(1),A,A,P'999999');
                    END;
              END;

        IF MODCOUNT ¬= 999999
           THEN DO;
              IF MODCOUNT < 32768
                 THEN DO;
                    MODIFICATION_COUNT = MODCOUNT;
                    END;
                 ELSE DO;
                    R15 = 92;
                    PUT EDIT ('**  ERROR  **  ',
                              'MODIFICATION COUNT MUST BE < 32768 - ',
                              MODCOUNT)
                             (SKIP(1),A,A,P'999999');
                    END;
              END;

        IF USERID ¬= HIGH(07)
           THEN DO;
              USER_ID = USERID;
              END;

        IF R15 ¬= 0
           THEN DO;
              PUT EDIT ('*************  ',
                        'PROGRAM DISCONTINUED BECAUSE OF INPUT ERRORS')
                       (SKIP(1),A);
              CALL PLIRETC(96);
              RETURN;
              END;

        CALL STOWR(DSNAME,MEMBER,DDNAME,
           R15,RC,WHY,DIR_DATA);

        IF R15 ¬= 0
           THEN DO;
              PUT EDIT ('**  ERROR  **  ',
                        'STOW ERROR = ',    R15,
                        ', REASON CODE = ', RC,
                        ', OTHER CODE = ',  WHY)
                       (SKIP(1),A,A,P'99999',A,P'-ZZ,ZZ9',
                        A,P'-ZZ,ZZ9');
              PUT EDIT ('               ',
                        'DSNAME = ', DSNAME)
                       (SKIP(1),A,A,A);
              PUT EDIT ('               ',
                        'MEMBER = ', MEMBER)
                       (SKIP(1),A,A,A);
              CALL PLIRETC(28);
              RETURN;
              END;

        DSNAME         = HIGH(44);
        DDNAME         = ' ';
        MEMBER         = HIGH(08);
        ALIAS          = HIGH(01);
        VERSIONNUMBER  = 999;
        MAINTNUMBER    = 999;
        CREATEDATE     = 99999999;
        CHANGEDATE     = 99999999;
        CHANGETIME     = 99999;
        CURRENTSIZE    = 999999;
        INITIALSIZE    = 999999;
        MODCOUNT       = 999999;
        USERID         = HIGH(07);

        GET DATA FILE(SYSIN) COPY(SYSPRINT);

        END;        /**  END OF DO WHILE ( SYSIN_EOF = 'N' )  **/

     END TOUCH;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//L          EXEC     PGM=HEWLKED,COND=(5,LT),
//             PARM='XREF,LIST,LET,DCBS',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//             DD       DISP=SHR,DSN=-YOUR-LOADLIB-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   99102600
  ENTRY    PLISTART
  NAME     TOUCH(R)
//***
