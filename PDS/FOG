/**  REXX -- Generate pseudo random text  **/

"ISREDIT MACRO ("times")   PROCESS"
'ISREDIT (Line,Col) = CURSOR'
If times = '?' then Signal DispDoc

/* List of LEADINS, all of which mean nothing but buy time */
leadin.1  = "In particular,"
leadin.2  = "On the other hand,"
leadin.3  = "However,"
leadin.4  = "Similarly,"
leadin.5  = "As a resultant implication,"
leadin.6  = "In this regard,"
leadin.7  = "Based on integral subsystem considerations,"
leadin.8  = "For example,"
leadin.9  = "Thus,"
leadin.10 = "In respect to specific goals,"
leadin.11 = "Interestingly enough,"
leadin.12 = "Without going into the technical details,"
leadin.13 = "Of course,"
leadin.14 = "To approach true user-friendliness,"
leadin.15 = "In theory,"
leadin.16 = "It is assumed that"
leadin.17 = "Conversely,"
leadin.18 = "We can see, in retrospect,"
leadin.19 = "It is further assumed that"
leadin.20 = "Further,"
leadin.21 = "In summary,"
leadin.22 = "It should be noted that"
leadin.23 = "To further describe and annotate,"
leadin.24 = "Specifically,"
leadin.0  = 24

/* List of SUBJECT clauses chosen for no redeeming value whatsoever */
subject.1  = "a large portion of interface coordination communication"
subject.2  = "a constant flow of effective communication"
subject.3  = "the characterization of specific criteria"
subject.4  = "initiation of critical subsystem development"
subject.5  = "the fully integrated test program"
subject.6  = "the product configuration baseline"
subject.7  = "any associated supporting element"
subject.8  = "the incorporation of additional mission constraints"
subject.9  = "the independent functional principle"
subject.10 = "the interrelation of system and/or subsystem technologies"
subject.11 = "the product assurance architecture"
subject.0  = 11

/* List of VERB clauses chosen for auto-recursive obfuscation */
verb.1  = "must utilize and be functionally interwoven with"
verb.2  = "maximizes the probability of project success,",
   || " yet minimizes cost and time required for"
verb.3  = "adds explicit performance limits to"
verb.4  = "necessitates that urgent consideration be applied to"
verb.5  = "requires considerable systems analysis and",
   || " trade-off studies to arrive at"
verb.6  = "is further compounded when taking into account"
verb.7  = "presents extremely interesting challenges to"
verb.8  = "recognizes other systems' importance and the necessity for"
verb.9  = "affects a significant implementation of"
verb.10 = "adds overriding performance constraints to"
verb.11 = "mandates staff-meeting-level attention to"
verb.12 = "is functionally equivalent and parallel to"
verb.0  = 12

/* List of OBJECT clauses selected for profound meaninglessness */
object.1  = "the most recent level of hardware.  "
object.2  = "the anticipated fourth-generation equipment.  "
object.3  = "the subsystem compatibility testing.  "
object.4  = "the structural design, based on system",
   || " engineering concepts.  "
object.5  = "the preliminary qualification limit.  "
object.6  = "the evolution of specifications over a given",
   || " time period.  "
object.7  = "the philosophy of commonality and standardization.  "
object.8  = "the greater fight-worthiness concept.  "
object.9  = "any discrete configuration mode.  "
object.10 = "the management-by-contention principle.  "
object.11 = "the total system rationale.  "
object.12 = "possible bidirectional logical relationship approaches.  "
object.13 = "the postulated use of dialog management technology.  "
object.14 = "the overall negative profitability.  "
object.0  = 14

If times = '?' Then Do
    Say "FOG is an interactive productivity tool designed to"
    Say "assist in the composition of monthly reports, project"
    Say "plans, memos to management and so forth.   If entered"
    Say "without parameters, FOG generates as output a"
    Say "high-fog-index sentence suitable for befuddling even"
    Say "the most determined seeker-after-content.   If you need"
    Say "vast amounts of FOG for really serious tush-covering,"
    Say "enter the number of sentences needed as a parameter."
    Say "For example, entering 'FOG 5' outputs half a screen"
    Say "of heat-treated, battle-hard, industrial-strength"
    Say "slop, well suited to choking hogs and assurance"
    Say "planners, after the line your cursor is on."
    Say " "
    Say "REMEMBER:"
    Say "   FOG can be a terrible weapon -- never abuse it!"
    Exit
  End

If times = '' then times = 1

leadnum  = 0
subnum   = 0
verbnum  = 0
objnum   = 0
lastlead = leadnum
lastsub  = subnum
lastverb = verbnum
lastobj  = objnum
outstuff = ""
linesize = 64

/* And, last but not least, here's the program */
Do i = 1 To times
  Do While leadnum = lastlead
    leadnum = RANDOM(1, leadin.0)
  End

  Do While subnum = lastsub
    subnum = RANDOM(1, subject.0)
  End

  Do While verbnum = lastverb
    verbnum = RANDOM(1, verb.0)
  End

  Do While objnum = lastobj
    objnum = RANDOM(1, object.0)
  End

  outstuff = outstuff leadin.leadnum subject.subnum,
     verb.verbnum object.objnum

  lastlead = leadnum;
  lastsub  = subnum;
  lastverb = verbnum;
  lastobj  = objnum;
End i

/* OK, now lets put that data out NICELY */
Do Until outstuff = ''
  k = LASTPOS(' ',outstuff,MIN(linesize,LENGTH(outstuff)))
  record = strip(LEFT(outstuff,k))
  "ISREDIT Line_After &line = (record)"
  Line = Line + 1
  outstuff=STRIP(SUBSTR(outstuff,k+1),'L')
End
Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
   SAY "FOG -- Generate Pseudo Randow Text                        "
   SAY
   SAY " Format "
   SAY
   SAY "    FOG  ( # )                                            "
   SAY
   SAY "       Generates meaningless random sentences after the   "
   SAY "       line the cursor is on or after the top line on the "
   SAY "       displeyed screen.                                  "
   SAY
   SAY "       # is the number of sentences to be generated. The  "
   SAY "       CAPS ON|OFF setting determines whether the         "
   SAY "       sentences are all upper or normal mixed case.      "
   SAY
   EXIT(1)

