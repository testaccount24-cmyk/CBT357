/** REXX - INSERT EVALUATE STRUCTURE **/

   SAVEMSG = MSG()
   X = MSG("OFF")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY "IE MUST BE RUN UNDER ISPF EDIT!"
      SIGNAL QUIT
      END
   SUBCOM ISREDIT
   UMODE = "ISREDIT"
   ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
   ADDRESS ISREDIT
   "MACRO (INDENT) NOPROCESS"

   IF INDENT = "" THEN INDENT = 4
   ADDRESS ISREDIT
   "(LINE,COL) = CURSOR"
   "(REC) = LINE" LINE
   DO II = 8 TO 72 WHILE(SUBSTR(REC,II,1) = ' ')
   END II
   IF SUBSTR(REC,II,5) = 'ELSE ',
         | SUBSTR(REC,II,3) = 'IF ',
         | SUBSTR(REC,II,8) = 'PERFORM ',
         | SUBSTR(REC,II,5) = 'WHEN ',
      THEN OFFSET = II + INDENT - 1
      ELSE OFFSET = II - 1
   IF OFFSET < 11 | OFFSET > 58 THEN OFFSET = 11
   INDENT2 = OFFSET + INDENT + INDENT
   INDENT  = OFFSET + INDENT
   REC = COPIES(' ',OFFSET)  || "END-EVALUATE"
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',INDENT2) || "..."
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',INDENT)  || "WHEN OTHER"
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',INDENT2) || "..."
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',INDENT)  || "WHEN ..."
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',INDENT2) || "..."
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',INDENT)  || "WHEN ..."
   "LINE_AFTER (LINE) = (REC)"
   REC = COPIES(' ',OFFSET)  || "EVALUATE ..."
   "LINE_AFTER (LINE) = (REC)"
   "LOCATE" LINE
   LINE_X  = LINE + 1
   COL_X   = INDENT2 + 2
   "CURSOR =" LINE_X COL_X
   RETURN(0)
