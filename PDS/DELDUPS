/**  REXX -- SORT AND GET RID OF ALL DUPS WITHIN A FILE  **/

   SUBCOM ISREDIT
   IF  RC = 0  THEN DO
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS)"
      IF  RC = 0  THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF  DSN = 'DSN'  THEN DO
      SAY 'DELDUPS MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END

   PARMS   = TRANSLATE(PARMS)
   PARMCNT = 0
   COL.    = ""
   LBL1 = ""
   LBL2 = ""
   DO II = 1 TO 16;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LBL1 = "" THEN LBL1 = TOKEN
         ELSE IF LBL2 = "" THEN LBL2 = TOKEN
            ELSE DO
               ZEDSMSG = ".LABEL RC=8"
               ZEDLMSG = "TOO MANY LABELS --" LBL1 LBL2 TOKEN
               ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
               EXIT 8
            END
      END
      ELSE DO
         PARMCNT = PARMCNT + 1
         COL.PARMCNT = TOKEN
      END
   END II
   IF LBL1 <> ""  &  LBL2 = ""  THEN LBL2 = LBL1
   IF (PARMCNT // 2) <> 0 THEN DO
      SAVER = COL.PARMCNT
      PARMCNT = PARMCNT + 1
      COL.PARMCNT = SAVER
   END

   ERRORMSG = ''
   MESG = ''
   IF  PARMCNT = 0 THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   SORTPARMS = ""
   DO II = 1 TO PARMCNT
      IF  Â¬DATATYPE(COL.II,'W') | COL.II = 0 THEN DO
         MESG = "'"COL.II"' COLUMN VALUE IS INVALID!"
         SIGNAL ERROR
      END
      IF  COL.II > LRECL  THEN DO
         MESG = "'"COL.II"' IS GREATER THAN RECORD SIZE("LRECL")"
         SIGNAL ERROR
      END
      SORTPARMS = SORTPARMS || " " || COL.II
   END II
   DO II = 1 TO PARMCNT BY 2
      JJ = II + 1
      SAVER = COL.JJ
      IF  COL.II > SAVER THEN DO
         MESG = "LEFT-COL "COL.II" IS GREATER THAN RIGHT-COL "SAVER
         SIGNAL ERROR
      END
   END II

   IF  LBL1 <> ''  THEN DO
      "ISREDIT L &LBL1"
       IF  RC <>  0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
   END

   IF  LBL2 <> ''  THEN DO
      "ISREDIT L &LBL2"
       IF  RC <>  0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
       END
   END

   IF  LBL1 <> ''  THEN  "ISREDIT (START) = LINENUM &LBL1"
                   ELSE  START = 1
   IF  LBL2 <> ''  THEN  "ISREDIT (END) = LINENUM &LBL2"
                   ELSE  "ISREDIT (END) = LINENUM .ZLAST"
   "ISREDIT (LASTLINE) = LINENUM .ZLAST"

   "ISREDIT ("ANUM") = AUTONUM"
   "ISREDIT NUMBER OFF "

   "ISREDIT SORT" SORTPARMS LBL1 LBL2
   DELCNT = 0

   PREVKEY = ""
   DO  PNTR  =  START TO END  BY 1
      "ISREDIT (RECORD) = LINE "PNTR
      NEXTKEY = ""
      DO II = 1 TO PARMCNT BY 2
         JJ = II + 1
         SAVER = COL.JJ
         NEXTKEY = NEXTKEY || SUBSTR(RECORD,COL.II,SAVER-COL.II+1)
      END II
      IF NEXTKEY == PREVKEY THEN DO
         "ISREDIT LABEL "PNTR" = .TMP"
         "ISREDIT X ALL .TMP .TMP"
         DELCNT = DELCNT + 1
      END
      PREVKEY = NEXTKEY
   END

   "ISREDIT DEL ALL X"
   CALL SETMSG '* NO ' DELCNT 'RECS DELETED$RECS WITH DUP KEYS DELETED'
   IF START = 1 THEN "ISREDIT LOCATE" 0
                ELSE DO
                   START = START - 1
                   "ISREDIT LOCATE" START
                   END
   "ISREDIT AUTONUM = "ANUM
   "ISREDIT CURSOR = "SAVLINE SAVCOL
   SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      ERRORMSG = '===>' MESG
   END
   SIGNAL DISPDOC

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

EOJ:
   RETURN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "DELDUPS - SORT AND DELETE ANY DUP LINES WITHIN A FILE "
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY
   SAY "    DELDUPS  COL1 COL2 ...  ( .A .B )                  "
   SAY "       ONE PAIR OF COLUMNS IS MANDATORY. THE LABEL     "
   SAY "       RANGE IS OPTIONAL.                              "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    DELDUPS  17 19  4 11                               "
   SAY "       WILL SORT FILE BY COLUMNS 17 THRU 19 AND 4 THRU "
   SAY "       11 AND DELETE ANY RECORDS, AFTER THE FIRST      "
   SAY "       OCCURRENCE, WITH DUPLICATE KEYS.                "
   SAY
   EXIT(1)
