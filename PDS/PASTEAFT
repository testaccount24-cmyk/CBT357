/**  REXX -- Place lines from the user profile pool after multiple   **/
/***         lines in the current file                                */

   Address ISPEXEC
   'ISREDIT MACRO (PARM1 PARM2) NOPROCESS'
   Parm1 = translate(Parm1)
   Parm2 = translate(Parm2)
   If Parm1 = '?' | Parm2 = '?' then signal DispDoc

   skip_value = 1
   If  datatype(Parm1,'W')  |  datatype(Parm2,'W') then do
       If datatype(Parm1,'W') then do
           Skip_Value = Parm1
           If  Parm2 = 'D'  |  Parm2 = 'DELETE'  then Parm1 = Parm2
                                                 Else Parm1 = ''
       End
       Else Skip_Value = Parm2
   End

   If  parm1 <> ''  &  parm1 <> 'D'  &  parm1 <> 'DELETE'  then do
       zedsmsg = 'Invalid parameter'
       zedlmsg = 'Use ? for help, D or DELETE to cause line(s) deletion'
       'SETMSG MSG(ISRZ001)'
       Exit(12)
   End

   'CONTROL ERRORS RETURN'
   'ISREDIT PROCESS RANGE C'
   Select
       When rc = 0 then do
            'ISREDIT (ZDEST1) = LINENUM .ZFRANGE'
            'ISREDIT (ZDEST2) = LINENUM .ZLRANGE'
       End
       When rc <= 8 then do
            zedsmsg = 'Enter "C/CC" line cmds'
            zedlmsg = 'PASTEAFT requires a "C" or "CC" line command'
            'SETMSG MSG(ISRZ001)'
            Exit(12)
       End
       When rc < 20 then
           Exit 12
       When rc = 20 then
           zdest = 0
       Otherwise Exit 12
   End
   'CONTROL ERRORS CANCEL'

   'VGET (CUTCNT) PROFILE'
   If rc <> 0 | CutCnt <= 0 then do
       zedsmsg = 'Use CUTX before PASTEAFT'
       zedlmsg = 'Use CUTX first before attempting to use PASTEAFT'
       'SETMSG MSG(ISRZ001)'
       Exit(12)
   End

   TruncCnt = 0
   CutCount = CutCnt
   if  ( ZDest2 - ZDest1 ) // Skip_Value <> 0
      then ZDest2 = ZDest2 - ( ( ZDest2 - ZDest1 ) // Skip_Value )
   Do ZDest = ZDest2 to ZDest1 by -Skip_Value
      Do i = CutCnt to 1 by -1
        'VGET (CL'i') PROFILE'
        'ISREDIT LINE_AFTER' ZDest '= DATALINE (CL'i')'
        If rc = 4 then TruncCnt = TruncCnt + 1
      End
   End

   If Parm1 = 'D' | Parm1 = 'DELETE' then do
       Do i = 1 to CutCnt by 1
         'VERASE (CL'i') PROFILE'
       End
       CutCnt   = 0
       'VPUT (CUTCNT) PROFILE'
   End

   If TruncCnt > 0 then do
       zedsmsg = TruncCnt 'lines truncated'
       msg     = 'Current record length shorter than origin'
       zedlmsg = msg '-' TruncCnt 'of' CutCount 'records truncated'
       'SETMSG MSG(ISRZ001)'
   End
   Else do
       zedsmsg = CutCount 'lines pasted'
       zedlmsg = CutCount 'lines were pasted into the current file'
       'SETMSG MSG(ISRZ000)'
   End

   'ISREDIT CURSOR =' ZDest1 0
   Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
   Say
   Say ' PASTEAFT will place cut lines from the PROFILE pool after     '
   Say ' one or more lines in the current file.                        '
   Say
   Say '     PASTEAFT  ( D | DELETE )  ( #-lines-to-skip )             '
   Say
   Say ' Enter PASTEAFT on the COMMAND line and use C or CC range line '
   Say ' command to specify where the lines are to be pasted. A number '
   Say ' of lines to skip may also be specified, 1 is the default.     '
   Say
   Say ' A parameter of D or DELETE can be specified to cause the macro'
   Say ' to erase the PROFILE variables after the lines have been      '
   Say ' pasted into the file.                                         '
   Say
   Exit(0)
