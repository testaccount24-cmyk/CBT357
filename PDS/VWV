/** REXX -- REPRO a VSAM file to a sequential copy and View it **/
   Parse Arg Dsn
   Msg = Msg(Off)
   Dsn = Translate(Dsn)
   If substr(Dsn,1,1) = "?" | Dsn = "" then signal DISPDOC
   Parms = ''
   If words(Dsn) > 1 then do
      Do ii = length(Dsn) to 1 by -1,
            while(substr(Dsn,ii,1) <> ' ' & substr(Dsn,ii,1) <> ') ')
      End ii
      Parms = substr(Dsn,1,ii)
      Dsn   = substr(Dsn,ii+1)
   End
   'ISPEXEC CONTROL ERRORS RETURN'
   If SysDsn(Dsn) <> 'OK' then DO
       ZedSmsg=''
       'ISPEXEC SETMSG MSG(ISRZ000)'
       Exit(12)
   End
   now     = Time(s)
   Gdd     ='F'  || now
   Flat    ='FL' || now
   Vsam    ='VS' || now
   FlatDSN = '$$$TEMP.VSAM.FLAT.' || Flat
   'ALLOC DD('Vsam') DSN('Dsn') SHR REUSE'
   Call GetFileAttr
   'ALLOC F('Flat') NEW CAT REU RECFM('Recfm')',
      'LRECL('Lrecl') BLKSIZE(0)',
      'CYL SP(20 20) RELEASE DSORG(PS) DSN('FlatDSN')'
   If Rc <> 0 Then do
       Say '* ERROR allocation for' Flat 'failed with code' RC
       Exit(12)
   End
   'REPRO IFI('Vsam') OFI('Flat')' Parms
   'FREE DD('Vsam' 'Flat' ifi ofi)'
   'FREE DSN('Dsn FlatDSN')'
   ZedSmsg=''
   ZedLmsg=''
   Address ISPEXEC "SELECT PGM(ISPSTRT)",
      "PARM(CMD(ISREPDF "FlatDSN" V))"
   Exit

GetFileAttr:
   'ALLOC F('Gdd') NEW DEL REU RECFM(V B)',
      'LRECL(129) BLKSIZE(0) TR SP(5)',
       'DSORG(PS)'
   'LISTCAT ENT('Dsn') ALL OUTFILE('Gdd')'
   'EXECIO * DISKR 'Gdd' (STEM LCDATA. FINIS)'
   'FREE F('Gdd')'

   AvgLrecl = 0
   MaxLrecl = 0
   Do x = 3 to LcData.0 while(AvgLrecl = 0 | MaxLrecl = 0)
      If index(LcData.x,'AVGLRECL') <> 0 Then do
         ii = index(LcData.x,'AVGLRECL')
         do jj = (ii+8) to 125 while(substr(LcData.x,jj,1) = '-')
         end jj
         do kk = (jj+1) to 125 while(substr(LcData.x,kk,1) <> ' ')
         end kk
         AvgLrecl = substr(LcData.x,jj,kk-jj)
      End
      If index(LcData.x,'MAXLRECL') <> 0 Then do
         ii = index(LcData.x,'MAXLRECL')
         do jj = (ii+8) to 125 while(substr(LcData.x,jj,1) = '-')
         end jj
         do kk = (jj+1) to 125 while(substr(LcData.x,kk,1) <> ' ')
         end kk
         MaxLrecl = substr(LcData.x,jj,kk-jj)
      End
   End

   if AvgLrecl = MaxLrecl then do
      Recfm = 'F B'
      Lrecl = MaxLrecl
   End
   Else do
      Recfm = 'V B'
      Lrecl = MaxLrecl + 4
   End
   Return

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "VWV  --  View all or part of a VSAM file                  "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    VWV  ( limits )  VSAM-file-name                       "
   SAY
   SAY "       The VSAM file name can be specified or defaulted   "
   SAY "       from a DS List entry.  The limits must be before   "
   SAY "       the file name. They may be COUNT(#), SKIP(#),      "
   SAY "       FROMKEY(key), TOKEY(key), FROMNUMBER(#), or        "
   SAY "       TONUMBER(#). It puts you in View on the set desired"
   SAY "       but saves the time-stamped file upon exit.         "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    VWV  COUNT(500)  A-VSAM-FILE-UNDER-YOUR-ID            "
   SAY
   SAY "       will View the first 500 records of that file and   "
   SAY "       keep the file on exiting.                          "
   SAY
   EXIT(1)

