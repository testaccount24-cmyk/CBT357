/**  REXX  --  SUBMIT A SUPERC SCAN OF A PDS FOR A STRING  **/
   SAVEMSG = MSG("OFF")
   CALLED = SYSVAR(SYSNEST)
   X = ADDRESS()
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT "ISREDIT MACRO ("PARMS")"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      UMODE = 'ISPEXEC'
      ARG PARMS
   END
   IF PARMS = "?" | PARMS = "" THEN SIGNAL DISPDOC
   IF SUBSTR(PARMS,1,1) = "'" THEN DO
      DO II = 2 TO LENGTH(PARMS),
            WHILE(SUBSTR(PARMS,II,1) <> "'")
      END II
   END
   ELSE IF SUBSTR(PARMS,1,1) = '"' THEN DO
      DO II = 2 TO LENGTH(PARMS),
            WHILE(SUBSTR(PARMS,II,1) <> '"')
      END II
   END
   ELSE DO
      SIGNAL DISPDOC
   END
   FINDSTR = SUBSTR(PARMS,2,II-2)

   USER_ID = USERID()
   PARMS   = TRANSLATE(SUBSTR(PARMS,II+1))
   DO II = 1 TO LENGTH(PARMS) WHILE(SUBSTR(PARMS,II,1) = ' ')
   END II
   IF II > 0 THEN PARMS = SUBSTR(PARMS,II)
   IF SUBSTR(PARMS,1,1) = "'" | SUBSTR(PARMS,1,1) = '"',
      THEN PARMS = SUBSTR(PARMS,2,LENGTH(PARMS)-2)
      ELSE PARMS = USER_ID || "." || PARMS


   ADDRESS TSO
   "FREE DD(SUBXDD)"
   "ALLOC DD(SUBXDD) SYSOUT(A) WRITER(INTRDR)",
      "LRECL(80) BLKSIZE(800) RECFM(F B)"

   QUEUE "//" || USER_ID || "S    JOB      (ACCT-INFO),'",
      || USER_ID || "',"
   QUEUE "//             CLASS=2,MSGCLASS=X,NOTIFY=",
      || USER_ID
   QUEUE "//***"
   QUEUE "//SEARCH     EXEC     PGM=ISRSUPC,REGION=4M,"
   QUEUE "//             PARM=(SRCHCMP,",
      || "'ANYC,IDPFX,NOPRTCC,SDUPM,XREF')"
   QUEUE "//OUTDD        DD       SYSOUT=*"
   QUEUE "SRCHFOR  '" || FINDSTR || "'"
   QUEUE "//NEWDD        DD       DISP=SHR,DSN=",
      || PARMS
   QUEUE "//***"

   "EXECIO" QUEUED() "DISKW SUBXDD (FINIS"
   "FREE DD(SUBXDD)"
   CALL SETMSG "* NO JOB SUBMITTED$JOB SUBMITTED SUCCESSFULLY!"
   EXIT(0)

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "FS -- SUBMIT A SUPERC PDS SEARCH FOR A STRING         "
   SAY
   SAY "   FS  'STRING TO BE FOUND' PDS-TO-BE-SEARCHED        "
   SAY
   SAY "     STRING MUST BE IN EITHER SINGLE OR DOUBLE QUOTES."
   SAY "     THE PDS NAME MAY BE FULLY QUALIFIED, I.E. IN     "
   SAY "     QUOTES, OR THE USER-ID WILL BE PREFIXED.         "
   SAY
   SAY "     A SINGLE QUESTION MARK (?) AS THE ONLY PARAMETER "
   SAY "     WILL CAUSE THIS HELP TO BE DISPLAYED.            "
   SAY
   EXIT(1)
