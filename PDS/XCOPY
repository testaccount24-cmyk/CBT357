/**  REXX -- COPY A DATA SET OR MEMBER INTO THE EDITED FILE  **/
   NUMERIC DIGITS 20
   SAVEMSG = MSG()
   X = MSG("OFF")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY "XCOPY MUST BE RUN UNDER ISPF EDIT!"
      EXIT(0)
      END
   SUBCOM ISREDIT
   UMODE = "ISREDIT"
   ADDRESS ISPEXEC "CONTROL ERRORS RETURN"

   ADDRESS ISREDIT
   "MACRO (INFILE FROMREC TOREC) NOPROCESS"
   UPPER INFILE
   IF INFILE = '' THEN SIGNAL DISPDOC
   DSNSTATUS = SYSDSN(INFILE)
   IF DSNSTATUS <> "OK" THEN DO
      SAY INFILE DSNSTATUS ":("
      EXIT(0)
   END
   IF FROMREC <> '' THEN DO
      IF ¬DATATYPE(FROMREC,'W') THEN DO
         SAY "FROM RECORD NUMBER, "FROMREC", NOT NUMERIC :("
         SIGNAL QUIT
      END
   END
   ELSE FROMREC = 1
   IF TOREC <> '' THEN DO
      IF ¬DATATYPE(TOREC,'W') THEN DO
         SAY "TO RECORD NUMBER, "TOREC", NOT NUMERIC :("
         SIGNAL QUIT
      END
   END
   ELSE TOREC = 999999999

   "ISREDIT PROCESS DEST"
   IF RC = 0 THEN "ISREDIT (ZDEST) = LINENUM .ZDEST"
             ELSE ZDEST = 0

   ADDRESS TSO
   "FREE DD($$INDATA)"
   "ALLOCATE DD($$INDATA) DSN("INFILE") SHR BUFNO(30)"

   ADDRESS ISREDIT
   ADDRESS TSO "EXECIO   0 DISKR" $$INDATA "(OPEN"
   ADDRESS TSO "EXECIO 200 DISKR" $$INDATA "(STEM RECS."
   RETCODE  = RC
   READREC  = 0
   INSERTED = 0
   II = ZDEST
   DO WHILE RETCODE = 0
      DO JJ = 1 TO RECS.0
         READREC = READREC + 1
         IF  READREC >= FROMREC  &  READREC <= TOREC  THEN DO
            INDATA = RECS.JJ
            "ISREDIT LINE_AFTER &II = (INDATA)"
            II = II + 1
            INSERTED = INSERTED + 1
         END
         ELSE IF READREC > TOREC THEN SIGNAL QUIT0
      END
      ADDRESS TSO "EXECIO 200 DISKR" $$INDATA "(STEM RECS."
      RETCODE = RC
   END
   IF RETCODE = 2 THEN DO
      DO JJ = 1 TO RECS.0
         READREC = READREC + 1
         IF  READREC >= FROMREC  &  READREC <= TOREC  THEN DO
            INDATA = RECS.JJ
            "ISREDIT LINE_AFTER &II = (INDATA)"
            II = II + 1
            INSERTED = INSERTED + 1
         END
         ELSE IF READREC > TOREC THEN SIGNAL QUIT0
      END
   END

QUIT0:
   ADDRESS TSO "EXECIO 0 DISKR" $$INDATA "(FINIS"
   ADDRESS TSO "FREE DD($$INDATA)"

QUIT:
   IF ZDEST = 0 THEN ZDEST = 1
                ELSE ZDEST = ZDEST + 1
   "ISREDIT CURSOR = " ZDEST 1
   CALL SETMSG '* NO ' INSERTED 'RECORDS INSERTED$RECORDS COPIED'
   RETURN

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "XCOPY - COPY ANY DATASET OR MEMBER DIRECTLY INTO EDITED FILE "
   SAY
   SAY " FORMAT "
   SAY "    XCOPY  FILE-NAME ( FROM-REC# ( TO-REC# ) )               "
   SAY "       THE FILE NAME OR PDS MEMBER NAME IS MANDATORY.  IF    "
   SAY "       SPECIFIED, ONLY THE RECORDS INCLUDING THE FROM-REC#   "
   SAY "       TO THE TO-REC# (OR TO THE END OF THE FILE IF NO       "
   SAY "       TO-REC# IS SPECIFIED) WILL BE COPIED.  A DESTINATION  "
   SAY "       (EITHER AN 'A' OR 'B' IN THE PREFIX AREA) MAY BE      "
   SAY "       SPECIFIED; IF NONE IS SPECIFIED, THE DATA IS COPIED   "
   SAY "       TO THE FRONT OF THE EDITED FILE.                      "
   SAY
   SAY " EXAMPLE "
   SAY "    XCOPY  'SYS1.MACLIB(GET)' "
   SAY "       WILL COPY THE 'GET' MEMBER OF SYS1.MACLIB INTO        "
   SAY "       THE EDITIED FILE, EITHER TO THE BEGINNING OR TO       "
   SAY "       WHERE THE AFTER/BEFORE PREFIX WAS SPECIFIED.          "
   SAY
   EXIT(1)
