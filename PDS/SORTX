
//***  GENERATE CURRENT DATE AS MM-DD-YYYY
  OUTFIL FNAMES=...,OUTREC=(...,&DATE(MD4-),...

//***  CONVERT PACKED TO ZONED IN OUTREC
  OUTREC FIELDS=(1,10,11,3,PD,TO=ZD,LENGTH=5,...)

//***  CONVERT PACKED TO BINARY IN OUTREC
  OUTREC FIELDS=(1,10,11,3,PD,TO=BI,LENGTH=4,...)

//***  ADD DECIMAL PLACES ON RIGHT
  OUTREC FIELDS=(1,7,PD,MUL,+100,TO=PD,LENGTH=8)

//***  COPY AND REPLACE HEX 00 AND FF WITH BLANKS
  SORT  FIELDS=COPY
    OUTFIL OUTREC=(1,10,11,10,TRAN=ALTSEQ,21,60)
    ALTSEQ CODE=(0040,FF40)
     **   FIRST 10 (1,10) POSITIONS ARE COPIED AS-IS
     **   NEXT 10 (11,10) ARE TRANSLATED TO BLANKS IF X'00' OR X'FF'
     **   THE REST OF THE POSITIONS (21,60) ARE COPIED ASIS

//***  SORT AND INCLUDE ONLY CERTAIN VALUES
  SORT     FIELDS=(0001,0003,CH,A)
    INCLUDE  COND=(0001,0001,CH,EQ,C'D',
               AND,0002,0002,CH,GT,C'02')

//***  COPY INCLUDING STRING ANYWHERE WITHIN RANGE OF INPUT
  SORT    FIELDS=COPY
    INCLUDE COND=(0001,0080,SS,EQ,C'SOME STRING')

//***  COPY INCLUDING WHERE 2 FIELDS ON SAME RECORD NOT EQUAL
  SORT FIELDS=COPY
    INCLUDE COND=(0001,0003,NE,0011,0003),FORMAT=CH

//***  COPY CHANGING THE RANGE INTO ITS HEX EQUIVALENT
  SORT      FIELDS=COPY
    OUTREC  FIELDS=(0001,0003,HEX)

//***  SPLIT FILE
  OPTION  COPY
    OUTFIL  FNAMES=DDNAME01,STARTREC=0000001,ENDREC=0000005
    OUTFIL  FNAMES=DDNAME02,STARTREC=0000006,ENDREC=0000011
    ...
    OUTFIL  FNAMES=DDNAME99,STARTREC=0000012

//***  PICK, SORT AND ALTER O/P
  SORT  FIELDS=(0001,0001,CH,A)
    INCLUDE  COND=(0001,0001,CH,EQ,C'2')
    OUTREC   FIELDS=(0002X,0001,0001,0002X,C'''OTHER JUNK''',00012X)

//***  SELECT BY NUMBER OF DUPS
  INREC     FIELDS=(0001,0080,         * ENTIRE RECORD
                    0004X'00',         * 4 LEADING BINARY 0'S
                    0001X'1C')         * PACKED DECIMAL 1
  SORT      FIELDS=(0001,0001,CH,A)    * SORT BY KEY FIELD(S)
    SUM     FIELDS=(0081,05,PD)        * SUM THE "COUNTER"
    OUTFIL  INCLUDE=(0081,05,PD,GT,2)  * ONLY OUTPUT RECS WITH 2 DUPS

//***  SELECT VARIOUS RECORDS TO VARIED FILES
   SORT  FIELDS=COPY
     OUTFIL  FILES=01,                 * PICK OFF A'S
       INCLUDE=(0001,0001,CH,EQ,C'A')
     OUTFIL  FILES=02,                 * PICK OFF C'S
       INCLUDE=(0001,0001,CH,EQ,C'C')
     ...
     OUTFIL  FILES=99,SAVE             * REMAINING

//***  SPLIT VARIOUS RECORDS TO VARIED FILES
  SORT  FIELDS=COPY
    OUTFIL FNAMES=(FILE1,FILE2),SPLIT,
      INCLUDE=(0002,0002,ZD,GE,02),OUTREC=(5X,1,75)

//***
  SUM  FIELDS=NONE,XSUM              * WRITE DUPS TO SORTXSUM DD
  SORT FIELDS=(0001,0080,CH,A)       * WRITE OTHERS SORTED TO SORTOUT

//***  PICK UP SINGLE ENTRIES ONLY
  INREC   FIELDS=(0001,0080,            * ENTIRE INPUT RECORD
                  X'000000001C')        * COUNT AS PIC S9(9) COMP-3
  SORT    FIELDS=(0001,0001,CH,A)       * SORT BY KEY
  SUM     FIELDS=(0081,0005,PD)         * ADD UP "COUNTER" FIELDS
  OUTFIL  INCLUDE=(0081,0005,PD,EQ,1),  * ONLY SINGLE OCCURANCES
          OUTREC=(0001,0080)            * TRIM OFF COUNTER FIELD

//***  ADD FIELDS FROM SAME RECORD
  INREC FIELDS=(0001,0001,
                0002X,
                C'ADDED TO ITSELF IS  ',
                (0001,0001,ZD,ADD,0001,0001,ZD),ZD,LENGTH=0005,
                0052X)
  SORT  FIELDS=(0001,0001,CH,A)

//***  ELIMINATE ALL DUPS
  SORT  FIELDS=(0001,0001,CH,A)
    SUM FIELDS=NONE

//***  TALLY KEYS
  INREC FIELDS=(0001,0001,      * SORT KEY
                0002X,          * COUPLE OF BLANKS
                C'00001',       * FIELD TO BE TALLIED
                0074X)          * BLANK REMAINING DATA
  SORT  FIELDS=(0001,0001,CH,A) * SORT THEM
    SUM FIELDS=(0004,0005,ZD)   * COUNT THE DUP KEYS

//***  CHANGE STUFF
    OPTION COPY * LEAVE 1 TO 10, CHANGE 11 FOR 2
                * CHANGE 11 TO 66 AND 22 TO 99, ELSE LEAVE ALONE
                * THEN ALL ELSE
    OUTREC FIELDS=(1,10,
      11:1,2,CHANGE=(2,C'11',C'66',C'22',C'99'),
        NOMATCH=(11,2),
      13:13,999)

//***  FULL BLOODY REPORT
  SORT  FIELDS=(0001,0001,CH,A)
  OUTFIL  LINES=55,
      HEADER2=(1:'PAGE ',&PAGE,' OF TRIAL REPORT FOR ',&DATE,
               2/,
               5:'  KEY   ',25:'DESCRIPTION',/,
               5:' NUMBER ',25:'  OF TYPE  ',/,
               5:'--------',25:'-----------'),
      OUTREC=(0007:0001,0001,
              0025:0001,0001,CHANGE=(0011,
                              C'1',C'TYPE I',
                              C'2',C'TYPE II',
                              C'3',C'TYPE III'),
                      NOMATCH=(C'*UNKNOWN TYPE*'),
              0079:X),
      TRAILER1=(3/,'NUMBER RECORDS IN = ',COUNT)

//***
   INREC FIELDS=(11,05,PD,DIV,+50,   * DIVIDE $ BY 50
      EDIT=(IIIIIIIIT),              * MAKE IT A 9 BYTE ZD FIELD
      X'0000001C'                    * PD "COUNTER" FIELD
   SORT FIELDS=(01,09,CH,A)          * SORT BY "BIN"
   SUM FIELDS=(10,04,PD)             * SUM "COUNTER" FIELD
   OUTREC FIELDS=(C' ',              * OUTPUT A SPACE,
      01,09,ZD,MUL,+50,              * RANGE START
      EDIT=(IIIIIIII9),              * EDIT MASK
      C' TO ',                       * RANGE SEPARATOR
      01,09,ZD,MUL,+50,ADD,+49,      * RANGE END
      EDIT=(IIIIIIII9),              * EDIT MASK
      C' COUNT:',                    * LITERAL
      10,04,PD,                      * "COUNTER" FIELD
      EDIT=(IIIIII9))                * EDIT MASK

//***  DFSORT SPECIFY NUMBER OF SORTWK FILES TO BE ALLOCATED
//DFSPARM DD *
  OPTION  DYNALLOC=(SYSDA,##)

//***  CHANGE FIELD OR LEAVE AS IT WAS
 SORT   FIELDS=COPY
   OUTFIL  OUTREC=(001,003,CHANGE=(003,C'236',C'292'),
      NOMATCH=(001,003),004,0252)

//***  PICK OFF EVERY HUNDREDTH RECORD
  SORT    FIELDS=COPY
    INREC FIELDS=(00001,00080,SEQNUM,8,ZD)
    OUTFIL INCLUDE=(00087,2,CH,EQ,C'00'),
        OUTREC=(00001,00080)

//***  REVERSE INPUT
    INREC  FIELDS=(00001,00080,SEQNUM,8,ZD)
  SORT   FIELDS=(0081,0008,ZD,D)
  OUTREC FIELDS=(00001,00080)

//***  PICK OFF LAST 20 RECORDS (IN REVERSE ORDER - NEED TO RESORT)
  INREC  FIELDS=(00001,00080,SEQNUM,8,ZD)
  SORT   FIELDS=(0081,0008,ZD,D)
  OUTFIL FNAMES=SORTOUT,ENDREC=20,OUTREC=(1,80)

//***  ADD A PSEUDO TIMESTAMP
  SORT FIELDS=COPY
  INREC FIELDS=(1,80,             * KEEP THE ORIGINAL RECORD
                DATE1,TIME1,1,6)  * ADD CCYYMMDD AND HHMMSS
  OUTREC FIELDS=(1,80,            * KEEP THE ORIGINAL RECORD
                81,4,             * CCYY
                C'-',             * DASH
                85,2,             * MM
                C'-',             * DASH
                87,2,             * DD
                C'-',             * DASH
                89,2,             * HH
                C'.',             * PERIOD
                91,2,             * MM
                C'.',             * PERIOD
                93,2,             * SS
                C'.000000')       *

