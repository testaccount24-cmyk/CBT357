//-YOUR-USERID-B JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  GET DIRECTORY ENTRY FOR A SPECIFIC MEMBER
//***
//A          EXEC     PGM=ASMA90,REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=SYS1.MACLIB
//             DD       DISP=SHR,DSN=-YOUR-SYSLIB-
BLDLR    TITLE 'DYNAMIC BLDL FOR DSN(MEMBER)'
**********************************************************************
*  INPUT PARAMETERS ARE:
*    DATA SET NAME       44 BYTES - ALPHANUMERIC  POINTED TO IN R2
*    MEMBER               8 BYTES - ALPHANUMERIC                R3
*    DDNAME               8 BYTES - ALPHANUMERIC                R4
*    DYNALLOC R15 R/C     2 BYTES - BINARY                      R5
*    DYNALLOC RETURN CODE 2 BYTES - BINARY                      R6
*    DYNALLOC REASON CODE 2 BYTES - BINARY                      R7
*    RETURNED BLDL DATA  80 BYTES - ALPHANUMERIC                R8
**********************************************************************
         COPY  MACSDYNA
BLDLR    RENTER

         LM    R2,R8,0(R1)
         USING BLDLDATA,R8

         ST    R2,DSADDR          DSNAME ADDR FOR ALLOC MACRO
         MVC   MEMNAME,0(R3)      SET UP BLDL MEMBER NAME
         MVC   USERDATA,BLANKS
         CLC   MEMNAME,BLANKS
         BE    NOMEMBER

         LA    R9,PDS
         USING IHADCB,R9

         CLC   0(8,R4),BLANKS     CHECK IF DDNAME PROVIDED
         BE    DEFAULT
         ST    R4,DDADDR          DDNAME ADDR
         MVC   DCBDDNAM,0(R4)
         B     POSTDEF
DEFAULT  LA    R15,DCBDDNAM
         ST    R15,DDADDR

POSTDEF  MVC   IUDATA,BLANKS
         MVC   0(2,R5),HEX00S     DEFAULT RETURN CODES TO 0
         MVC   0(2,R6),HEX00S
         MVC   0(2,R7),HEX00S

         ALLOC DISP=SHR,DSN=DSADDR,DDN=DDADDR
         LTR   R15,R15            CHECK ALLOC RETURN CODE
         BNZ   CHECKOUT

         OPEN  ((9),INPUT)
         LTR   R15,R15            CHECK BLDL RETURN CODE
         BNZ   OPENPROB

         MVI   ICONCAT,X'00'
         MVI   ILIBTYPE,X'00'
         MVI   IALIASCT,X'00'
         BLDL  PDS,BLDLPARM
         LTR   R15,R15            CHECK BLDL RETURN CODE
         BNZ   BLDLPROB

         MVC   MEM,MEMNAME        FILL IN OUTPUT FIELDS
         MVC   TTR,ITTR
         MVC   C,IALIASCT
         MVC   USERDATA,IUDATA

         CLOSE ((9),DISP)
         LTR   R15,R15            CHECK BLDL RETURN CODE
         BNZ   CLOSPROB

         FREE  UNALC,DDN=DDADDR
         REXIT

BLDLPROB STH   R15,0(R5)          SEND BACK BLDL R15 VALUE
         STH   R0,0(R6)           SEND ERROR REASON CODE BACK
         MVC   0(2,R7),HEXFFS     SET DUMMY INFO CODE
         REXIT

CHECKOUT STH   R15,0(R5)          SEND BACK DYNALLOC R15 VALUE
         DROP  R14
         USING S99RB,R14
         MVC   0(2,R6),S99ERROR   SEND ERROR REASON CODE BACK
         MVC   0(2,R7),S99INFO    SEND INFO  REASON CODE BACK
         REXIT

CLOSPROB STH   R15,0(R5)          SEND BACK OPEN R15 VALUE
         MVC   0(2,R6),HEXFES     SET DUMMY REASON CODE
         MVC   0(2,R7),HEXFES     SET DUMMY INFO CODE
         REXIT

NOMEMBER LA    R15,32             SEND BACK NO MEMBER SUPPLIED
         STH   R15,0(R5)
         STH   R15,0(R6)          SEND ERROR REASON CODE BACK
         STH   R15,0(R7)          SEND ERROR REASON CODE BACK
         REXIT

OPENPROB STH   R15,0(R5)          SEND BACK OPEN R15 VALUE
         MVC   0(2,R6),HEXFFS     SET DUMMY REASON CODE
         MVC   0(2,R7),HEXFFS     SET DUMMY INFO CODE
         REXIT

**       ALLOC MACRO PARAMETERS

DSADDR   DS    A
DSLEN    DC    H'44'              LENGTH  OF DSNAME (44)
DDADDR   DS    A
DDLEN    DC    H'08'              LENGTH  OF DDNAME (08)

**       BLDL PARAMETER LIST

BLDLPARM DS    0D
NUMENTRY DC    H'01'
LENENTRY DC    H'76'
MEMNAME  DS    CL8
ITTR     DS    CL3
ICONCAT  DC    X'00'
ILIBTYPE DC    X'00'
IALIASCT DC    X'00'
IUDATA   DS    CL62

BLANKS   DC    CL62' '
HEX00S   DC    X'0000'
HEXFFS   DC    X'FFFF'
HEXFES   DC    X'FFFE'

PDS      DCB   DSORG=PO,MACRF=R,DDNAME=$$DUMMY

         LTORG

         DYNSPACE
         IEFZB4D0
         IEFZB4D2

         DCBD  DSORG=(PO),DEVD=DA

**       OUTPUT OVERLAY

BLDLDATA DSECT
MEM      DS    CL8
TTR      DS    CL3
C        DS    CL1
USERDATA DS    CL62

         END
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*,
//             DCB=(RECFM=FM,LRECL=121,BLKSIZE=121)
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(2960,(111,12)),DCB=BLKSIZE=2960
//***
//L          EXEC     PGM=IEWL,COND=(5,LT),
//             PARM='XREF,LIST,LET,DCBS',
//             REGION=4M
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,
//             DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   99070900
  ENTRY    BLDLR
  NAME     BLDLR(R)
//***
