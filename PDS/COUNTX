/***  REXX -- SHOW # RECORDS IN FILE (UNDER ISPF OPTION 3.4)  ***/
   PARSE ARG ZINPDSN
   IF SUBSTR(ZINPDSN,1,1) = '?' THEN SIGNAL DISPDOC
   ISPEXEC VGET (ZDLRECFM) SHARED
   ISPEXEC VGET (ZDLLRECL) SHARED
   ISPEXEC VGET (ZDLBLKSZ) SHARED
   ISPEXEC VGET (ZDLDSORG) SHARED
   IF ZDLDSORG = "PS" | ZDLDSORG = "VS" | ZDLDSORG = "PS-E"
      THEN DO
         "ALLOC DD(INFILE)   DSN("ZINPDSN") SHR REUSE BUFNO(30)"
         "ALLOC DD(OUTFILE)  DUMMY BLKSIZE(32000) REUSE"
         "ALLOC DD(SYSPRINT) DSN(*) REUSE"
         CALL OUTTRAP "SYSPRINT." , "*"
         "REPRO INFILE(INFILE) OUTFILE(OUTFILE) "
         CALL OUTTRAP "OFF"
         DO II = 1 TO SYSPRINT.0
            PARSE VAR SYSPRINT.II " WAS " #RECS
            IF DATATYPE(#RECS) = "NUM" THEN LEAVE
         END II
         "FREE DD(INFILE OUTFILE)"
         "ALLOC DD(SYSPRINT) DSN(*) REUSE"
         $RECS = ""
         DO JJ = 3 TO 21 BY 3 WHILE JJ < LENGTH(#RECS)
            $RECS = "," || SUBSTR(#RECS,LENGTH(#RECS)-JJ+1,3) || $RECS
         END
         $RECS = SUBSTR(#RECS,1,LENGTH(#RECS)+3-JJ) || $RECS
         FINPDSN = LEFT(SUBSTR(ZINPDSN,2,LENGTH(ZINPDSN)-2),44)
         IF ZDLDSORG = "PS" THEN DO
            SAY FINPDSN"("LEFT(ZDLRECFM,4,' ')"/"ZDLLRECL"/"ZDLBLKSZ")",
               RIGHT($RECS,14)
         END
         ELSE DO
            SAY FINPDSN RIGHT($RECS,14)
         END
      END
      ELSE DO
         SAY "DSORG" ZDLDSORG "IS NOT SUPPORTED..."
      END
   EXIT

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "COUNTX - SHOW # OF RECORDS IN SEQ FILE UNDER DS LIST      "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    COUNTX  ( ? )                                         "
   SAY
   SAY "       OVERTYPED ON A DS LIST SEQUENTIAL OR VSAM DATA SET,"
   SAY "       COUNTX WILL DISPLAY THE DATA SET NAME (SINCE IT    "
   SAY "       CAN BE INVOKED ON MULTIPLE LINES) AND THE NUMBER   "
   SAY "       OF RECORDS IN THE FILE. IT WILL ALSO SUPPLY THE    "
   SAY "       RECORD FORMAT, LOGICAL RECORD LENGTH AND BLOCK     "
   SAY "       FOR SEQUENTIAL DATASETS.                           "
   EXIT(1)

