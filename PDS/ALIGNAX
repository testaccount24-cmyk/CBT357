/**  REXX -- ALIGN ALL COLUMNS BASED ON DELIMITER & REMOVE SAME     **/

   ADDRESS ISREDIT
   "MACRO (PARM) NOPROCESS"
   IF PARM = "" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      TOP = 1
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END
   IF SUBSTR(PARM,1,1) = '"' | SUBSTR(PARM,1,1) = "'",
      THEN PARM = TRANSLATE(SUBSTR(PARM,2,LENGTH(PARM)-2))
      ELSE PARM = TRANSLATE(PARM)

   OFFSET. = -1
   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      FF = 1
      OO = 0
      OX = 1
      DO WHILE(OX < LENGTH(REC))
         DO OX = (OO+1) TO LENGTH(REC) BY 1,
            WHILE( ( OX < LENGTH(REC) ),
               & ( SUBSTR(REC,OX,LENGTH(PARM)) <> PARM ) )
         END OX
         IF OX <= LENGTH(REC) THEN DO
            IF (OX-OO-1) > OFFSET.FF THEN OFFSET.FF = (OX-OO-1)
            OO = OX
            FF = FF + 1
         END
      END
   END NN

   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      FF = 1
      OO = 0
      OX = 1
      DO WHILE ( OX < LENGTH(REC) & OFFSET.FF <> -1 )
         DO OX = (OO+1) TO LENGTH(REC),
            WHILE( (OX < LENGTH(REC) ),
               & ( SUBSTR(REC,OX,LENGTH(PARM)) <> PARM ) )
         END OX
         LL = OFFSET.FF
         REC = SUBSTR(REC,1,OX-1) || COPIES(" ",LL-(OX-OO-1)),
            || PARM || SUBSTR(REC,OX+1)
         OO = OX + LL - ( OX - OO - 1 )
         FF = FF + 1
      END
      LL = 1
      RECX = ""
      DO XX = 1 TO 32760 WHILE ( OFFSET.XX <> -1 )
         MM   = OFFSET.XX
         RECX = RECX || SUBSTR(REC,LL,MM)
         LL   = LL + 1 + OFFSET.XX
      END XX
      "LINE " NN " = (RECX)"
   END NN

   "LOCATE" TOP
   RETURN(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "ALIGNAX -- ALIGN ALL COLUMNS BASED ON DELIMITER           "
   SAY
   SAY " FORMAT "
   SAY "    ALIGNAX   CHAR                                        "
   SAY "       WILL ALIGN THE SPECIFIED LINES AT THE CHARACTER    "
   SAY "       OR CHARACTERS PROVIDED.  A RANGE OF LINES MAY BE   "
   SAY "       SPECIFIED BY C/CC/C## OR THE ENTIRE FILE WILL BE   "
   SAY "       PROCESSED.  NO CHAR SPECIFIED WILL CAUSE THIS      "
   SAY "       HELP TO BE SHOWN. QUOTES MAY BE USED. THE DELIMITER"
   SAY "       WILL BE REMOVED.                                   "
   SAY
   SAY " EXAMPLE "
   SAY "    ALIGNAX  !                                            "
   SAY "       ON THE FOLLOWING:                                  "
   SAY "          111!111111111!1                                 "
   SAY "          2!2!                                            "
   SAY "          33333!33333!55555                               "
   SAY "       WILL ALIGN AS:                                     "
   SAY "          111  !111111111!1                               "
   SAY "          2    !2        !2                               "
   SAY "          33333!33333    !33333                           "
   EXIT(1)

