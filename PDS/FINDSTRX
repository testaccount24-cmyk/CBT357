/**  REXX - INVOKE SUPERC TO SCAN FOR A STRING  **/

   MSG = MSG(OFF)
   PARSE ARG PARMS

   STRING = ''
   DSN    = ''

   DO II = (LENGTH(PARMS)-1) TO 1 BY -1,
      WHILE (SUBSTR(PARMS,II,1) <> "'")
   END
   DSN = SUBSTR(PARMS,II)
   PARMS = STRIP(SUBSTR(PARMS,1,LENGTH(PARMS)-LENGTH(DSN)),'B')
   IF PARMS = "" | PARMS = "?" THEN SIGNAL DISPDOC
   IF SUBSTR(PARMS,1,1) = "'",
         & SUBSTR(PARMS,LENGTH(PARMS),1) = "'"
      THEN STRING = PARMS
      ELSE STRING = "'" || PARMS || "'"

   "FREE  FILE(NEWDD OUTDD SYSIN)"
   "ALLOC FILE(NEWDD) DA("DSN") SHR REUSE"
   ADDRESS ISPEXEC "VGET ("ZSSFLDS") ASIS"
   IF RC <> 0 | ZSSFLDS = 'ZSSFLDS' THEN ZSSFLDS = 'SRCHFOR.LIST'
   "ALLOC FILE(OUTDD) DA("ZSSFLDS") SHR REUSE"
   IF RC <> 0 THEN DO
     "ALLOC FILE(OUTDD) DA("ZSSFLDS")" ,
           " NEW CATALOG TRACKS SPACE(30,30)",
           " REUSE LRECL(133) RECFM(F B) UNIT(SYSDA)"
   END
   "ALLOC FILE(SYSIN) NEW TRACKS SPACE(1,1) DELETE",
         " REUSE LRECL(80) RECFM(F B) BLKSIZE(3120) UNIT(SYSDA)"
   QUEUE "SRCHFOR" STRING
   "EXECIO 1 DISKW SYSIN  (FINIS"

   SAY "SEARCHING FOR "STRING" IN" DSN
   ADDRESS ISPEXEC "SELECT PGM(ISRSUPC)" ,
         "PARM(SRCHCMP,ANYC)"

   IF RC <> 0 THEN ADDRESS ISPEXEC ,
     "VIEW DATASET("ZSSFLDS")"
         /*** "BROWSE DATASET("ZSSFLDS")" ***/
   ELSE SAY 'STRING' STRING 'WAS NOT FOUND IN' DSN

   "FREE FILE(NEWDD OUTDD SYSIN)"
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY
   SAY "  FINDSTR   INVOKES SUPERC AGAINST THE SPECIFIED PDS         "
   SAY "            SEARCHING FOR THE STRING TYPED.  THE STRING IS   "
   SAY "            MANDATORY AND MAY BE WITHIN SINGLE QUOTES (').   "
   SAY "            THE OUTPUT WILL BE VIEWED IF ANY FOUND.          "
   SAY
   SAY "  SYNTAX:   FINDSTR  SOME-STRING                             "
   SAY
   SAY "            SOME-STRING IS THE CHARACTER SEQUENCE FOR WHICH  "
   SAY "            THE PDS WILL BE SCANNED. SOME ODD CHARACTERS     "
   SAY "            SUCH AS CLOSE PARENTHESIS MAY CAUSE PROBLEMS     "
   SAY "            AND SHOULD BE IN SINGLE QUOTES, E.G.             "
   SAY "               FINDSTR 'TRANSLATE('                          "
   SAY "            BLANKS SHOULD BE WITHIN QUOTES.                  "
   SAY
   RETURN
