/**  REXX -- FIND NEXT EXCLUDED LINE  **/
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'FEXC EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      RETURN
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS) NOPROCESS"
      PARMS = TRANSLATE(PARMS)
   END
   ELSE DO
      SAY 'FEXC EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      RETURN
   END

   PARMS     = TRANSLATE(PARMS)
   IF PARMS = '?' THEN SIGNAL DISPDOC
   IF PARMS = '-=PFK=-' THEN DO
      ADDRESS ISPEXEC "VGET (FNSTART) SHARED"
      START = FNSTART
      SIGNAL REPEAT
   END

   "ISREDIT (SAVELINE,SAVECOL) = CURSOR"
   START = SAVELINE

REPEAT:

   ADDRESS ISREDIT "(END)   = LINENUM .ZLAST"

   DO PNTR = START TO END
      "ISREDIT (LINESTAT) = XSTATUS" PNTR
      IF LINESTAT = "X" THEN DO
         STR = 'FOUND!'
         CALL SETMSG '* NO ' STR '$FOUND EXCLUDED LINES!'
         IF PNTR = 1 THEN DO
            "ISREDIT UP MAX"
         END
         ELSE DO
            WHERE = PNTR - 1
            "ISREDIT LOCATE" WHERE
         END
         DO II  =  (PNTR+1)  TO  END  WHILE  LINESTAT = 'X'
            "ISREDIT (LINESTAT) = XSTATUS" II
         END
         FNSTART = II
         ADDRESS ISPEXEC "VPUT (FNSTART) SHARED"
         FALTNM = 'FEXC'
         ADDRESS ISPEXEC "VPUT (FALTNM)  SHARED"
         EXIT 1
      END
   END PNTR

   STR = 'NO X FOUND!'
   CALL SETMSG '* NO ' STR '$NO EXCLUDED LINES FOUND!'
   EXIT 1

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "FEXC - FIND EXCLUDED LINE"
   SAY
   SAY " FORMAT "
   SAY "   FEXC                                              "
   SAY
   SAY "       WILL FIND THE NEXT EXCLUDED LINE, IF ANY,     "
   SAY "       FROM THE TOP OF THE CURRENT SCREEN DISPLAY.   "
   SAY
   RETURN
