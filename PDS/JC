/**  REXX -- JC -- CREATE A JOB CARD AT THE TOP OF THE FILE EDITED  **/
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'JC EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS) NOPROCESS"
      PARMS = TRANSLATE(PARMS)
      IF PARMS = "?" THEN SIGNAL DISPDOC
   END
   ELSE DO
      SAY 'JC EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END

   ADDRESS ISREDIT
   CARD3 = "//***"
   CARD2 = "//             CLASS=2,MSGCLASS=X,NOTIFY="USERID()
   CARD1 = "//"USERID()"   JOB      (ACCT-INFO),'"USERID()"',"
   LINE  = 0
   "LINE_AFTER (LINE) = (CARD3)"
   "LINE_AFTER (LINE) = (CARD2)"
   "LINE_AFTER (LINE) = (CARD1)"
   COL_X = 3 + LENGTH(USERID())
   "CURSOR ="  1  COL_X
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "JC  -  INSERT JOB CARD AT TOP OF EDITED FILE              "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    JC  ( ? )                                             "
   SAY
   SAY "       INSERT A JOB CARD AT THE TOP OF THE FILE BEING     "
   SAY "       EDITED, USING THE TSO USER-ID AS THE PREFIX OF     "
   SAY "       THE JOBNAME, THE NOTIFIED PARTY, AND THE USER     "
   SAY "       NAME WITHIN THE NAME FIELD.                       "
   SAY
   SAY "       A QUESTION MARK ALONE CAUSES THIS HELP TO DISPLAY. "
   SAY
   EXIT(1)

