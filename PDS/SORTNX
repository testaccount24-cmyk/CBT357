/**  REXX -- SORT UNEXCLUDED LINES KEEPING EXCLUDED LINES ATTACHED  **/

   SUBCOM ISREDIT
   IF  RC = 0  THEN DO
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS)"
      IF  RC = 0  THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF  DSN = 'DSN'  THEN DO
      SAY 'SORTNX MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END

   ADDRESS "ISREDIT"
   PARMS   = TRANSLATE(PARMS)
   PARMCNT = 0
   COL.    = ""
   LBL1    = ""
   LBL2    = ""
   DO II = 1 TO 16;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LBL1 = "" THEN LBL1 = TOKEN
         ELSE IF LBL2 = "" THEN LBL2 = TOKEN
            ELSE DO
               ZEDSMSG = ".LABEL RC=8"
               ZEDLMSG = "TOO MANY LABELS --" LBL1 LBL2 TOKEN
               ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
               EXIT 8
            END
      END
      ELSE DO
         PARMCNT     = PARMCNT + 1
         COL.PARMCNT = TOKEN
      END
   END II
   IF LBL1 <> ""  &  LBL2 = ""  THEN LBL2 = LBL1
   IF (PARMCNT // 2) <> 0 THEN DO
      SAVER       = COL.PARMCNT
      PARMCNT     = PARMCNT + 1
      COL.PARMCNT = SAVER
   END

   ERRORMSG = ''
   MESG     = ''
   IF  PARMCNT = 0 THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   DO II = 1 TO PARMCNT
      IF  Â¬DATATYPE(COL.II,'W') | COL.II = 0 THEN DO
         MESG = "'"COL.II"' COLUMN VALUE IS INVALID!"
         SIGNAL ERROR
      END
      IF  COL.II > LRECL  THEN DO
         MESG = "'"COL.II"' IS GREATER THAN RECORD SIZE("LRECL")"
         SIGNAL ERROR
      END
   END II
   DO II = 1 TO PARMCNT BY 2
      JJ = II + 1
      IF  COL.II > COL.JJ THEN DO
         MESG = "LEFT-COL "COL.II" IS GREATER THAN RIGHT-COL "COL.JJ
         SIGNAL ERROR
      END
   END II

   IF  LBL1 <> ''  THEN DO
      "ISREDIT L &LBL1"
       IF  RC <>  0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
   END

   IF  LBL2 <> ''  THEN DO
      "ISREDIT L &LBL2"
       IF  RC <>  0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
       END
   END

   IF  LBL1 <> ''  THEN  "ISREDIT (START) = LINENUM &LBL1"
                   ELSE  START = 1
   IF  LBL2 <> ''  THEN  "ISREDIT (END) = LINENUM &LBL2"
                   ELSE  "ISREDIT (END) = LINENUM .ZLAST"

   "ISREDIT ("ANUM") = AUTONUM"
   "ISREDIT NUMBER OFF "

   STEMCNT = 0
   DO HH = START TO END
      "(LINESTAT) = XSTATUS" HH
      IF LINESTAT = 'NX' THEN DO
         "ISREDIT (RECORD)  = LINE" HH
         STEMCNT = STEMCNT + 1
         IF STEMCNT = 1 THEN START = HH
         STEM.STEMCNT = SUBSTR(RECORD,COL.1,COL.2-COL.1+1)
         DO II = 3 TO PARMCNT BY 2
            JJ = II + 1
            STEM.STEMCNT = STEM.STEMCNT,
               || SUBSTR(RECORD,COL.II,COL.JJ-COL.II+1)
         END II
         STEM.STEMCNT = STEM.STEMCNT || RIGHT(HH,6,'0')
      END
   END HH
   IF STEMCNT = 0 | STEMCNT = 1 THEN SIGNAL TERM

   HH = 1
   DO WHILE HH <= (STEMCNT)
      HH = ( 3 * HH ) + 1
   END
   DO WHILE HH > 0
      DO II = ( HH + 1 ) TO STEMCNT
         TT = STEM.II
         JJ = II
         KK = JJ - HH
         DO WHILE ( JJ > HH ) & ( STEM.KK >> TT )
            STEM.JJ = STEM.KK
            JJ = JJ - HH
            KK = JJ - HH
         END
         STEM.JJ = TT
      END
      HH = HH % 3
   END


   DO II = STEMCNT TO 1 BY -1
      JJ = SUBSTR(STEM.II,LENGTH(STEM.II)-5)
      "ISREDIT (RECORD) = LINE" JJ
      "ISREDIT LINE_AFTER &END = (RECORD)"
      KK = JJ + 1
      "(LINESTAT) = XSTATUS" KK
      LL = END + 1
      DO WHILE LINESTAT = 'X'
         "ISREDIT (RECORD) = LINE" KK
         "ISREDIT LINE_AFTER &LL = (RECORD)"
         KK = KK + 1
         LL = LL + 1
         "XSTATUS" LL "= X"
         "(LINESTAT) = XSTATUS" KK
      END
   END
   "ISREDIT DELETE" START END


TERM:
   CALL SETMSG '* NO ' STEMCNT 'RECORD SETS $UNEXCLUDED RECORDS SORTED'
   IF START = 1 THEN "ISREDIT LOCATE" 0
                ELSE "ISREDIT LOCATE" START
   "ISREDIT AUTONUM = "ANUM
    SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      ERRORMSG = '===>' MESG
   END
   SIGNAL DISPDOC

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

EOJ:
   RETURN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SORTNX - SORT UNEXCLUDED LINES KEEPING ATTACHED EXCLUDED LINES"
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY
   SAY "    SORTNX  COL1 COL2  ...  ( .LABEL1 .LABEL2) "
   SAY
   SAY "       ONE PAIR OF COLUMNS IS MANDATORY. UP TO 10 PAIR  "
   SAY "       MAY BE SPECIFIED. THE LABEL RANGE IS OPTIONAL    "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    SORTNX  17 19  4 11 "
   SAY "       WILL SORT THE ENTIRE NON EXCLUDED FILE BY COLUMNS"
   SAY "       17 THRU 19 AND 4 THRU 11, PRESERVING ANY EXCLUDED"
   SAY "       LINES IMMEDIATELY FOLLOWING EACH NON EXCLUDED    "
   SAY "       LINE.                                            "
   SAY
   RETURN
