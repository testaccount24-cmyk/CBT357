/**  REXX -- VIEW  **/
   ZEDLMSG = ""
   RSLT = ""
   SAVEMSG = MSG()
   if SAVEMSG = 'ON'  then  X = MSG("OFF")
   MSGTYPE = "ISRZ001"
   X = ADDRESS()
   SUBCOM ISREDIT
   if RC <> 0  then do
        UMODE = "NATIVE"
        ARG FNAME VOLSER
        SETMSG = "     Must be run under ISPF"
        call SETM
        RSLT = " NOT in ISPF - UNABLE to run."
        signal BYEBYE
   end
   address ISPEXEC "CONTROL ERRORS RETURN"
   address ISREDIT "ISREDIT MACRO ("FNAME","VOLSER")"
   if RC <> 0  then do
        UMODE = "ISPEXEC"
        ARG FNAME VOLSER
   end
   else  do
      UMODE = "ISREDIT"
      address ISREDIT "ISREDIT ("LIN","COL") = CURSOR"
   end

CHKDSN:
   WFILE = ""
   if FNAME = ""  then do
      SETMSG = "     You must supply the DSN to VIEW.",
         "  I.E. VIEW 'some.filename'"
      call SETM
      RSLT = "NO DSN SUPPLIED"
      signal BYEBYE
   end
   if substr(FNAME,1,1)="'" & substr(FNAME,length(FNAME),1)="'" then do
      LEN = length(FNAME)-2
      WFILE = substr(FNAME,2,LEN)
      signal BROWSIT
   end
   call UPROFREX
   PROFILE = RESULT
   if index(PROFILE,'NOPREFIX') = 0    then  WFILE = USERID()"."FNAME
   else WFILE = FNAME

BROWSIT:
   upper WFILE
   if VOLSER = ""  &  sysdsn(''''WFILE'''') <> 'OK'  then do
      SETMSG= "     Can't find "WFILE". It may not be CATALOGED or",
              "you didn't spell the file correctly, or you didn't use",
              "QUOTES if they were required, or file may be HELD by ",
              "another task."
      call SETM
      RSLT = "NOT FOUND"
      signal BYEBYE
   end
   if VOLSER = "" then Address ISPEXEC "SELECT PGM(ISPSTRT)",
      "PARM(CMD(ISREPDF '"WFILE"' V))"
   else address ISPEXEC "VIEW DATASET('"WFILE"') VOLUME("VOLSER")"
   BRC = RC
   if BRC = 0  then do
      RSLT = "FOUND"
      signal OKVIEW
   end

   X = VSAMCHK(WFILE)
   if X = 1  then do
      SETMSG = "     "WFILE" is a VSAM file. Cannot VIEW it."
      call SETM
      RSLT = "NO ACTION-VSAM FILE"
      signal BYEBYE
   end

   VOLS = GVOLS(WFILE)
   VOLCNT = words(VOLS)
   if VOLCNT = 1  then do
      if VOLSER <> "" then VOLDATA = "VOLUME("VOLSER")"
      else VOLDATA = ""
      D=LISTDSI((FNAME) VOLDATA 'NODIRECTORY')
      if D <> 0  then do
         SETMSG = "     "WFILE", VOL "VOLDATA" LISTDSI error. RC=",
                 D". "SYSMSGLVL2
         call SETM
         RSLT = "LISTDSI ERR. RC="D
         signal BYEBYE
      end
      if SYSUSED = 0  then do
         SETMSG = "     "WFILE" contains NO data. Nothing to VIEW."
         call SETM
         RSLT = "NO ACTION-EMPTY FILE"
         signal BYEBYE
      end
      if substr(SYSDSORG,1,1)<>'P' then do
         SETMSG = "     Can't VIEW "WFILE".  DSORG="SYSDSORG"."
         call SETM
         RSLT = "NO ACTION-DSORG="SYSDSORG
         signal BYEBYE
      end
   end

   VOLSER = word(VOLS,VOLCNT)

   SETMSG = "     "WFILE" was multi-volume, residing on "VOLS"."

   call SETM
   address ISPEXEC "VIEW DATASET('"WFILE"') VOLUME("VOLSER")"
   BRC = RC
   if BRC = 0  then do
      RSLT=" ON "VOLCNT" VOLS"
      signal OKVIEW
   end

   QUIT = 'N'
   do V = VOLCNT TO 1 BY -1
      if QUIT='Y' then
         NOP
      else do
         VOLSER = word(VOLS,V)
         address ISPEXEC "VIEW DATASET('"WFILE"') VOLUME("VOLSER") "
         BRC = RC
         if BRC = 0 then QUIT = "Y"
      end
   end

   if BRC = 0 then do
      RSLT = " ON "VOLCNT" VOLS-TRUNC"
      signal OKVIEW
   end

HUH:
   SETMSG = "     "WFILE" on "VOLS" cannot be VIEWED.",
            "'VIEW' RC="BRC
   call SETM
   RSLT = "NO ACTION-VIEW ERROR RC =" BRC
   signal BYEBYE

OKVIEW:
   MSGTYPE = "ISRZ000"
   signal BYEBYE

BYEBYE:
   if UMODE <> "NATIVE" then address ispexec "SETMSG MSG("MSGTYPE")"
   else say ZEDLMSG
   if SAVEMSG='ON'  then X=MSG("ON")
   EXIT  0

SETM:
   if UMODE <> "NATIVE" then
      ZEDLMSG = center(REX||VERSION,74,' ')
   else
      ZEDLMSG = center(REX||VERSION,77,' ')
   ZEDLMSG = ">"ZEDLMSG"<"
   ZEDLMSG = ZEDLMSG || SETMSG
   return
