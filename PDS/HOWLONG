/**  REXX -- SHOW SHOW SHORTEST & LONGEST REC SIZE UNDER ISPF 3.4)  **/
/**          FOR FIXED, LONGEST HAS THE LEAST NUMBER OF BLANKS ON   **/
/**          THE RIGHT; SHORTEST HAS THE GREATEST                   **/
   PARSE ARG ZINPDSN
   MSG = MSG(OFF)
   IF SUBSTR(ZINPDSN,1,1) = '?' THEN SIGNAL DISPDOC
   "FREE  F(INFILE)"
   "ALLOC DSN("ZINPDSN") F(INFILE) SHR BUFNO(30)"
   "EXECIO 0 DISKR" INFILE "(OPEN"
   LONGEST_SIZE  = 0
   SHORTEST_SIZE = 32767
   "EXECIO 100 DISKR INFILE(STEM RECS."
   RETCODE = RC

   LDSI = LISTDSI(ZINPDSN)
   IF INDEX(SYSRECFM,'F') <> 0 THEN DO
                   /* FOR FIXED, COUNT # BLANKS AT END */
                   /* RATHER THEN ACTUAL RECORD LENGTH */
      LL = SYSLRECL
      DO WHILE(RETCODE = 0)
         DO II = 1 TO RECS.0
            DO JJ = LL TO 1 BY -1 WHILE(SUBSTR(RECS.II,JJ,1) = ' ')
            END
            IF JJ > LONGEST_SIZE,
               THEN LONGEST_SIZE = JJ
            IF JJ < SHORTEST_SIZE,
               THEN SHORTEST_SIZE = JJ
         END
         "EXECIO 100 DISKR INFILE(STEM RECS."
         RETCODE = RC
      END

      DO II = 1 TO RECS.0
         DO JJ = LL TO 1 BY -1 WHILE(SUBSTR(RECS.II,JJ,1) = ' ')
         END
         IF JJ > LONGEST_SIZE
            THEN LONGEST_SIZE = JJ
         IF JJ < SHORTEST_SIZE
            THEN SHORTEST_SIZE = JJ
      END

      "EXECIO 0 DISKR" INFILE "(FINIS"
      SAY ZINPDSN "- LONGEST/SHORTEST IS" LONGEST_SIZE"/"SHORTEST_SIZE
      SAY "  ----- BASED ON BLANKS AT END OF RECORDS (FIXED DATA SET)"
      "FREE  F(INFILE)"
      IF RETCODE = 2 THEN EXIT(0)
                     ELSE EXIT(RETCODE)
   END
   ELSE DO
      DO WHILE(RETCODE = 0)
         DO II = 1 TO RECS.0
            IF LENGTH(RECS.II) > LONGEST_SIZE,
               THEN LONGEST_SIZE = LENGTH(RECS.II)
            IF LENGTH(RECS.II) < SHORTEST_SIZE,
               THEN SHORTEST_SIZE = LENGTH(RECS.II)
         END
         "EXECIO 100 DISKR INFILE(STEM RECS."
         RETCODE = RC
      END

      DO II = 1 TO RECS.0
         IF LENGTH(RECS.II) > LONGEST_SIZE
            THEN LONGEST_SIZE = LENGTH(RECS.II)
         IF LENGTH(RECS.II) < SHORTEST_SIZE
            THEN SHORTEST_SIZE = LENGTH(RECS.II)
      END

      "EXECIO 0 DISKR" INFILE "(FINIS"
      SAY ZINPDSN "- LONGEST/SHORTEST IS" LONGEST_SIZE"/"SHORTEST_SIZE
      "FREE  F(INFILE)"
      IF RETCODE = 2 THEN EXIT(0)
                     ELSE EXIT(RETCODE)
   END

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "HOWLONG - DISPLAY MAX AND MIN RECORD LENGTHS UNDER DS LIST"
   SAY
   SAY " FORMAT "
   SAY
   SAY "    HOWLONG                                                  "
   SAY
   SAY "       WILL CAUSE A MESSAGE TO BE WRITTEN AS:                "
   SAY
   SAY "          '-THE-DATASET-NAME-' - LONGEST/SHORTEST IS 860/204 "
   SAY
   SAY "       THE LENGTHS DO NOT INCLUDE THE 4 BYTE LENGTH FIELD    "
   SAY "       NOR THE 4 BYTE BLOCK LENGTH FIELD FOR VARIABLE        "
   SAY "       FILES.  SUBSEQUENT FILES MAY BE INDICATED USING       "
   SAY "       THE EQUAL SIGN AS IN NORMAL DS LIST USAGE. NOTE THAT  "
   SAY "       THE ACTUAL RECORD LENGTH IS OFTEN FAR LARGER THEN     "
   SAY "       THE LONGEST RECORD ON THE FILE.                       "
   SAY
   EXIT(1)

