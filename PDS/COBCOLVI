/**  REXX  -- VIEW COBOL COLUMN OFFSETS FROM INSYNC               **/

/*  SET UP ISPF ENVIRONMENT AND DETERMINE STARTING/ENDING LINES    */
   SAVEMSG=MSG()
   X=MSG("OFF")
   X=ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY "COBCOLV MUST BE RUN UNDER ISPF EDIT!"
      SIGNAL QUIT
   END
   SUBCOM ISREDIT
   UMODE = "ISREDIT"
   ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
   ADDRESS ISREDIT
   "MACRO (PARM) NOPROCESS"
   IF PARM = "?" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      "(TOP) = LINENUM .ZFIRST"
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END

/*  DELETE OLD TEMP PDS FILE, IF ANY, AND ALLOCATE NEW ONE         */
   ADDRESS TSO
   "FREE DD(DDXX #INDD)"
   "DELETE $$$TEMP.COPYBOOK.PDS"
   "ALLOCATE DD(#INDD) DSN($$$TEMP.COPYBOOK.PDS) NEW UNIT(SYSDA)",
      "CYL SPACE(01 01) RECFM(F B) LRECL(80) BLKSIZE(27920) DIR(12)"
   "ALLOCATE DD(DDXX) DSN($$$TEMP.COPYBOOK.PDS(DUMMYMEM)) SHR"

/*  WRITE LINES IN SPECIFIED RANGE TO TEMP PDS FILE AS MEMBER      */
   "EXECIO 0 DISKW" DDXX "(OPEN"
   GOT_FIRST = 'NO'
   DO II = TOP TO BOT BY 1
      "ISREDIT (RECORD) = LINE &II"
      IF SUBSTR(RECORD,7,1) <> '*',
               & SUBSTR(RECORD,7,1) <> '/',
               & SUBSTR(RECORD,8,65) <> ' ' THEN DO
         IF GOT_FIRST = 'NO' THEN DO
            IF INDEX(SUBSTR(RECORD,8,4),'1') = 0 THEN DO
               DUMMY_REC = '       01  DUMMY-01-LEVEL.'
               PUSH DUMMY_REC
               ADDRESS TSO "EXECIO 1 DISKW" DDXX
            END
            GOT_FIRST = 'YES'
         END
         BB = INDEX(RECORD,'( ');
         DO WHILE ( BB <> 0 );
            RECORD = SUBSTR(RECORD,1,BB) || SUBSTR(RECORD,BB+2)
            BB = INDEX(RECORD,'( ');
         END
         PUSH RECORD
         ADDRESS TSO "EXECIO 1 DISKW" DDXX
      END
   END
   ADDRESS TSO
   "EXECIO 0 DISKW" DDXX "(FINIS"
   "FREE DD(DDXX)"

/*  FREE & ALLOCATE FILES FOR CALL TO INSYNC                       */
   "FREE DD(#PARM #PRINT XPRINT #LOG SYSTSPRT #ELIBDD)"
   "DELETE $$$TEMP.PARM.FILE"
   "ALLOC DD(#PARM) DSN($$$TEMP.PARM.FILE) NEW TRACKS SPACE(1)",
      "UNIT(SYSDA) LRECL(80) BLKSIZE(800) RECFM(F B)"
   QUEUE "FUNCTION=LAYOUT"
   QUEUE "INDD=#INDD"
   QUEUE "MEMBER=DUMMYMEM"
   QUEUE "LINESPERPAGE=999"
   "EXECIO" QUEUED() "DISKW #PARM (FINIS"
   "DELETE $$$TEMP.PRINT.FILE"
   "ALLOCATE DD(#PRINT) DSN($$$TEMP.PRINT.FILE) NEW UNIT(SYSDA)",
      "CYL SPACE(10,10) RECFM(F B A) LRECL(133) BLKSIZE(27930)",
      "RELEASE"
   "DELETE $$$TEMP.LOG.FILE"
   "ALLOCATE DD(#LOG) DSN($$$TEMP.LOG.FILE) NEW UNIT(SYSDA)",
      "CYL SPACE(01,01) RECFM(F B A) LRECL(133) BLKSIZE(27930)"
   "ALLOCATE DD(SYSTSPRT) DUMMY"
   "ALLOCATE DD(#ELIBDD) DSN('TCH.PROD.INSYNC.ELIB') SHR"
   "CALL 'TCH.PROD.INSYNC.LOADLIB(INSYNC)' 'MVS'"

/*  SHRINK PRINTED OUTPUT TO FIT WITHIN 72 COLUMNS               */
   "DELETE $$$TEMP.PRINTX.FILE"
   "ALLOCATE DD(XPRINT) DSN($$$TEMP.PRINTX.FILE) NEW UNIT(SYSDA)",
      "CYL SPACE(01,01) RECFM(F B) LRECL(72) BLKSIZE(27936)"
   "EXECIO 0 DISKR" #PRINT "(OPEN"
   "EXECIO 0 DISKW" XPRINT "(OPEN"
   "EXECIO 1 DISKR" #PRINT
   RETCODE = RC

   DO WHILE ( RETCODE = 0 )
      PULL PRTLINE
      IF SUBSTR(PRTLINE,1,1) = "1" THEN DO
         "EXECIO 1 DISKR" #PRINT
         PULL PRTLINE
         "EXECIO 1 DISKR" #PRINT
         PULL PRTLINE
         "EXECIO 1 DISKR" #PRINT
         PULL PRTLINE
         "EXECIO 1 DISKR" #PRINT
         PULL PRTLINE
         "EXECIO 1 DISKR" #PRINT
         RETCODE = RC
      END
      ELSE DO
         IF SUBSTR(PRTLINE,63,05) <> "VALUE" THEN DO
            SHRUNK = SUBSTR(PRTLINE,11,51),
               || SUBSTR(PRTLINE,63,09),
               || SUBSTR(PRTLINE,76,12)
         END
         ELSE DO
            SHRUNK = SUBSTR(PRTLINE,11,51),
               || SUBSTR(PRTLINE,63,21)
         END
         PUSH SHRUNK
         "EXECIO 1 DISKW" XPRINT
         "EXECIO 1 DISKR" #PRINT
         RETCODE = RC
      END
   END
   "EXECIO 0 DISKR" #PRINT "(FINIS"
   "EXECIO 0 DISKW" XPRINT "(FINIS"

/*  VIEW SHRUNKEN PRINT FILE  */
   ADDRESS ISPEXEC "SELECT PGM(ISPSTRT)",
      "PARM(CMD(ISREPDF "$$$TEMP.PRINTX.FILE" V))"
   IF RC <> 0 THEN ADDRESS ISPEXEC "SETMSG MSG("ZERRMSG")"
   "FREE DD(#PARM #INDD #PRINT XPRINT #LOG SYSTSPRT #ELIBDD)"
   "DELETE $$$TEMP.PARM.FILE"
   "DELETE $$$TEMP.PRINT.FILE"
   "DELETE $$$TEMP.PRINTX.FILE"
   "DELETE $$$TEMP.LOG.FILE"
   "DELETE $$$TEMP.COPYBOOK.PDS"
   EXIT 0

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "COBCOLV - VIEW INSYNC COBOL STRUCTURE OFFSETS             "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    COBCOLV  ( ? )                                        "
   SAY
   SAY "       INVOKES INSYNC AGAINST A COBOL STRUCTURE AND VIEWS "
   SAY "       THE RESULTING OUTPUT IN A SEPARATE SCREEN.         "
   SAY
   SAY "       A QUESTION MARK WILL CAUSE THIS HELP TO BE SHOWN.  "
   SAY "       C/C#/CC PREFIX COMMANDS MAY BE USED TO LIMIT THE   "
   SAY "       LINES PROCESSED.                                   "
   SAY
   EXIT(1)

