//-YOUR-USERID-D JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  BASIC PL/I TO DYNAMICALLY ALLOCATE A SYSOUT AND WRITE A
//***  FILE'S CONTENT TO IT
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 BASE:  PROC OPTIONS(MAIN) REORDER;

        DECLARE DYNS   ENTRY(CHAR(8),       /* DDNAME               */
                             CHAR(1),       /* SYSOUT CLASS         */
                             FIXED BIN(15), /* DYNALLOC R15 R/C     */
                             FIXED BIN(15), /* DYNALLOC ERROR CODE  */
                             FIXED BIN(15)) /* DYNALLOC REASON CODE */
                       OPTIONS(ASM);

        DECLARE DYNF   ENTRY(CHAR(08) )     /* DDNAME            */
                       OPTIONS(ASM);

        DCL  SYSOUT    FILE OUTPUT RECORD SEQL
                       ENV(F,RECSIZE(121),BLKSIZE(121),TOTAL);
        DCL  REC       CHAR(00080) STATIC;
        DCL  OREC      CHAR(00121) STATIC;
        DCL  I_EOF_IND CHAR(01) STATIC INIT('N');

        DCL  READ#     FIXED BIN(31) STATIC INIT(0);
        DCL  WRITTEN#  FIXED BIN(31) STATIC INIT(0);
        DCL  DATE      BUILTIN;
        DCL  TIME      BUILTIN;

        DCL  DDNAME    CHAR(08) STATIC INIT('SYSOUT');
        DCL  SYSCLS    CHAR(01) STATIC INIT('X');
        DCL  DYNA_R15  FIXED BIN(15) STATIC INIT(0);
        DCL  DYNA_RC   FIXED BIN(15) STATIC INIT(0);
        DCL  DYNA_WHY  FIXED BIN(15) STATIC INIT(0);


        CALL DYNS(DDNAME,SYSCLS,DYNA_R15,DYNA_RC,DYNA_WHY);

        IF DYNA_R15 = 0
           THEN DO;
              ON ENDFILE(I) I_EOF_IND = 'Y';

              READ FILE(I) INTO(REC);
              DO WHILE ( I_EOF_IND  = 'N' );
                 CALL READ_WRITE_LOOP;
                 END;

              CALL TERMINATION;
              CALL DYNF(DDNAME);
              CALL PLIRETC(DYNA_R15);

              END;
           ELSE DO;
              PUT EDIT ( '**************************************' )
                       ( SKIP(1), A );
              PUT EDIT ( '***  ', DATE, ' STATISTICS ', TIME, '  ****' )
                       ( SKIP(1), A, A, A, A, A );
              PUT EDIT ( '**************************************' )
                       ( SKIP(1), A );
              PUT EDIT ( '***  DYNALLOC ERROR  ***'                )
                       ( SKIP(2), A );
              PUT EDIT ( '***  DDNAME:                 ', DDNAME   )
                       ( SKIP(2), A, A );
              PUT EDIT ( '***  SYSOUT CLASS:           ', SYSCLS   )
                       ( SKIP(2), A, A );
              PUT EDIT ( '**************************************' )
                       ( SKIP(1), A );
              PUT EDIT ( '***  DYNALLOC R15 RET VALUE: ', DYNA_R15 )
                       ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '***  DYNA ERROR:             ', DYNA_RC  )
                       ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '***  DYNA REASON:            ', DYNA_WHY )
                       ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '**************************************' )
                       ( SKIP(1), A );
              CALL PLIRETC(DYNA_R15);
              END;


 READ_WRITE_LOOP: PROC;
        READ# = READ# + 1;
        OREC = ' ' || REC;
        WRITE FILE(SYSOUT) FROM(OREC);
        WRITTEN# = WRITTEN# + 1;
        READ FILE(I) INTO(REC);
        END READ_WRITE_LOOP;

 TERMINATION: PROC;
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  ', DATE, ' STATISTICS ', TIME, '  ****' )
                 ( SKIP(1), A, A, A, A, A );
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  NUMBER RECORDS READ:    ', READ#    )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS WRITTEN: ', WRITTEN# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        END TERMINATION;

        END BASE;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//GO         EXEC     PGM=LOADER,
//             PARM='MAP,PRINT//',
//             REGION=4M,COND=(5,LT)
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//             DD       DISP=SHR,DSN=-YOUR-LOADLIB-
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//SYSLOUT      DD       SYSOUT=*
//SYSPRINT     DD       SYSOUT=*
//I            DD       DISP=SHR,DSN=-YOUR-SYSLIB-(TESTIP)
//***
