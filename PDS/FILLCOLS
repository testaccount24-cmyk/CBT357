/**  REXX -- OVERLAY FILL DATA ONTO SPECIFIC COLUMN LOCATIONS  **/
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'FILLCOLS MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
             "ISREDIT MACRO (TOLEFT,TORIGHT,CONTENT,LBL1,LBL2)"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY 'FILLCOLS EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG     = ''
   IF CONTENT = ''  |  TOLEFT = ''  |  TORIGHT = ''  THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TOLEFT,'W')  |  TOLEFT = 0  THEN DO
      MESG = "'"TOLEFT"' VALUE OF TO-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TORIGHT,'W') |  TORIGHT = 0 THEN DO
      MESG = "'"TORIGHT"' VALUE OF TO-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF SUBSTR(CONTENT,1,1) = "'",
         & SUBSTR(CONTENT,LENGTH(CONTENT),1) = "'" THEN DO
      CONTENT = SUBSTR(CONTENT,2,LENGTH(CONTENT)-2)
   END
   LEN = LENGTH(CONTENT)
   "ISREDIT ("LRECL") = LRECL "
   IF TOLEFT > TORIGHT THEN DO
      MESG = "TO-LEFT COLUMN IS GREATER THAN TO-RIGHT!"
      SIGNAL ERROR
   END
   IF TOLEFT > LRECL THEN DO
      MESG = "TO-LEFT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END
   IF TORIGHT > LRECL THEN DO
      MESG = "TO-RIGHT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
   END

   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
       END
   END

   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR   "
   "ISREDIT ("ANUM") = AUTONUM              "
   "ISREDIT (END) = LINENUM .ZLAST"

   IF LBL1 <> '' THEN "ISREDIT (START) = LINENUM &LBL1"
      ELSE START = 1

   IF LBL2 <> '' THEN "ISREDIT (END) = LINENUM &LBL2"
      ELSE LBL2 = LBL1

   IF START > END THEN DO
      FLIP  = START
      START = END
      END   = FLIP
   END

   CONTENTX = SUBSTR(COPIES(CONTENT,(TORIGHT-TOLEFT+1)%LEN+1),
                     ,1,TORIGHT-TOLEFT+1)

   DO PNTR =  START  TO  END  BY 1
      "ISREDIT (RECORD) = LINE &PNTR"
      RECORD = SUBSTR(RECORD,1,TOLEFT-1),
         || CONTENTX,
         || SUBSTR(RECORD,TORIGHT+1)
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END

   IF LBL1 = '' THEN "ISREDIT LOCATE 1"
      ELSE "ISREDIT LOCATE" LBL1
  "ISREDIT AUTONUM = "ANUM
  SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   SIGNAL DISPDOC

EOJ:
   X = MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY " FILLCOLS - PLACE A STRING ONTO SPECIFIED COLUMNS      "
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY "    FILLCOLS  LEFT-COL  RIGHT-COL  STRING  ( .A .B )   "
   SAY
   SAY "       COLUMNS AND STRING ARE MANDATORY. STRING MAY BE "
   SAY "       IN QUOTES OR NOT.  LABEL RANGE IS OPTIONAL.     "
   SAY
   SAY " EXAMPLES "
   SAY "    FILLCOLS  17 19  ABC                               "
   SAY "       WILL OVERLAY 'ABC' ONTO COLUMNS 17-19           "
   SAY
   SAY "    FILLCOLS  73 80  '-=-=-'  .A .B                    "
   SAY "       WILL PLACE '-=-=--=-' IN COLUMNS 73 THRU 80     "
   SAY "       IN LABEL RANGE .A THRU .B                       "
   SAY
   RETURN
