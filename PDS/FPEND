/**  REXX -- FPEND  --  FIND PENDING LINE COMMAND                  **/
/**                 --  OPTION P WILL CAUSE A BACKWARD SEARCH      **/
/**                 --  OPTION F WILL FIND THE FIRST PENDING CMD   **/
   "ISREDIT MACRO (PARMS) NOPROCESS"
   IF PARMS = '?' THEN SIGNAL DISPDOC
   DIRECTION = 'NEXT'
   IF PARMS <> '' THEN DO
      PARMS = TRANSLATE(PARMS)
      IF PARMS = '-=PFK=-' THEN DO;
         ADDRESS ISPEXEC "VGET (FPDIR) SHARED"
         DIRECTION = FPDIR
      END
      ELSE DO
         IF SUBSTR(WORD(PARMS,1),1,1) = 'P' THEN DIRECTION = 'PREV'
         IF SUBSTR(WORD(PARMS,1),1,1) = 'F' THEN DIRECTION = 'FIRST'
      END
   END
   "ISREDIT LOCATE COM" DIRECTION
   FALTNM  = 'FPEND'
   ADDRESS ISPEXEC "VPUT (FALTNM) SHARED"
   IF DIRECTION = 'FIRST' THEN FPDIR = 'NEXT'
                          ELSE FPDIR = DIRECTION
   ADDRESS ISPEXEC "VPUT (FPDIR)  SHARED"
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "FPEND -  DELETE ALL DATA OR MEMBERS FROM DS LIST FILE     "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    FPEND  ( NEXT | PREVIOUS | FIRST )                    "
   SAY
   SAY "       FIND A PENDING PREFIX COMMAND SUCH AS CC IN THE    "
   SAY "       DIRECTION SPECIFIED. ONLY THE FIRST CHARACTER      "
   SAY "       NEEDS TO BE SPECIFIED AND 'N' FOR NEXT IS THE      "
   SAY "       DEFAULT. THE FALT MACRO MAY BE ASSIGNED TO A PF    "
   SAY "       KEY TO ALLOW REPEAT FINDS.                         "
   SAY
   SAY
   EXIT(1)

