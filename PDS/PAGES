//-YOUR-USERID-P JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  ALLOW STRIPPING OF FIRST, MIDDLE, LAST OR FROM/TO # OF ASCII
//***  CONTROL DELIMITED PAGES FROM A SEQUENTIAL FILE
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 PAGES: PROC OPTIONS(MAIN) REORDER;

        DCL  INFILE    FILE INPUT RECORD SEQL;
        DCL  REC       CHAR(32760) STATIC VARYING;
        DCL  INFILE_EOF_IND CHAR(01) STATIC INIT('N');
        DCL  OK_SO_FAR     CHAR(01) STATIC INIT('Y');
        DCL  ONLY_1ST      CHAR(01) STATIC INIT('N');
        DCL  REC#1         CHAR(01) STATIC INIT('N');

        DCL  FIRST         FIXED BIN(31) STATIC INIT(-1);
        DCL  MIDDLE        FIXED BIN(31) STATIC INIT(-1);
        DCL  MIDDLE#1      FIXED BIN(31) STATIC INIT(0);
        DCL  MIDDLE#2      FIXED BIN(31) STATIC INIT(0);
        DCL  LAST          FIXED BIN(31) STATIC INIT(-1);
        DCL  LAST#         FIXED BIN(31) STATIC INIT(0);
        DCL  FROM          FIXED BIN(31) STATIC INIT(-1);
        DCL  TO            FIXED BIN(31) STATIC INIT(-1);

        DCL  RECORDS#      FIXED BIN(31) STATIC INIT(0);
        DCL  RECORDS_OUT#  FIXED BIN(31) STATIC INIT(0);
        DCL  PAGES#        FIXED BIN(31) STATIC INIT(0);
        DCL  PAGES_OUT#    FIXED BIN(31) STATIC INIT(0);
        DCL  PG            FIXED BIN(31) STATIC INIT(0);

        DCL  DATE      BUILTIN;
        DCL  TIME      BUILTIN;


        CALL FIGURE_REQUEST;

        IF OK_SO_FAR = 'Y' THEN CALL REVIEW_INPUT;

        IF OK_SO_FAR = 'Y' THEN CALL PULL_OFF_REQUEST;

        CALL TERMINATION;


 FIGURE_REQUEST: PROC;
        GET DATA;
        IF FIRST = -1 & MIDDLE = -1 & LAST = -1 & FROM = -1 & TO = -1
           THEN DO;
              OK_SO_FAR = 'N';
              CALL PLIRETC(08);
              PUT EDIT ( '**  ERROR  **   NO RANGE SELECTED' )
                       ( SKIP(2), A );
              PUT EDIT ( '**    IN SYSIN FILE, SPECIFY:    ' )
                       ( SKIP(1), A );
              PUT EDIT ( '**      FIRST=###                ' )
                       ( SKIP(1), A );
              PUT EDIT ( '**      MIDDLE=###               ' )
                       ( SKIP(1), A );
              PUT EDIT ( '**      LAST=###                 ' )
                       ( SKIP(1), A );
              PUT EDIT ( '**      FROM=###,TO=###          ' )
                       ( SKIP(1), A );
              PUT EDIT ( '**        OR SOME COMBINATION... ' )
                       ( SKIP(1), A );
              END;
           ELSE DO;
              IF ( FROM ¬= -1 & TO = -1 ) | ( FROM = -1 & TO ¬= -1 )
                 THEN DO;
                    OK_SO_FAR = 'N';
                    CALL PLIRETC(08);
                    PUT EDIT ( '**  ERROR  **   FROM/TO RANGE NOT',
                               ' CONSISTENT' )
                             ( SKIP(2), A, A );
                    PUT EDIT ( '**    SPECIFY BOTH OR NEITHER... ' )
                             ( SKIP(1), A );
                    END;
              END;
        IF FIRST ¬= -1 & MIDDLE = -1 & LAST = -1 & FROM = -1 & TO = -1
           THEN ONLY_1ST = 'Y';
        END FIGURE_REQUEST;

 REVIEW_INPUT: PROC;
        IF ONLY_1ST = 'N'
           THEN DO;
              OPEN FILE(INFILE);
              INFILE_EOF_IND = 'N';
              ON ENDFILE(INFILE) INFILE_EOF_IND = 'Y';
              REC#1 = 'Y';
              READ FILE(INFILE) INTO(REC);
              DO WHILE ( INFILE_EOF_IND = 'N' );
                 RECORDS# = RECORDS# + 1;
                 IF SUBSTR(REC,1,1) = '1' | REC#1 = 'Y'
                    THEN PAGES# = PAGES# + 1;
                 READ FILE(INFILE) INTO(REC);
                 REC#1 = 'N';
                 END;
              CLOSE FILE(INFILE);
              IF PAGES# = 0
                 THEN DO;
                    OK_SO_FAR = 'N';
                    CALL PLIRETC(04);
                    PUT EDIT ( '**  ERROR  **   NO PAGES IN INPUT',
                               ' FILE' )
                             ( SKIP(2), A, A );
                    PUT EDIT ( '**    PLEASE CHECK INPUT... ' )
                             ( SKIP(1), A );
                    END;
              END;
        IF MIDDLE ¬= -1
           THEN DO;
              IF MIDDLE < ( PAGES# - 2 )
                 THEN DO;
                    MIDDLE#1 = ( ( PAGES# - MIDDLE ) / 2 ) + 1;
                    MIDDLE#2 = MIDDLE#1 + MIDDLE - 1;
                    END;
                 ELSE DO;
                    MIDDLE#1 = MIDDLE;
                    MIDDLE#2 = MIDDLE#1;
                    END;
              END;
        IF LAST ¬= -1
           THEN DO;
              LAST# = PAGES# - LAST + 1;
              END;
        END REVIEW_INPUT;

 PULL_OFF_REQUEST: PROC;
        INFILE_EOF_IND = 'N';
        ON ENDFILE(INFILE) INFILE_EOF_IND = 'Y';
        REC#1 = 'Y';
        PG = 1;
        OPEN FILE(INFILE);
        READ FILE(INFILE) INTO(REC);
        DO WHILE ( INFILE_EOF_IND = 'N' );
           IF FIRST ¬= -1 & PG <= FIRST
              THEN CALL WRITE_IT;
              ELSE IF MIDDLE ¬= -1 & PG >= MIDDLE#1 & PG <= MIDDLE#2
                 THEN CALL WRITE_IT;
                 ELSE IF LAST ¬= -1 & PG >= LAST#
                    THEN CALL WRITE_IT;
                    ELSE IF FROM ¬= -1 & PG >= FROM & PG <= TO
                       THEN CALL WRITE_IT;
           READ FILE(INFILE) INTO(REC);
           REC#1 = 'N';
           IF SUBSTR(REC,1,1) = '1'
              THEN PG = PG + 1;
           END;
        END PULL_OFF_REQUEST;

 WRITE_IT: PROC;
        RECORDS_OUT# = RECORDS_OUT# + 1;
        WRITE FILE(OUTFILE) FROM(REC);
        IF REC#1 = 'Y' | SUBSTR(REC,1,1) = '1'
           THEN PAGES_OUT# = PAGES_OUT# + 1;
        END WRITE_IT;

 TERMINATION: PROC;
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  ', DATE, ' STATISTICS ', TIME, '  ****' )
                 ( SKIP(1), A, A, A, A, A );
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        IF PAGES# > 0
           THEN PUT EDIT ( '** TOTAL PAGES ON FILE:      ', PAGES#   )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF RECORDS# > 0
           THEN PUT EDIT ( '** TOTAL RECORDS ON FILE:    ', RECORDS# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF FIRST > 0
           THEN PUT EDIT ( '** FIRST PAGES SELECTED:     ', FIRST    )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF MIDDLE > 0
           THEN PUT EDIT ( '** MIDDLE PAGES SELECTED:    ', MIDDLE   )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF LAST > 0
           THEN PUT EDIT ( '** LAST PAGES SELECTED:      ', LAST     )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF FROM > 0
           THEN DO;
              PUT EDIT ( '** PAGE RANGE SELECTED FROM: ', FROM     )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '**                      TO:  ', TO       )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              END;
        IF PAGES_OUT# > 0
           THEN PUT EDIT ( '** TOTAL PAGES SELECTED:     ', PAGES_OUT# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF RECORDS_OUT# > 0
           THEN PUT EDIT ( '** TOTAL LINES SELECTED:     ',
                           RECORDS_OUT# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        END TERMINATION;

        END PAGES;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//L          EXEC     PGM=HEWLKED,COND=(5,LT),
//             PARM='XREF,LIST,LET,DCBS,AMODE=31,RMODE=ANY',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DISP=SHR,DSN=-YOUR-LOADLIB-
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD       *
  SETSSI   00031700
  ENTRY    PLISTART
  NAME     PAGES(R)
//***
