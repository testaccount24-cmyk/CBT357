/**  REXX  -- VIEW COBOL COLUMN OFFSETS FROM FILEAID              **/

/*  SET UP ISPF ENVIRONMENT AND DETERMINE STARTING/ENDING LINES    */
   SAVEMSG=MSG()
   X=MSG("OFF")
   X=ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
         SAY "COBCOLS MUST BE RUN UNDER ISPF EDIT!"
         SIGNAL QUIT
       END
   SUBCOM ISREDIT
   UMODE = "ISREDIT"
   ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
   ADDRESS ISREDIT
   "MACRO (HUH) NOPROCESS"
   IF HUH = "?" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN
      DO
         "(TOP) = LINENUM .ZFIRST"
         "(BOT) = LINENUM .ZLAST"
      END
   ELSE
      DO
         "(TOP) = LINENUM .ZFRANGE"
         "(BOT) = LINENUM .ZLRANGE"
      END

/*  DELETE OLD TEMP PDS FILE, IF ANY, AND ALLOCATE NEW ONE         */
   ADDRESS TSO
   "FREE DD(DDXX DD01)"
   "DELETE $$$TEMP.COPYBOOK.PDS"
   "ALLOCATE DD(DD01) DSN($$$TEMP.COPYBOOK.PDS) NEW",
      "CYL SPACE(01 01) RECFM(F B) LRECL(80) BLKSIZE(27920) DIR(12)"
   "ALLOCATE DD(DDXX) DSN($$$TEMP.COPYBOOK.PDS(DUMMYMEM)) SHR"

/*  WRITE LINES IN SPECIFIED RANGE TO TEMP PDS FILE AS MEMBER      */
   ADDRESS ISREDIT
   ADDRESS TSO "EXECIO 0 DISKW" DDXX "(OPEN"
   GOT_FIRST = 'NO'
   DO II = TOP TO BOT BY 1
      "ISREDIT (RECORD) = LINE &II"
      IF SUBSTR(RECORD,7,1) <> '*',
               & SUBSTR(RECORD,7,1) <> '/',
               & SUBSTR(RECORD,8,65) <> ' ' THEN DO
         IF GOT_FIRST = 'NO' THEN DO
            IF INDEX(SUBSTR(RECORD,8,4),'1') = 0 THEN DO
               DUMMY_REC = '       01  DUMMY-01-LEVEL.'
               PUSH DUMMY_REC
               ADDRESS TSO "EXECIO 1 DISKW" DDXX
            END
            GOT_FIRST = 'YES'
         END
         BB = INDEX(RECORD,'( ');
         DO WHILE ( BB <> 0 );
            RECORD = SUBSTR(RECORD,1,BB) || SUBSTR(RECORD,BB+2)
            BB = INDEX(RECORD,'( ');
         END
         PUSH RECORD
         ADDRESS TSO "EXECIO 1 DISKW" DDXX
      END
   END
   ADDRESS TSO "EXECIO 0 DISKW" DDXX "(FINIS"
   ADDRESS TSO
   "FREE DD(DDXX DD01)"

/*  FREE & ALLOCATE DD01, SYSIN, SYSLIST AND SYSPRINT FILES        */
/*  FOR INITIAL CALL TO FILEAID                                    */
   ADDRESS TSO
   "FREE DD(DD01 SYSIN SYSLIST SYSPRINT)"
   "ALLOCATE DD(DD01) DSN($$$TEMP.COPYBOOK.PDS) SHR"
   "DELETE $$$TEMP.SYSIN.FILE1"
   "ALLOC DD(SYSIN) DSN($$$TEMP.SYSIN.FILE1) NEW TRACKS SPACE(1)",
      "LRECL(80) BLKSIZE(800) RECFM(F B)"
   QUEUE "$$DD01 RLPRINT MEMBER=(DUMMYMEM)"
   "EXECIO" QUEUED() "DISKW SYSIN (FINIS"
   "DELETE $$$TEMP.SYSLIST.FILE"
   "ALLOCATE DD(SYSLIST) DSN($$$TEMP.SYSLIST.FILE) NEW",
      "CYL SPACE(01,01) RECFM(F,B) LRECL(80) BLKSIZE(27920)"
   "ALLOCATE DD(SYSPRINT) DUMMY",
      "RECFM(F,B) LRECL(133) BLKSIZE(6118)"
   "CALL *(FILEAID)"
   "FREE DD(DD01 SYSIN SYSLIST SYSPRINT)"

/*  VIEW SYSLIST FILE AND THEN QUIT                                */
   ADDRESS ISPEXEC 'VIEW DATASET('$$$TEMP.SYSLIST.FILE')'
   ADDRESS TSO
   "DELETE $$$TEMP.COPYBOOK.PDS"
   "DELETE $$$TEMP.SYSIN.FILE1"
   "DELETE $$$TEMP.SYSLIST.FILE"
   ADDRESS ISPEXEC
   X=MSG(SAVEMSG)
   ADDRESS ISREDIT "LOCATE" TOP
   ADDRESS ISREDIT "RESET"
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "COBCOLV - SHOW FILEAID COBOL STRUCTURE OFFSETS            "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    COBCOLV  ( ? )                                        "
   SAY
   SAY "       INVOKES FILEAID AGAINST A COBOL STRUCTURE AND      "
   SAY "       VIEWS THE RESULTING OUTPUT WHICH HAS STARTING AND  "
   SAY "       ENDING POSITIONS OF EACH VARIABLE.                 "
   SAY
   SAY "       A QUESTION MARK WILL CAUSE THIS HELP TO BE SHOWN.  "
   SAY "       C/C#/CC PREFIX COMMANDS MAY BE USED TO LIMIT THE   "
   SAY "       LINES PROCESSED.                                   "
   SAY
   EXIT(1)

