//-YOUR-USERID-A JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  CHANGE TAGGED DOC I/P INTO ASCII CARRIAGE CONTROLLED O/P
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 ADDCC: PROC OPTIONS(MAIN) REORDER;

    DCL  REC           CHAR(00255) STATIC VARYING;
    DCL  I_EOF_IND     CHAR(01) STATIC INIT('N');
    DCL  SYSIN_EOF_IND CHAR(01) STATIC INIT('N');

    DCL  LINESIZE      FIXED BIN(31) STATIC INIT(072);
    DCL  PAGESIZE      FIXED BIN(31) STATIC INIT(058);
    DCL  TOC           CHAR(001)     STATIC INIT('Y');
    DCL  RESET         CHAR(001)     STATIC INIT('N');
    DCL  DOCTITLE      CHAR(255)     STATIC VARYING INIT('');
    DCL  DOCTITLE2     CHAR(255)     STATIC VARYING INIT('');
    DCL  DOCTITLE3     CHAR(255)     STATIC VARYING INIT('');
    DCL  TLRDATE       CHAR(255)     STATIC VARYING INIT('');
    DCL  TLRDESC       CHAR(255)     STATIC VARYING INIT('');

    DCL  LINE_TBL(32760)  CHAR(255)  STATIC VARYING;
    DCL  LINE_TBL#     FIXED BIN(15) STATIC INIT(0);
    DCL  1  TOC_TBL(2048) STATIC,
          10  HDR      CHAR(255)     VARYING,
          10  HDRTYPE  CHAR(001),
          10  HDRDESC  CHAR(057),
          10  PAGE     FIXED BIN(31);
    DCL  TOC#          FIXED BIN(15) STATIC INIT(0);

    DCL  (II,JJ,KK)    FIXED BIN(31) STATIC INIT(0);
    DCL  CURRHDR1      CHAR(255)     STATIC VARYING INIT('');
    DCL  CURRHDR2      CHAR(255)     STATIC VARYING INIT('');
    DCL  CURRPAGE      FIXED BIN(15) STATIC INIT(0);
    DCL  FORMATTED_PAGE  PIC'ZZ,ZZ9' STATIC;
    DCL  CURRLINE      FIXED BIN(15) STATIC INIT(0);
    DCL  BLANKS        CHAR(255)     STATIC INIT(' ');
    DCL  UNDERSCORES   CHAR(255)     STATIC INIT((255)'_');
    DCL  DOTS          CHAR(254)     STATIC INIT((127)' .');
    DCL  VV            CHAR(255)     STATIC VARYING INIT('');

    DCL  SUBSTR        BUILTIN;
    DCL  LENGTH        BUILTIN;


    ON ENDFILE(I) I_EOF_IND = 'Y';
    READ FILE(I) INTO(REC);

 START_OVER:
    DO WHILE ( I_EOF_IND  = 'N' );
       IF SUBSTR(REC,1,1) = ':'
          THEN CALL PROCESS_CONTROL;
          ELSE CALL PROCESS_LINE;
       READ FILE(I) INTO(REC);
    END;

 FINISH_SET:
    CALL PROCESS_TITLE_PAGE;
    IF TOC = 'Y' THEN CALL PROCESS_TOC;
                 ELSE CURRPAGE = 1;
    CALL PROCESS_ACTUAL_LINES;
    IF RESET = 'Y'
       THEN DO;
          RESET     = 'N';
          CURRPAGE  = 0;
          CURRLINE  = 0;
          LINE_TBL# = 0;
          TOC#      = 0;
       END;
    IF I_EOF_IND  = 'N' THEN GOTO START_OVER;


 PROCESS_LINE:  PROC;
    CURRLINE  = CURRLINE + 1;
    IF CURRLINE > PAGESIZE
       THEN DO;
          CURRPAGE = CURRPAGE + 1;
          LINE_TBL#     = LINE_TBL# + 1;
          FORMATTED_PAGE = CURRPAGE;
          LINE_TBL(LINE_TBL#) = '1'
             || SUBSTR(BLANKS,1,
                   (LINESIZE-LENGTH(CURRHDR1)-1)/2-11)
             || CURRHDR1
             || SUBSTR('             (Continued)',
                   12*(LENGTH(CURRHDR1) > 0)+1,12)
             || SUBSTR(BLANKS,1,
                    (LINESIZE-LENGTH(CURRHDR1))/2-12)
             || 'Page '
             || FORMATTED_PAGE;
          LINE_TBL#     = LINE_TBL# + 1;
          LINE_TBL(LINE_TBL#) = SUBSTR(BLANKS,1,LINESIZE);
          CURRLINE = 2;
       END;
    LINE_TBL# = LINE_TBL# + 1;
    LINE_TBL(LINE_TBL#) = REC
       || SUBSTR(BLANKS,1,LINESIZE-LENGTH(REC));
    END PROCESS_LINE;


 PROCESS_CONTROL:  PROC;
    IF SUBSTR(REC,02,01) = '*'
       THEN;
    else IF SUBSTR(REC,02,05) = 'RESET'
       THEN DO;
          RESET = 'Y';
          GOTO FINISH_SET;
       END;
    ELSE IF SUBSTR(REC,02,07) = 'HEADER2'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 9 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          DO KK = JJ TO LINESIZE
                WHILE ( SUBSTR(REC,KK,1) ¬= ' ' );
          END;
          TOC# = TOC# + 1;
          HDR(TOC#) = SUBSTR(REC,JJ,KK-JJ);
          HDRTYPE(TOC#)   = '2';
          HDRDESC(TOC#)   = ' ';
          PAGE(TOC#) = CURRPAGE;
          CURRHDR2  = SUBSTR(REC,JJ,KK-JJ);
          IF PAGESIZE < CURRLINE + 5
             THEN DO;
                CURRPAGE      = CURRPAGE + 1;
                CURRLINE      = 2;
                PAGE(TOC#)    = CURRPAGE;
                LINE_TBL#     = LINE_TBL# + 1;
                FORMATTED_PAGE = CURRPAGE;
                LINE_TBL(LINE_TBL#) = '1'
                   || SUBSTR(BLANKS,1,
                         (LINESIZE-LENGTH(CURRHDR1)-1)/2-11)
                   || CURRHDR1
                   || ' (Continued)'
                   || SUBSTR(BLANKS,1,
                         (LINESIZE-LENGTH(CURRHDR1))/2-12)
                   || 'Page '
                   || FORMATTED_PAGE;
                LINE_TBL#     = LINE_TBL# + 1;
                LINE_TBL(LINE_TBL#) = SUBSTR(BLANKS,1,LINESIZE);
             END;
          CURRLINE  = CURRLINE + 1;
          LINE_TBL# = LINE_TBL# + 1;
          LINE_TBL(LINE_TBL#) = ' ' || SUBSTR(REC,JJ,II-JJ+1)
             || SUBSTR(BLANKS,1,LINESIZE-II+JJ-2);
          LINE_TBL# = LINE_TBL# + 1;
          LINE_TBL(LINE_TBL#) = '+'
             || SUBSTR(UNDERSCORES,1,LENGTH(CURRHDR2))
             || SUBSTR(BLANKS,1,LINESIZE-LENGTH(CURRHDR2)-1);
       END;
    ELSE IF SUBSTR(REC,02,07) = 'HEADERT'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 9 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          HDRDESC(TOC#) = SUBSTR(REC,JJ,II-JJ+1)
             || SUBSTR(' ',1,MOD(II-JJ+1,2))
             || REPEAT('. ',24);
       END;
    ELSE IF SUBSTR(REC,02,07) = 'HEADER1'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 9 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          TOC#          = TOC# + 1;
          HDR(TOC#)     = SUBSTR(REC,JJ,II-JJ+1);
          HDRTYPE(TOC#) = '1';
          CURRHDR1      = SUBSTR(REC,JJ,II-JJ+1);
          CURRPAGE      = CURRPAGE + 1;
          PAGE(TOC#)    = CURRPAGE;
          CURRLINE      = 2;
          LINE_TBL#     = LINE_TBL# + 1;
          FORMATTED_PAGE = CURRPAGE;
          LINE_TBL(LINE_TBL#) = '1'
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(CURRHDR1)-1)/2-5)
             || CURRHDR1
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(CURRHDR1))/2-6)
             || 'Page '
             || FORMATTED_PAGE;
          LINE_TBL#     = LINE_TBL# + 1;
          LINE_TBL(LINE_TBL#) = SUBSTR(BLANKS,1,LINESIZE);
       END;
    ELSE IF SUBSTR(REC,02,08) = 'PAGESIZE'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 10 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          PAGESIZE  = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE IF SUBSTR(REC,02,03) = 'TOC'
       THEN DO;
          DO JJ = 5 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          IF SUBSTR(REC,JJ,03) = 'YES'
             THEN TOC = 'Y';
             ELSE TOC = 'N';
       END;
    ELSE IF SUBSTR(REC,02,09) = 'DOCTITLE '
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 10 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          DOCTITLE = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE IF SUBSTR(REC,02,09) = 'DOCTITLE2'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 11 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          DOCTITLE2 = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE IF SUBSTR(REC,02,09) = 'DOCTITLE3'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 11 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          DOCTITLE3 = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE IF SUBSTR(REC,02,07) = 'TLRDATE'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 9 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          TLRDATE = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE IF SUBSTR(REC,02,07) = 'TLRDESC'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 9 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          TLRDESC = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE IF SUBSTR(REC,02,08) = 'LINESIZE'
       THEN DO;
          DO II = LINESIZE TO 1 BY -1
                WHILE ( SUBSTR(REC,II,1) = ' ' );
          END;
          DO JJ = 10 TO LINESIZE
                WHILE ( SUBSTR(REC,JJ,1) = ' ' );
          END;
          LINESIZE  = SUBSTR(REC,JJ,II-JJ+1);
       END;
    ELSE CALL PROCESS_LINE;
 END PROCESS_CONTROL;


 PROCESS_TITLE_PAGE: PROC;
    REC = ' ' || SUBSTR(BLANKS,1,LINESIZE-1);
    WRITE FILE(O) FROM(REC);
    IF DOCTITLE ¬= ''
       THEN DO;
          JJ = 3;
          DO II = 1 TO (((PAGESIZE/2)-4)/3);
             REC = '-' || SUBSTR(BLANKS,1,LINESIZE-1);
             WRITE FILE(O) FROM(REC);
             JJ = JJ + 3;
          END;
          REC = ' '
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(DOCTITLE)-1)/2)
             || DOCTITLE
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(DOCTITLE))/2);
          WRITE FILE(O) FROM(REC);
          REC = ' '
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(DOCTITLE2)-1)/2)
             || DOCTITLE2
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(DOCTITLE2))/2);
          WRITE FILE(O) FROM(REC);
          REC = ' '
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(DOCTITLE3)-1)/2)
             || DOCTITLE3
             || SUBSTR(BLANKS,1,(LINESIZE-LENGTH(DOCTITLE3))/2);
          WRITE FILE(O) FROM(REC);
          IF TLRDATE ¬= '' | TLRDESC ¬= ''
             THEN DO;
                DO II = 1 TO ((PAGESIZE-JJ)/3);
                   REC = '-' || SUBSTR(BLANKS,1,LINESIZE-1);
                   WRITE FILE(O) FROM(REC);
                END;
                REC = ' ' || TLRDATE
                   || SUBSTR(BLANKS,1,
                      LINESIZE-LENGTH(TLRDATE)-LENGTH(TLRDESC)-1)
                   || TLRDESC;
                WRITE FILE(O) FROM(REC);
             END;
       END;
 END PROCESS_TITLE_PAGE;


 PROCESS_TOC:   PROC;
    IF TOC# > 0
       THEN DO;
          KK       = LINE_TBL# + 1;
          CURRLINE = 2;
          CURRPAGE = 2;
          FORMATTED_PAGE = CURRPAGE;
          LINE_TBL(KK) = '1' || SUBSTR(BLANKS,1,(LINESIZE-16)/2-6)
             || 'TABLE OF CONTENTS'
             || SUBSTR(BLANKS,1,(LINESIZE-17)/2-6)
             || 'Page '
             || FORMATTED_PAGE;
          LINE_TBL(KK+1) = SUBSTR(BLANKS,1,LINESIZE);
          KK = KK + 1;
          DO II = 1 TO TOC#;
             IF CURRLINE > PAGESIZE
                THEN DO;
                   CURRLINE = 2;
                   CURRPAGE = CURRPAGE + 1;
                   FORMATTED_PAGE = CURRPAGE;
                   KK = KK + 1;
                   LINE_TBL(KK) = '1'
                      || SUBSTR(BLANKS,1,(LINESIZE-28)/2-6)
                      || 'TABLE OF CONTENTS (Continued)'
                      || SUBSTR(BLANKS,1,(LINESIZE-29)/2-6)
                      || 'Page '
                      || FORMATTED_PAGE;
                   KK = KK + 1;
                   LINE_TBL(KK) = SUBSTR(BLANKS,1,LINESIZE);
                END;
             IF HDRTYPE(II) = '1'
                THEN DO;
                   FORMATTED_PAGE = PAGE(II);
                   KK = KK + 1;
                   LINE_TBL(KK) = SUBSTR(BLANKS,1,LINESIZE);
                   KK = KK + 1;
                   LINE_TBL(KK) = ' ' || HDR(II)
                      || SUBSTR(BLANKS,1,MOD(LENGTH(HDR(II)),2)+1)
                      || SUBSTR(DOTS,1,LINESIZE
                         -LENGTH(HDR(II))-8-MOD(LENGTH(HDR(II)),2))
                      || FORMATTED_PAGE;
                   KK = KK + 1;
                   LINE_TBL(KK) = SUBSTR(BLANKS,1,LINESIZE);
                   CURRLINE = CURRLINE + 3;
                END;
                ELSE DO;
                   FORMATTED_PAGE = PAGE(II);
                   KK = KK + 1;
                   LINE_TBL(KK) = '    ' || HDR(II)
                      || SUBSTR(BLANKS,1,MOD(LENGTH(HDR(II)),2))
                      || SUBSTR(DOTS,1,LINESIZE
                         -LENGTH(HDR(II))-10-MOD(LENGTH(HDR(II)),2))
                      || FORMATTED_PAGE;
                   IF HDRDESC(II) ¬= ' '
                      THEN SUBSTR(LINE_TBL(KK),16,57) = HDRDESC(II);
                   CURRLINE = CURRLINE + 1;
                END;
          END;
          DO II = (LINE_TBL# + 1) TO KK;
             REC = LINE_TBL(II);
             IF SUBSTR(REC,LINESIZE-6,1) = '.'
                   & VERIFY(SUBSTR(REC,LINESIZE,1),'0123456789') = 0
                THEN DO;
                   FORMATTED_PAGE = SUBSTR(REC,LINESIZE-5,6);
                   FORMATTED_PAGE = FORMATTED_PAGE + CURRPAGE;
                   SUBSTR(REC,LINESIZE-5,6) = FORMATTED_PAGE;
                END;
             WRITE FILE(O) FROM(REC);
          END;
       END;
 END PROCESS_TOC;


 PROCESS_ACTUAL_LINES: PROC;
    DO II = 1 TO LINE_TBL#;
       REC = LINE_TBL(II);
       IF SUBSTR(REC,LINESIZE-10,5) = 'Page '
          THEN DO;
             CURRPAGE                 = CURRPAGE + 1;
             FORMATTED_PAGE           = CURRPAGE;
             SUBSTR(REC,LINESIZE-5,6) = FORMATTED_PAGE;
          END;
       WRITE FILE(O) FROM(REC);
    END;
 END PROCESS_ACTUAL_LINES;


 END ADDCC;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//L          EXEC     PGM=HEWLKED,
//             PARM='XREF,LIST,LET,DCBS,AMODE=31,RMODE=ANY',
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DISP=SHR,DSN=-YOUR-LOADLIB-
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD       *
  SETSSI   03011400
  ENTRY    PLISTART
  NAME     ADDCC(R)
//***
