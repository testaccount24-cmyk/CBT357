//-YOUR-USERID-D JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//A          EXEC     PGM=ASMA90,REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=SYS1.MACLIB
//             DD       DISP=SHR,DSN=-YOUR-SYSLIB-
DYNN     TITLE 'DYNAMICALLY CREATE A NEW DSN W/ ALL SPECS'
**********************************************************************
*  INPUT PARAMETERS ARE:
*    DATA SET NAME       44 BYTES - ALPHANUMERIC  POINTED TO IN R2
*    DDNAME               8 BYTES - ALPHANUMERIC                R3
*    RECFM                2 BYTES - ALPHANUMERIC                R4
*    LRECL                2 BYTES - BINARY                      R5
*    BLKSI                2 BYTES - BINARY                      R6
*    UNIT                 8 BYTES - ALPHANUMERIC                R7
*    DYNALLOC R15 R/C     2 BYTES - BINARY                      R8
*    DYNALLOC RETURN CODE 2 BYTES - BINARY                      R9
*    DYNALLOC REASON CODE 2 BYTES - BINARY                      R10
**********************************************************************
         COPY  MACSDYNA
DYNN     RENTER  BASEREG=R12
         LM    R2,R10,0(R1)       LOAD REGS FOR ALL PARAMETERS

         ST    R2,DSADDR          DSNAME ADDR FOR ALLOC MACRO
         ST    R3,DDADDR          DDNAME ADDR

         MVC   RECFM,0(R4)
         LA    R4,RFLEN           RECFM SET UP
         CLI   RECFM+1,C' '
         BNE   RFNOTL1
         MVC   RFLEN,HW1
         B     RFOK
RFNOTL1  EQU   *
         CLI   RECFM+2,C' '
         BNE   RFNOTL2
         MVC   RFLEN,HW2
         B     RFOK
RFNOTL2  EQU   *
         MVC   RFLEN,HW3
RFOK     EQU   *

         LH    R5,0(R5)           LRECL
         ST    R5,LRECL

         LH    R6,0(R6)           BLKSI
         ST    R6,BLKSI

         MVC   UNIT,0(R7)         UNIT SET UP
         CLC   UNIT,BLANKS
         BE    UNITDEF
         LA    R7,8
         LA    R14,UNIT+7
UNITLNCK EQU   *
         CLI   0(R14),C' '
         BE    UNITDONE
         BCTR  R14,0
         BCT   R7,UNITLNCK
UNITDONE EQU   *
         STH   R7,UNLEN
UNITDEF  EQU   *

         ALLOC DSN=DSADDR,DDN=DDADDR,DISP=(NEW,CATLG),                 X
               UNIT=UNADDR,SPACE=(CYL,(10,10),RLSE),                   X
               RECFM=(R4),LRECL=LRECL,BLKSIZE=BLKSI

         DROP  R14
         USING S99RB,R14
         STH   R15,0(R8)          SEND BACK DYNALLOC R15 VALUE
         MVC   0(2,R9),S99ERROR   SEND ERROR REASON CODE BACK
         MVC   0(2,R10),S99INFO   SEND INFO  REASON CODE BACK
         REXIT

**       ALLOC MACRO PARAMETERS

DSADDR   DS    A
DSLEN    DC    H'44'              LENGTH  OF DSNAME (44)

DDADDR   DS    A
DDLEN    DC    H'8'               LENGTH  OF DDNAME (08)

UNADDR   DC    A(UNIT)
UNLEN    DC    H'5'               LENGTH  OF UNIT
UNIT     DC    CL8'TEST'

RFLEN    DC    H'0'               LENGTH  OF RECFM  (1,2 OR 3)
RECFM    DS    CL3
LRECL    DC    F'80'
BLKSI    DC    F'0'

HW1      DC    H'1'
HW2      DC    H'2'
HW3      DC    H'3'
BLANKS   DC    CL44' '

         LTORG
         DYNSPACE
         IEFZB4D0
         IEFZB4D2
         END
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*,
//             DCB=(RECFM=FM,LRECL=121,BLKSIZE=121)
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(2960,(111,12)),DCB=BLKSIZE=2960
//***
//L          EXEC     PGM=IEWL,COND=(5,LT),
//             PARM='XREF,LIST,LET,DCBS',
//             REGION=4M
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,
//             DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   99120100
  ENTRY    DYNN
  NAME     DYNN(R)
//***
