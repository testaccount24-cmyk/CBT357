/**  REXX -- MEMBER LIST INSERTION MACRO  **/

   "ISREDIT MACRO (PARMS)"
   PARMS = TRANSLATE(PARMS)
   IF PARMS = "" | PARMS = "?" THEN SIGNAL DISPDOC
   IF INDEX(PARMS,"(") <> 0 THEN DO
      II = INDEX(PARMS,"(")
      IF SUBSTR(PARMS,1,1) = "'" THEN DS = SUBSTR(PARMS,1,II-1) || "'"
                                 ELSE DS = SUBSTR(PARMS,1,II-1)
      JJ = INDEX(PARMS,")")
      MWC = SUBSTR(PARMS,II+1,JJ-II-1)
      II = INDEX(MWC,"*")
      IF II > 0 THEN MWC = SUBSTR(MWC,1,II-1)
   END
   ELSE DO
      DS = PARMS
      MWC = "*"
   END

   "ISREDIT (LINE) = CURSOR"
   X=OUTTRAP(MEMBER.)
   "LISTDS" DS "HIST MEMBER"
   X=OUTTRAP('OFF')
   DO II = MEMBER.0 TO 1 BY -1
      IF MEMBER.II = "--MEMBERS--" THEN LEAVE
      MEMBER.II = STRIP(MEMBER.II,'L')
      MEMNAME = MEMBER.II
      IF MWC = "*" | SUBSTR(MEMNAME,1,LENGTH(MWC)) = MWC,
         THEN "ISREDIT LINE_AFTER &LINE = '&MEMNAME'"
   END
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "GETMEMS - PLACE LIST OF MEMBER NAMES AFTER CURSOR         "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    GETMEMS  PDS | ?                                      "
   SAY
   SAY "       RETRIEVE MEMBER LIST FROM DATA SET PROVIDED AND    "
   SAY "       PLACE LIST AFTER CURSOR. A WILDCARD MAY BE USED AS "
   SAY
   SAY "          GETMEMS  'SYS4.REXX.EXEC(HI*)'                  "
   SAY
   SAY "       WOULD ONLY LIST THOSE MEMBERS OF THE PDS WITH      "
   SAY "       'HI' AS THE FIRST CHARACTERS OF EACH MEMBER.  IF   "
   SAY "       NO PARAMETERS ARE PASSED OR A SINGLE QUESTION MARK "
   SAY "       (?), THIS HELP WILL BE DISPLAYED.                  "
   SAY
   EXIT(1)

