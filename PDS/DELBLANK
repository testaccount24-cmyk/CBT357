/**  REXX -- DELETE BLANK LINES; USE LABEL RANGE IF SPECIFIED  **/

   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'DELBLANK MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
             "ISREDIT MACRO (LBL1,LBL2)"
      IF RC = 0 THEN DO
         IF LBL1 = '?' THEN SIGNAL DISPDOC
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY 'DELBLANK EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT',
          'WITH NO PENDING EDIT COMMANDS!'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG = ''

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
       "ISREDIT (START) = LINENUM &LBL1"
   END
   ELSE START = 1

   IF LBL2 <> '' THEN DO
       "ISREDIT L &LBL2"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
       END
       "ISREDIT (END) = LINENUM &LBL2"
   END
   ELSE "ISREDIT (END) = LINENUM .ZLAST "

   IF START > END THEN DO
      FLIP  = START
      START = END
      END   = FLIP
   END

   "ISREDIT ("LRECL") = LRECL "
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR "
   "ISREDIT ("ANUM") = AUTONUM "

   PNTR = END
   DO WHILE PNTR >= START
      "ISREDIT (RECORD) = LINE &PNTR"
      IF RECORD = ' ' THEN "ISREDIT DELETE &PNTR"
      PNTR = PNTR - 1
   END

  "ISREDIT LOCATE" 1
  "ISREDIT AUTONUM = "ANUM
   SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '*** ERROR *** ' MESG
   END

EOJ:
   X = MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "DELBLANK - DELETE ALL BLANK LINES WITHIN OPTIONAL RANGE"
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY "    DELBLANK  ( .A .B )"
   SAY "       THE LABEL RANGE IS OPTIONAL. "
   SAY
   SAY " EXAMPLE "
   SAY "    DELBLANK "
   SAY "       WILL DELETE ANY BLANK LINES IN THE EDITED FILE "
   SAY
   SAY "    DELBLANK  .AA .BB "
   SAY "       WILL DELETE ANY BLANK LINES IN THE LABEL RANGE "
   SAY "       .AA THRU .BB "
   SAY
   SIGNAL EOJ
