/**  REXX -- SAVE MEMBER IN PLACE                                 **/
/*    THE SAVED MEMBER MUST BE THE SAME SIZE AS THE ORIGINAL       */

   ADDRESS ISPEXEC
   'CONTROL ERRORS RETURN'
   'ISREDIT MACRO (PARMS)'
   IF RC ¬=0 THEN DO
      ZEDSMSG = ''
      ZEDLMSG = ' SAVEINPL CAN ONLY RUN AS AN ISPF EDIT MACRO !!!'
      'SETMSG MSG(ISRZ001)'
      EXIT
   END
   IF PARMS = '?' THEN SIGNAL DISPDOC

   'ISREDIT (MYDSN) = DATASET'
   IF DSN_NOGOOD(MYDSN) THEN EXIT
   'ISREDIT (MYMEM)    = MEMBER'
   'ISREDIT (STRTLINE) = LINENUM .ZFIRST'
   'ISREDIT (ENDLINE)  = LINENUM .ZLAST'
   'ISREDIT RECOVERY ON'
   'ISREDIT RESET'

   NUM_LINES = (ENDLINE-STRTLINE)+1
   DATANAME  = "'"||MYDSN||"("||MYMEM||")'"
   FILENAME  = '##'||RANDOM(1000,9999)||RANDOM(10,99)
   "ISREDIT ("LRECL") = LRECL"
   ADDRESS TSO "ALLOC DA("||DATANAME||") FILE("||FILENAME||") SHR"

   ADDRESS TSO 'EXECIO * DISKR' FILENAME '(STEM NEWLINE. FINIS'
   IF NEWLINE.0 <> NUM_LINES THEN DO
      ZEDSMSG = ''
      ZEDLMSG = 'NUMBER OF RECORDS WRITTEN OUT IS DIFFERENT THAN',
                'SIZE OF OLD MEMBER. UPDATE NOT ALLOWED.',
                'NUMBER OF OLD RECORDS='||NEWLINE.0||'. NUMBER OF NEW',
                'RECORDS='||NUM_LINES||'.'
      'SETMSG MSG(ISRZ001)'
      ADDRESS TSO 'FREE FILE('FILENAME')'
      EXIT(1)
   END

   DO I=STRTLINE TO ENDLINE
      'ISREDIT (MYINPUT) = LINE' I
      NEWLINE.I = MYINPUT
   END

   CNT = 0
   NOT_THE_END = 1
   DO WHILE NOT_THE_END
      ADDRESS TSO 'EXECIO 1 DISKRU' FILENAME
      TEMPRC = RC
      IF TEMPRC=2 THEN NOT_THE_END = 0
      ELSE DO
         IF TEMPRC ¬= 0 THEN DO
            ZEDSMSG = ''
            ZEDLMSG = 'EXECIO RETCODE='||TEMPRC||'.',
                      'PARTIAL DATA WAS WRITTEN OUT - PROCESS ABORTED.'
            'SETMSG MSG(ISRZ001)'
            CALL CLOSE_FILE
            EXIT
         END
         ELSE DO
            PULL
            CNT = CNT + 1
            QUEUE NEWLINE.CNT
            ADDRESS TSO 'EXECIO 1 DISKW' FILENAME
         END
      END
   END  /* DO WHILE NOT_THE_END */

   CALL CLOSE_FILE

   ZEDSMSG = ''
   ZEDLMSG = MYMEM||" HAS BEEN SUCCESSFULLY SAVED IN "||MYDSN||". ",
      "DATA BEING EDITED WAS UPDATED IN PLACE.  HOWEVER, THE",
      "THE ISPF STATISTICS WERE NOT CHANGED AND",
      "THEREFORE ARE NO LONGER CORRECT."
   'SETMSG MSG(ISRZ001)'
   'ISREDIT CANCEL'
   EXIT

CLOSE_FILE:
   ADDRESS TSO 'EXECIO 0 DISKW' FILENAME '(FINIS'
   ADDRESS TSO 'FREE FILE('FILENAME')'
   RETURN

DSN_NOGOOD:
   ARG CHECK_DSN
   CHECK_DSN = "'"||STRIP(CHECK_DSN)||"'"
   X = LISTDSI(CHECK_DSN 'NODIRECTORY NORECALL')
   IF X = 16 THEN DO
      IF SYSREASON = 9 THEN DO
         ZEDSMSG = 'DATA SET MIGRATED'
         ZEDLMSG = ''
         'SETMSG MSG(ISRZ001)'
         RETURN 1
      END
      IF SYSREASON = 5 THEN DO
         ZEDSMSG = 'DATASET NOT CATALOGED'
         ZEDLMSG = ''
         'SETMSG MSG(ISRZ001)'
         RETURN 1
      END
      ELSE DO
         ZEDSMSG = 'LISTDSI RC=16'
         ZEDLMSG = 'LISTDSI ERROR RC=16 - SYSREASON=' SYSREASON
         'SETMSG MSG(ISRZ001)'
         RETURN 1
      END
   END
   IF SYSRECFM = 'F' | SYSRECFM = 'FB' THEN NOP
   ELSE DO
      ZEDSMSG = ''
      ZEDLMSG = 'INVALID RECFM AS '||SYSRECFM||'.',
                'SAVEINPL CAN ONLY BE USED TO PROCESS RECFM F OR FB.'
      'SETMSG MSG(ISRZ001)'
      RETURN 1
   END
   IF SYSDSORG = 'PO' THEN RETURN 0
   ELSE DO
      ZEDSMSG = ''
      ZEDLMSG = 'INVALID DSORG AS '||SYSDSORG||'.',
                'SAVEINPL CAN ONLY BE USED TO UPDATE A PDS MEMBER.'
      'SETMSG MSG(ISRZ001)'
      RETURN 1
   END

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SAVEINPL - OVERLAY CURRENT MEMBER WITH THIS DATA"
   SAY
   SAY " FORMAT "
   SAY
   SAY "    SAVEINPL "
   SAY
   SAY "       THE DATA BEING EDITED WILL OVERLAY THE CURRENT MEMBER'S"
   SAY "       DATA IN THE PDS. THE PDS MUST BE FIXED RECORD FORMAT,  "
   SAY "       RECFM EQUAL TO F OR FB, AND THE NUMBER OF LINES MUST   "
   SAY "       NOT HAVE CHANGED.                                      "
   SAY
   EXIT(1)
