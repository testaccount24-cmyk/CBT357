/**  REXX -- ALIGN COLUMNS BASED ON ONE OR MORE CHARACTERS          **/

   ADDRESS ISREDIT
   "MACRO (PARM) NOPROCESS"
   IF PARM = "" | PARM = "?" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      TOP = 1
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END
   IF SUBSTR(PARM,1,1) = '"' | SUBSTR(PARM,1,1) = "'",
      THEN PARM = TRANSLATE(SUBSTR(PARM,2,LENGTH(PARM)-2))
      ELSE PARM = TRANSLATE(PARM)

   OFFSET = 0
   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      II = POS(PARM,REC)
      IF II > OFFSET & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D",
         THEN OFFSET = II
   END NN

   DO NN = TOP TO BOT
      "(REC) = LINE" NN
      II = POS(PARM,REC)
      IF II > 0 & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D" THEN DO
         REC = SUBSTR(REC,1,II-1) || COPIES(" ",OFFSET-II),
            || PARM || SUBSTR(REC,II+LENGTH(PARM))
         "LINE " NN " = (REC)"
      END
   END NN

   "LOCATE" TOP
   RETURN(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "ALIGN - ALIGN COLUMNS                                     "
   SAY
   SAY " FORMAT "
   SAY "    ALIGN  CHAR                                           "
   SAY "       WILL ALIGN THE SPECIFIED LINES AT THE CHARACTER    "
   SAY "       OR CHARACTERS PROVIDED.  A RANGE OF LINES MAY BE   "
   SAY "       SPECIFIED BY C/CC/C## OR THE ENTIRE FILE WILL BE   "
   SAY "       PROCESSED.  NO CHAR SPECIFIED OR ONLY A ? WILL     "
   SAY "       CAUSE THIS HELP TO BE SHOWN. QUOTES MAY BE USED.   "
   SAY
   SAY " EXAMPLE "
   SAY "    ALIGN  ' TO '                                         "
   SAY "       ON THE FOLLOWING:                                  "
   SAY "          MOVE 7 TO WS-SHORT-FIELD                        "
   SAY "          MOVE     16    TO WS-MIDDLE-FIELD               "
   SAY "          MOVE     427 TO WS-REAL-BIG-FIELD               "
   SAY "       WILL ALIGN AS:                                     "
   SAY "          MOVE 7         TO  WS-SHORT-FIELD               "
   SAY "          MOVE     16    TO  WS-MIDDLE-FIELD              "
   SAY "          MOVE     427   TO  WS-REAL-BIG-FIELD            "
   EXIT(1)

