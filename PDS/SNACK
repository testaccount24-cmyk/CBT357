/* REXX -- SNACK -- X ALL AND F ALL THE STRING THE CURSOR IS ON     */

   ADDRESS ISREDIT
   "MACRO (PARMS)"
   IF PARMS = "?" THEN SIGNAL DISPDOC
   ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
   "(ROW,COL) = CURSOR"
   "(DATA1)   = LINE  " ROW

   DO JJ = COL-1 TO  1 BY -1 WHILE ( SUBSTR(DATA1,JJ,1) <> ' ' )
   END
   DO KK = COL+1 TO 72 BY  1 WHILE ( SUBSTR(DATA1,KK,1) <> ' ' )
   END

   SRCHSTR = SUBSTR(DATA1,JJ+1,KK-JJ-1)
   "X ALL"
   "FIND ALL" SRCHSTR
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SNACK - DISPLAY ONLY LINES WITH STRING UNDER CURSOR "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    SNACK  ( ? )                                              "
   SAY
   SAY "       DOES AN 'X ALL' AND THEN A 'FIND ALL' AGAINST THE      "
   SAY "       STRING UNDER THE CURSOR. THE STRING MUST BE DELIMITED  "
   SAY "       BY BLANKS OR BE AT THE START OR END OF A LINE.         "
   SAY
   EXIT(1)


