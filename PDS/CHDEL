/** REXX -- DYNAMICALLY CREATE HDEL UP JCL AFTER CURSOR POSITION **/
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'CHDEL EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS) NOPROCESS"
      PARMS = TRANSLATE(PARMS)
      IF PARMS = "?" THEN SIGNAL DISPDOC
   END
   ELSE DO
      SAY 'CHDEL EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END

   ADDRESS ISREDIT
   "(LINE,COL) = CURSOR"
   IF LINE = 1  THEN DO
       ADDRESS ISPEXEC "VGET (ZSCREENC,ZSCREENI) SHARED"
       II = INDEX(ZSCREENI,'* Top of Data *')
       IF ( ZSCREENC % 80 )  <=  ( II % 80 )  THEN LINE = 0
       ELSE LINE = 1
   END
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      "(TOP) = LINENUM .ZFIRST"
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END

   CARD5 = "  CALL  *(IEFBR14)"
   CARD4 = "//SYSTSPRT     DD       SYSOUT=*"
   CARD3 = "//SYSTSIN      DD       *"
   CARD2 = "//BATCHTSO   EXEC     PGM=IKJEFT01,REGION=4M,DYNAMNBR=32"
   CARD1 = "//***"

   ARRAY.0 = 0
   DO II = TOP TO BOT
      "ISREDIT (REC) = LINE &II"
      IF (POS('DISP=(,CAT',REC) <> 0 | POS('DISP=(NEW,CAT',REC) <> 0),
            & SUBSTR(REC,1,3) <> '//*' THEN DO
         JJ = POS('DSN=',REC)
         IF JJ = 0 THEN DO
            JJ = POS('DSNAME=',REC)
            IF JJ <> 0 THEN JJ = JJ + 3
         END
         IF JJ = 0 THEN DO
            XX = II - 1
            "ISREDIT (REC) = LINE &XX"
            DO WHILE(SUBSTR(REC,1,3) = '//*' | SUBSTR(REC,1,2) = '/*')
               XX = XX - 1
               "ISREDIT (REC) = LINE &XX"
            END
            DO KK = 71 TO 1 BY -1 WHILE(SUBSTR(REC,KK,1) = ' ')
            END KK
            DO WHILE(SUBSTR(REC,KK,1) = ',')
               JJ = POS('DSN=',REC)
               IF JJ = 0 THEN DO
                  JJ = POS('DSNAME=',REC)
                  IF JJ <> 0 THEN JJ = JJ + 3
               END
               IF JJ <> 0 THEN LEAVE
               XX = XX - 1
               "ISREDIT (REC) = LINE &XX"
               DO WHILE(SUBSTR(REC,1,3) = '//*')
                  XX = XX - 1
                  "ISREDIT (REC) = LINE &XX"
               END
               DO KK = 71 TO 1 BY -1 WHILE(SUBSTR(REC,KK,1) = ' ')
               END KK
            END
            IF JJ = 0 THEN DO
               XX = II
               "ISREDIT (REC) = LINE &XX"
               DO KK = 71 TO 1 BY -1 WHILE(SUBSTR(REC,KK,1) = ' ')
               END KK
               DO WHILE(SUBSTR(REC,KK,1) = ',')
                  XX = XX + 1
                  "ISREDIT (REC) = LINE &XX"
                  DO WHILE(SUBSTR(REC,1,3) = '//*')
                     XX = XX + 1
                     "ISREDIT (REC) = LINE &XX"
                  END
                  JJ = POS('DSN=',REC)
                  IF JJ = 0 THEN DO
                     JJ = POS('DSNAME=',REC)
                     IF JJ <> 0 THEN JJ = JJ + 3
                  END
                  IF JJ <> 0 THEN LEAVE
                  DO KK = 71 TO 1 BY -1 WHILE(SUBSTR(REC,KK,1) = ' ')
                  END KK
               END
            END
         END
         IF JJ <> 0 THEN DO
            JJ = JJ + 4
            DO KK = (JJ+1) TO 71,
               WHILE(SUBSTR(REC,KK,1) <> ' ' & SUBSTR(REC,KK,1) <> ',')
            END KK
            ARRAY.0 = ARRAY.0 + 1
            NN      = ARRAY.0
            ARRAY.NN = SUBSTR(REC,JJ,KK-JJ)
         END
      END
   END II

   IF ARRAY.0 > 0 THEN DO
      "LINE_AFTER (LINE) = (CARD1)"
      "LINE_AFTER (LINE) = (CARD4)"
      "LINE_AFTER (LINE) = (CARD5)"
   END
   DO II = ARRAY.0 TO 1 BY -1
      CARD  = "  HDEL  '" || ARRAY.II || "'"
      "LINE_AFTER (LINE) = (CARD)"
   END II

   IF ARRAY.0 > 0 THEN DO
      "LINE_AFTER (LINE) = (CARD3)"
      "LINE_AFTER (LINE) = (CARD2)"
      "LINE_AFTER (LINE) = (CARD1)"
   END
   ELSE SAY "No 'NEW,CATLG' dispositions found; no JCL generated!"

   IF LINE = 0 AND COL = 0 THEN EXIT(1)
   ELSE DO
      'ISREDIT CURSOR = ' LINE COL
      EXIT(0)
   END

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "CHDEL - DYNAMICALLY GENERATE CLEAN UP JCL AFTER CURSOR    "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    CHDEL  ( ? )                                          "
   SAY
   SAY "       CREATE HDELETE JCL FOR EVERY NEW CATALOGED DATA    "
   SAY "       SET IN THE FILE AND PLACE THEM IMMEDIATELY AFTER   "
   SAY "       WHERE THE CURSOR IS POSITIONED.  C/CC/C# MAY BE    "
   SAY "       USED TO LIMIT THE SEARCH.                          "
   SAY
   SAY "       A QUESTION MARK ALONE CAUSES THIS HELP TO DISPLAY. "
   SAY
   EXIT(1)

