/**  REXX -- Write lines from a file to the profile  **/

   Address ISPEXEC
   'ISREDIT MACRO (Parm1) NOPROCESS'
   Parm1 = translate(Parm1)
   If Parm1 = '?' then signal DispDoc

   'ISREDIT PROCESS RANGE C M'
   Select
       When rc = 0 then do
           'ISREDIT (CMD)   = RANGE_CMD'
           'ISREDIT (LINE1) = LINENUM .ZFRANGE'
           'ISREDIT (LINE2) = LINENUM .ZLRANGE'
           LinesToCut = Line2 - Line1 + 1
       End
       When rc <= 4 then do
           zedsmsg = 'Enter "C"/"M" line cmd'
           zedlmsg = 'CUT requires a "C" or "M" line command'
           'SETMSG MSG(ISRZ001)'
           Exit 12
       End
       Otherwise  exit(12)
   End

   'VGET (CUTCNT) PROFILE'
   If RC <> 0 then CutCnt = 0
   If Parm1 <> 'A' & Parm1 <> 'APPEND' then do
       If CutCnt <> 0 then do
           Do i = 1 to CutCnt by 1
             'VERASE (CL'i') PROFILE'
           End
       End
       CutCnt = 0
   End

   Control Errors Return
   Do i = Line1 to Line2
       CutCnt = CutCnt + 1
       'ISREDIT (CL'CutCnt') = LINE' i
       Interpret "CL"CutCnt"= Strip(CL"CutCnt",'T')"
       'VPUT (CL'CutCnt') PROFILE'
       If RC <> 0 then do
           RetCode = RC
           Say '** An error occurred when placing the data',
               'into the Profile Pool'
           Say '** You may be trying to CUTX too much data',
               '- please try a smaller amount'
           Say '** The code passed back was' RetCode
           Exit(RetCode)
       End
   End
   'VPUT (CUTCNT) PROFILE'

   If Cmd = 'M' then do
       'ISREDIT DELETE' Line1 Line2
        zedsmsg = LinesToCut 'lines cut and deleted'
        msg = 'lines were cut and deleted from the current file'
        zedlmsg = LinesToCut msg
       'SETMSG MSG(ISRZ000)'
   End
   Else do
        zedsmsg = LinesToCut 'lines cut'
        zedlmsg = LinesToCut 'lines were cut from the current file'
       'SETMSG MSG(ISRZ000)'
   End
   Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
   Say 'CUTX will place or append lines to the PROFILE pool for later'
   Say 'inclusion by the various paste macros.                       '
   Say
   Say '     CUTX  ( A | APPEND )                                    '
   Say
   Say 'Enter CUTX on the COMMAND line and use the C or M line       '
   Say '(in any form) to select the lines to be cut. If the M line   '
   Say 'command is used, the lines will be deleted.                  '
   Say
   Say 'A parameter of A or APPEND can be specified to add to any    '
   Say 'previously cut lines (that have not yet been pasted) with the'
   Say 'newly selected lines.  Otherwise, the selected lines will    '
   Say 'replace any previously cut lines.                            '
   Say
   Exit(1)
