/**  REXX  --  SUBMIT THE JOB AFTER SUBSTITUTIONS  **/

   SAVEMSG = MSG("OFF")
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'SUBO MUST BE RUN UNDER ISPF!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (PARMS) PROCESS"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      ARG PARMS
      SIGNAL DSLIST
   END

   IF SUBSTR(PARMS,1,1) = '?' THEN SIGNAL DISPDOC
   CALL PARSE_PARMS
   "ISREDIT (THISMEM) = MEMBER"
   "ISREDIT (THISDSN) = DATASET"
   IF THISMEM <> ""
      THEN ZINPDSN = "'" || THISDSN || "(" || THISMEM || ")'"
      ELSE ZINPDSN = "'" || THISDSN || "'"

   ADDRESS TSO
   "FREE DD(SUBODD)"
   "ALLOC DD(SUBODD) SYSOUT(A) WRITER(INTRDR)"
   "EXECIO 0 DISKW "SUBODD"(OPEN"
   "ISREDIT (START) = LINENUM" .ZFIRST
   "ISREDIT (END)   = LINENUM" .ZLAST

   ORIGIN_NOW = "YES"
   DO PNTR =  START  TO  END  BY  1
      "ISREDIT (RECORD)   = LINE &PNTR"
      AMPER = 0
      XX = INDEX(RECORD,"&")
      IF XX > 0 THEN DO WHILE XX > 0
         CALL PROCESS_TOKENS
      END
      IF AMPER > 0 THEN DO KK = 1 TO AMPER
         LL = REAL.KK
         RECORD = OVERLAY('&',RECORD,LL,1)
      END
      QUEUE RECORD
      "EXECIO 1 DISKW" SUBODD
      IF ORIGIN = "YES" THEN DO
         IF ORIGIN_NOW = "YES" THEN DO
            DO II = 71 TO 1 BY -1 WHILE SUBSTR(RECORD,II,1) = " "
            END
            IF SUBSTR(RECORD,II,1) <> "," THEN DO
               CALL GEN_ORIGIN
               ORIGIN_NOW = "NO"
            END
         END
      END
   END

   IF ABEND <> "NO" THEN CALL GEN_ABEND
   "EXECIO 0 DISKW "SUBODD"(FINIS"
   "FREE DD(SUBODD)"
   CALL SETMSG "* NO JOB SUBMITTED$JOB SUBMITTED SUCCESSFULLY!"
   EXIT(0)

DSLIST:
   DO II = LENGTH(PARMS)-1 TO 1 BY -1 WHILE(SUBSTR(PARMS,II,1) <> "'")
   END II
   ZINPDSN = SUBSTR(PARMS,II)
   PARMS   = SUBSTR(PARMS,1,II-1)

   CALL PARSE_PARMS

   ADDRESS TSO
   "FREE DD(DDXX SUBODD)"
   "ALLOC DSN("ZINPDSN") DD(DDXX) SHR"
   "ALLOC DD(SUBODD) SYSOUT(A) WRITER(INTRDR)"
   "EXECIO 0 DISKR" DDXX   "(OPEN"
   "EXECIO 0 DISKW" SUBODD "(OPEN"

   "EXECIO 1 DISKR" DDXX
   RET_CD     = RC
   ORIGIN_NOW = 'YES'
   DO WHILE ( RET_CD = 0 )
      PULL RECORD
      AMPER = 0
      XX = INDEX(RECORD,"&")
      IF XX > 0 THEN DO WHILE XX > 0
         CALL PROCESS_TOKENS
      END
      IF AMPER > 0 THEN DO KK = 1 TO AMPER
         LL = REAL.KK
         RECORD = OVERLAY('&',RECORD,LL,1)
      END
      QUEUE RECORD
      "EXECIO 1 DISKW" SUBODD
      IF ORIGIN = 'YES' THEN DO
         IF ORIGIN_NOW = 'YES' THEN DO
            DO II = 71 TO 1 BY -1 WHILE SUBSTR(RECORD,II,1) = ' '
            END
            IF SUBSTR(RECORD,II,1) <> ',' THEN DO
               CALL GEN_ORIGIN
               ORIGIN_NOW = 'NO'
            END
         END
      END
      "EXECIO 1 DISKR" DDXX
      RET_CD = RC
   END

   IF ABEND <> "NO" THEN CALL GEN_ABEND

   "EXECIO 0 DISKW" DDXX "(FINIS"
   "FREE DD(DDXX SUBDDX)"
   EXIT(0)

PROCESS_TOKENS:
   IF SUBSTR(RECORD,XX+1,10) = "CCYY/MM/DD" THEN DO
      SS = DATE(S)
      RECORD = SUBSTR(RECORD,1,XX-1),
         || SUBSTR(SS,1,4) || "/" || SUBSTR(SS,5,2),
         || "/" || SUBSTR(SS,7,2),
         || SUBSTR(RECORD,XX+11)
   END
   ELSE IF SUBSTR(RECORD,XX+1,08) = "CCYY/DDD" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || LEFT(DATE(S),4) || "/" || RIGHT(DATE(J),3),
         || SUBSTR(RECORD,XX+9)
   END
   ELSE IF SUBSTR(RECORD,XX+1,07) = "CCYYDDD" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || LEFT(DATE(S),4) || RIGHT(DATE(J),3),
         || SUBSTR(RECORD,XX+8)
   END
   ELSE IF SUBSTR(RECORD,XX+1,08) = "CCYYMMDD" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || DATE(S),
         || SUBSTR(RECORD,XX+9)
   END
   ELSE IF SUBSTR(RECORD,XX+1,08) = "DD/MM/YY" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || DATE(E),
         || SUBSTR(RECORD,XX+9)
   END
   ELSE IF SUBSTR(RECORD,XX+1,08) = "HH:MM:SS" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || TIME(),
         || SUBSTR(RECORD,XX+9)
   END
   ELSE IF SUBSTR(RECORD,XX+1,05) = "HH:MM" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || LEFT(TIME(),5),
         || SUBSTR(RECORD,XX+6)
   END
   ELSE IF SUBSTR(RECORD,XX+1,06) = "HHMMSS" THEN DO
      SS = TIME()
      RECORD = SUBSTR(RECORD,1,XX-1),
         || SUBSTR(SS,1,2),
         || SUBSTR(SS,4,2),
         || SUBSTR(SS,7,2),
         || SUBSTR(RECORD,XX+7)
   END
   ELSE IF SUBSTR(RECORD,XX+1,04) = "HHMM"  THEN DO
      SS = TIME()
      RECORD = SUBSTR(RECORD,1,XX-1),
         || SUBSTR(SS,1,2),
         || SUBSTR(SS,4,2),
         || SUBSTR(RECORD,XX+5)
   END
   ELSE IF SUBSTR(RECORD,XX+1,08) = "MM/DD/YY" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || DATE(U),
         || SUBSTR(RECORD,XX+9)
   END
   ELSE IF SUBSTR(RECORD,XX+1,06) = "MMDDYY" THEN DO
      SS = DATE(U)
      RECORD = SUBSTR(RECORD,1,XX-1),
         || SUBSTR(SS,1,2),
         || SUBSTR(SS,4,2),
         || SUBSTR(SS,7,2),
         || SUBSTR(RECORD,XX+7)
   END
   ELSE IF SUBSTR(RECORD,XX+1,05) = "MONTH" THEN DO
      SS = DATE(U)
      II = SUBSTR(SS,1,2)
      IF II = 1 THEN MONTH_NAME = 'JANUARY'
         ELSE IF II =  2 THEN MONTH_NAME = 'FEBRUARY'
         ELSE IF II =  3 THEN MONTH_NAME = 'MARCH'
         ELSE IF II =  4 THEN MONTH_NAME = 'APRIL'
         ELSE IF II =  5 THEN MONTH_NAME = 'MAY'
         ELSE IF II =  6 THEN MONTH_NAME = 'JUNE'
         ELSE IF II =  7 THEN MONTH_NAME = 'JULY'
         ELSE IF II =  8 THEN MONTH_NAME = 'AUGUST'
         ELSE IF II =  9 THEN MONTH_NAME = 'SEPTEMBER'
         ELSE IF II = 10 THEN MONTH_NAME = 'OCTOBER'
         ELSE IF II = 11 THEN MONTH_NAME = 'NOVEMBER'
         ELSE IF II = 12 THEN MONTH_NAME = 'DECEMBER'
      RECORD = SUBSTR(RECORD,1,XX-1),
         || MONTH_NAME,
         || SUBSTR(RECORD,XX+6)
   END
   ELSE IF SUBSTR(RECORD,XX+1,03) = "MON" THEN DO
      SS = DATE(U)
      II = SUBSTR(SS,1,2)
      IF II = 1 THEN MONTH_NAME = 'JAN'
         ELSE IF II =  2 THEN MONTH_NAME = 'FEB'
         ELSE IF II =  3 THEN MONTH_NAME = 'MAR'
         ELSE IF II =  4 THEN MONTH_NAME = 'APR'
         ELSE IF II =  5 THEN MONTH_NAME = 'MAY'
         ELSE IF II =  6 THEN MONTH_NAME = 'JUN'
         ELSE IF II =  7 THEN MONTH_NAME = 'JUL'
         ELSE IF II =  8 THEN MONTH_NAME = 'AUG'
         ELSE IF II =  9 THEN MONTH_NAME = 'SEP'
         ELSE IF II = 10 THEN MONTH_NAME = 'OCT'
         ELSE IF II = 11 THEN MONTH_NAME = 'NOV'
         ELSE IF II = 12 THEN MONTH_NAME = 'DEC'
      RECORD = SUBSTR(RECORD,1,XX-1),
         || MONTH_NAME,
         || SUBSTR(RECORD,XX+4)
   END
   ELSE IF SUBSTR(RECORD,XX+1,07) = "DAYOFWK" THEN DO
      II = DATE('B')//7
      IF II = 0 THEN MONTH_NAME = 'MONDAY'
         ELSE IF II =  1 THEN MONTH_NAME = 'TUESDAY'
         ELSE IF II =  2 THEN MONTH_NAME = 'WEDNESDAY'
         ELSE IF II =  3 THEN MONTH_NAME = 'THURSDAY'
         ELSE IF II =  4 THEN MONTH_NAME = 'FRIDAY'
         ELSE IF II =  5 THEN MONTH_NAME = 'SATURDAY'
         ELSE IF II =  6 THEN MONTH_NAME = 'SUNDAY'
      RECORD = SUBSTR(RECORD,1,XX-1),
         || MONTH_NAME,
         || SUBSTR(RECORD,XX+8)
   END
   ELSE IF SUBSTR(RECORD,XX+1,03) = "DOW" THEN DO
      II = DATE('B')//7
      IF II = 0 THEN MONTH_NAME = 'MON'
         ELSE IF II =  1 THEN MONTH_NAME = 'TUE'
         ELSE IF II =  2 THEN MONTH_NAME = 'WED'
         ELSE IF II =  3 THEN MONTH_NAME = 'THU'
         ELSE IF II =  4 THEN MONTH_NAME = 'FRI'
         ELSE IF II =  5 THEN MONTH_NAME = 'SAT'
         ELSE IF II =  6 THEN MONTH_NAME = 'SUN'
      RECORD = SUBSTR(RECORD,1,XX-1),
         || MONTH_NAME,
         || SUBSTR(RECORD,XX+4)
   END
   ELSE IF SUBSTR(RECORD,XX+1,06) = "USERID" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || USERID(),
         || SUBSTR(RECORD,XX+7)
   END
   ELSE IF SUBSTR(RECORD,XX+1,04) = "YEAR" THEN DO
      SS = DATE(S)
      RECORD = SUBSTR(RECORD,1,XX-1),
         || SUBSTR(SS,1,4),
         || SUBSTR(RECORD,XX+5)
   END
   ELSE IF SUBSTR(RECORD,XX+1,08) = "YY/MM/DD" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || DATE(O),
         || SUBSTR(RECORD,XX+9)
   END
   ELSE IF SUBSTR(RECORD,XX+1,05) = "YYDDD" THEN DO
      RECORD = SUBSTR(RECORD,1,XX-1),
         || DATE(J),
         || SUBSTR(RECORD,XX+6)
   END
   ELSE IF SUBSTR(RECORD,XX+1,06) = "YYMMDD" THEN DO
      SS = DATE(U)
      RECORD = SUBSTR(RECORD,1,XX-1),
         || SUBSTR(SS,7,2),
         || SUBSTR(SS,1,2),
         || SUBSTR(SS,4,2),
         || SUBSTR(RECORD,XX+7)
   END
   ELSE DO
      RECORD = OVERLAY('.',RECORD,XX,1)
      AMPER = AMPER + 1
      REAL.AMPER = XX
   END
   XX = INDEX(RECORD,"&")
   RETURN

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

GEN_ORIGIN:
   ORGREC = "//***"
   QUEUE ORGREC
   "EXECIO 1 DISKW" SUBODD
   DD = SUBSTR(DATE(U),4,2)
   IF SUBSTR(DD,2,1) = '1'         THEN XX = 'ST'
      ELSE IF SUBSTR(DD,2,1) = '2' THEN XX = 'ND'
      ELSE IF SUBSTR(DD,2,1) = '3' THEN XX = 'RD'
      ELSE XX = 'TH'
   IF SUBSTR(DD,1,1) = '0'         THEN DD = SUBSTR(DD,2)
   UDOW = TRANSLATE(DATE(W))
   UMON = TRANSLATE(DATE(M))
   ORGREC = "//***  SUBMITTED BY ",
      || USERID() || " ON " || UDOW || " " || UMON,
      || " " || DD||XX',' SUBSTR(DATE(S),1,4),
      || " (DAY" SUBSTR(DATE(J),3,3)")"
   QUEUE ORGREC
   "EXECIO 1 DISKW" SUBODD
   ORGREC = "//***    " || " AT " || TIME(),
      ||  " FROM " || ZINPDSN
   QUEUE ORGREC
   "EXECIO 1 DISKW" SUBODD
   ORGREC = "//***"
   QUEUE ORGREC
   "EXECIO 1 DISKW" SUBODD
   ORIGIN = "NO"
   RETURN

PARSE_PARMS:
   PARMS     = TRANSLATE(PARMS)
   ABEND     = "NO"
   CODE      = "666"
   ORIGIN    = "YES"
   DO II = 1 TO 10
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF TOKEN = "ORIGIN" | TOKEN = "ORG" | TOKEN = "O",
         THEN ORIGIN = "YES"
      IF SUBSTR(TOKEN,1,5) = "ABEND" THEN DO
         IF LENGTH(TOKEN) = 5 THEN ABEND = "0,LT"
         ELSE ABEND = SUBSTR(TOKEN,7,LENGTH(TOKEN)-7)
      END
      IF SUBSTR(TOKEN,1,4) = "CODE" THEN DO
         CODE = SUBSTR(TOKEN,6,LENGTH(TOKEN)-6)
      END
   END II
   RETURN

GEN_ABEND:
   ABENDREC = "//***"
   QUEUE ABENDREC
   "EXECIO 1 DISKW" SUBODD
   ABENDREC = "//ABEND      EXEC",
      || "     PGM=ABENDX,PARM=",
      || CODE || ",COND=(" || ABEND || ")"
   QUEUE ABENDREC
   "EXECIO 1 DISKW" SUBODD
   ABENDREC = "//STEPLIB      DD       DISP=SHR,DSN=",
      || "-YOUR-LOADLIB-"
   QUEUE ABENDREC
   "EXECIO 1 DISKW" SUBODD
   ABENDREC = "//***"
   QUEUE ABENDREC
   "EXECIO 1 DISKW" SUBODD
   RETURN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SUBO -- SUBMIT FILE WITH VAR'S SUB & ORIGIN & ABEND   "
   SAY
   SAY "   SUBO  ( ABEND(COND) )  ( CODE(#) )  ( NOORIGIN )   "
   SAY
   SAY "     ABEND SPECIFIES COND CODE, DEFAULTS TO '0,LT';   "
   SAY "     CODE SPECIFIES USER ABEND CODE, DEFAULTS TO 666; "
   SAY "     NOORIGIN CAUSES COMMENTS WITH DATE/TIME, SOURCE  "
   SAY "     AND USER NOT TO BE DISPLAYED. TOKENS ARE:        "
   SAY
   SAY "       &CCYY/MM/DD ==> 2001/02/03                     "
   SAY "       &CCYY/DDD   ==> 2001/034                       "
   SAY "       &CCYYDDD    ==> 2001034                        "
   SAY "       &CCYYMMDD   ==> 20010203                       "
   SAY "       &DAYOFWK    ==> SATURDAY                       "
   SAY "       &DD/MM/YY   ==> 03/02/01                       "
   SAY "       &DOW        ==> SAT                            "
   SAY "       &HH:MM      ==> 12:34                          "
   SAY "       &HH:MM:SS   ==> 12:34:56                       "
   SAY "       &HHMM       ==> 1234                           "
   SAY "       &HHMMSS     ==> 123456                         "
   SAY "       &MM/DD/YY   ==> 02/03/01                       "
   SAY "       &MMDDYY     ==> 020301                         "
   SAY "       &MON        ==> FEB                            "
   SAY "       &MONTH      ==> FEBRUARY                       "
   SAY "       &USERID     ==> SPRK0JG                        "
   SAY "       &YY/MM/DD   ==> 01/02/03                       "
   SAY "       &YYDDD      ==> 01034                          "
   SAY "       &YYMMDD     ==> 010203                         "
   SAY
   EXIT(1)
