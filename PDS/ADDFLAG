/**  REXX -- ADD A STRING A COLUMN, RIGHT JUSTIFIED                 **/

   ADDRESS ISREDIT
   "MACRO (FLAG ENDCOL) NOPROCESS"
   IF FLAG = "" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
         TOP = 1
         "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
         "(TOP) = LINENUM .ZFRANGE"
         "(BOT) = LINENUM .ZLRANGE"
   END
   IF SUBSTR(FLAG,1,1) = "'" & SUBSTR(FLAG,LENGTH(FLAG),1) = "'",
         | SUBSTR(FLAG,1,1) = '"' & SUBSTR(FLAG,LENGTH(FLAG),1) = '"',
      THEN FLAG = SUBSTR(FLAG,2,LENGTH(FLAG)-2)
   IF ENDCOL = "" THEN ENDCOL = 72
   DO N = TOP TO BOT
      "(REC) = LINE" N
      REC = SUBSTR(REC,1,ENDCOL-LENGTH(FLAG)) || FLAG,
         || SUBSTR(REC,ENDCOL+1)
      "LINE " N " = (REC)"
   END
   "LOCATE" TOP
   RETURN(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "ADDFLAG - ADD A FLAG TO CC'D LINES"
   SAY
   SAY " FORMAT "
   SAY
   SAY "    ADDFLAG  FLAG  ENDING-COLUMN"
   SAY
   SAY "       THE FLAG IS MANDATORY BUT THE ENDING COLUMN WILL   "
   SAY "       DEFAULT TO 72.  A RANGE OF LINES MAY BE SPECIFIED  "
   SAY "       BY C/CC/C## OR THE FLAG WILL BE APPLIED TO THE     "
   SAY "       FILE. THE FLAG WILL BE RIGHT JUSTIFIED TO THE      "
   SAY "       ENDING COLUMN, OVERLAYING ANY DATA THERE.          "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    ADDFLAG  '/* 990711 */'  61 "
   SAY
   SAY "       WILL PLACE /* 990711 */ IN COLUMNS 50 THROUGH 61   "
   SAY "       OF EVERY COLUMN IN THE C/CC/C## RANGE OR THE ENTIRE"
   SAY "       FILE IF NO RANGE IS SPECIFIED.                     "
   SAY
   EXIT(1)

