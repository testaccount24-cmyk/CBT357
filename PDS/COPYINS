/**  REXX -- COPY COLUMNS INSERTING THEM INTO THE RECEIVING DATA  **/
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'COPYINS MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (FRMLEFT,FRMRIGHT,TOLEFT,LBL1,LBL2)"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY 'COPYINS EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT',
          'WITH NO PENDING EDIT COMMANDS!'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG = ''
   IF FRMLEFT = '' THEN DO
      SIGNAL ERROR
   END
   IF FRMLEFT = ''  |  FRMRIGHT = ''  |  TOLEFT = ''  THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMLEFT,'W')  |  FRMLEFT = 0  THEN DO
      MESG = "'"FRMLEFT"' VALUE OF FROM-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMRIGHT,'W')  |  FRMRIGHT=0  THEN DO
      MESG = "'"FRMRIGHT"' VALUE OF FROM-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF FRMLEFT > FRMRIGHT THEN DO
      MESG = "FROM-LEFT-COL "FRMLEFT" IS > THAN FROM-RIGHT-COL "FRMRIGHT
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TOLEFT,'W')  |  TOLEFT=0  THEN DO
      MESG = "'"TOLEFT"' VALUE OF TO-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   LEN = FRMRIGHT - FRMLEFT + 1
   TORIGHT = TOLEFT + LEN - 1
   IF TOLEFT > TORIGHT THEN DO
      MESG = "TO-LEFT-COL "TOLEFT" IS GREATER THAN TO-RIGHT-COL "TORIGHT
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   IF TORIGHT > LRECL THEN DO
      MESG = "TO-RIGHT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL1 "NOT FOUND"
          SIGNAL ERROR
       END
   END
   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
       IF RC <> 0 THEN DO
          MESG = "LABEL" LBL2 "NOT FOUND"
          SIGNAL ERROR
       END
   END
   "ISREDIT ("LRECL") = LRECL "
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR "
   "ISREDIT ("ANUM") = AUTONUM "
   "ISREDIT (END) = LINENUM .ZLAST"

   START = 1
   IF LBL1 <> '' THEN
       "ISREDIT (START) = LINENUM &LBL1"

   IF LBL2 <> '' THEN
        "ISREDIT (END) = LINENUM &LBL2"
   ELSE
        LBL2 = LBL1

   IF START > END THEN DO
      WST   = START
      START = END
      END   = WST
   END

   DO PNTR =  START  TO  END  BY 1
      "ISREDIT (RECORD) = LINE &PNTR"
      RECORD = SUBSTR(RECORD,1,TOLEFT-1),
         || SUBSTR(RECORD,FRMLEFT,LEN),
         || SUBSTR(RECORD,TOLEFT,LRECL-LEN-1)
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END

   "ISREDIT LOCATE" START
   "ISREDIT AUTONUM = "ANUM
    SIGNAL EOJ

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   SIGNAL DISPDOC

EOJ:
   X = MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "COPYINS - COPY COLUMNS INSERTING DATA"
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY
   SAY "    COPYINS  FROM-LEFT-COL FROM-RIGHT-COL TO-LEFT-COL (.A .B) "
   SAY
   SAY "       COLUMN PARAMETERS ARE REQUIRED. LABEL RANGE IS OPTIONAL"
   SAY
   SAY " EXAMPLES "
   SAY
   SAY "    COPYCOLS  1 8  21 "
   SAY "       WILL COPY THE CONTENTS OF COLUMNS 1 THRU 8 AFTER "
   SAY "       COLUMN  20 SHIFTING COLUMNS 21 ON TO THE RIGHT.  "
   SAY
   SAY "    COPYCOLS  15 18  1  .A .B "
   SAY "       WILL COPY THE CONTENTS OF COLS 15 THRU 18 TO     "
   SAY "       COLUMN 1 SHIFTING THE DATA IN COLUMN 1 FOUR      "
   SAY "       COLUMNS TO THE RIGHT RIGHT IN THE LABEL RANGE    "
   SAY "       .A TO .B.                                        "
   SAY
   EXIT(1)
