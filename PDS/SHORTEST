/**  REXX -- DETERMINE SHORTEST LINE & SHOW 1ST ONE **/
   ADDRESS "ISREDIT"
   "MACRO (PARMS) NOPROCESS"

   LABF   = ""
   LABL   = ""
   FIRST  = -1

   PARMS = TRANSLATE(PARMS)
   IF PARMS = '?' THEN SIGNAL DISPDOC
   DO II = 1 TO 10;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LABF = "" THEN LABF = TOKEN
         ELSE IF LABL = "" THEN LABL = TOKEN
         ELSE DO
            ZEDSMSG = ".LABEL RC=8"
            ZEDLMSG = "TOO MANY LABELS --" LABF LABL TOKEN
            ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
            EXIT 8
         END
      END
   END II
   IF LABF <> ""  &  LABL = ""  THEN LABL = LABF

   IF LABL = "" THEN DO
      "PROCESS RANGE C"
      IF RC = 0 THEN DO
         LABF = ".ZFRANGE"
         LABL = ".ZLRANGE"
      END
      ELSE DO
         LABF = ".ZFIRST"
         LABL = ".ZLAST"
      END
   END

   "(LABFNO) = LINENUM" LABF
   "(LABLNO) = LINENUM" LABL
   "ISREDIT ("LRECL") = LRECL"
   "ISREDIT ("RECFM") = RECFM"

   SHORTEST_SIZE = LRECL
   IF SUBSTR(RECFM,1,1) <> "F" THEN DO
      DO II = LABFNO TO LABLNO
         "ISREDIT (CURRSIZE) = SAVE_LENGTH" II
         IF CURRSIZE < SHORTEST_SIZE THEN DO
            SHORTEST_SIZE   = CURRSIZE
            NUMBER_SHORTEST = 1
            FIRST           = II
         END
         ELSE IF CURRSIZE = SHORTEST_SIZE,
            THEN NUMBER_SHORTEST = NUMBER_SHORTEST + 1
      END
   END
   ELSE DO
      DO II = LABFNO TO LABLNO
         "(REC) = LINE" II
         DO JJ = LRECL TO 1 BY -1 WHILE(SUBSTR(REC,JJ,1) = " ")
         END JJ
         IF JJ < SHORTEST_SIZE THEN DO
            SHORTEST_SIZE   = JJ
            NUMBER_SHORTEST = 1
            FIRST           = II
         END
         ELSE IF JJ = SHORTEST_SIZE,
            THEN NUMBER_SHORTEST = NUMBER_SHORTEST + 1
      END
   END

   "LOCATE" FIRST
   SHORTEST_SIZE = SHORTEST_SIZE + 0
   ZEDSMSG = "SHORTEST" SHORTEST_SIZE"("NUMBER_SHORTEST")"
   ZEDLMSG = NUMBER_SHORTEST "RECORDS OF" SHORTEST_SIZE "LENGTH"
   ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SHORTEST - FIND FIRST SHORTEST RECORD IN THE RANGE        "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    SHORTEST  ( .A .B )                                   "
   SAY
   SAY "       MAKES THE FIRST LINE WITH THE SMALLEST LENGTH THE  "
   SAY "       TOP LINE ON THE DISPLAY.  THE ACTUAL LENGTH IS     "
   SAY "       SHOWN IN THE UPPER RIGHT MESSAGE AREA AS WELL AS   "
   SAY "       THE NUMBER OF RECORDS OF SUCH SIZE IN THE RANGE.   "
   SAY
   SAY "       FOR FIXED DATA, THE SMALLEST LINE HAS THE LARGEST  "
   SAY "       NUMBER OF BLANKS ON THE RIGHT.                     "
   SAY
   SAY "       EITHER LABELS OR C/CC/C# MAY BE USED TO LIMIT THE  "
   SAY "       LENGTH CHECK; WILL PROCESS THE ENTIRE FILE IF      "
   SAY "       NEITHER IS SPECIFIED.  OBVIOUSLY MOST USEFUL FOR   "
   SAY "       VARIABLE LENGTH FILES.                             "
   SAY
   EXIT(1)

