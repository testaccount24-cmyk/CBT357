//-YOUR-USERID-B JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  BASIC PROGRAM TO READ AND WRITE A SEQUENTIAL FILE -
//***  ADDITIONAL PROCESSING MAY BE INSERTED AS NEEDED
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 BASE:  PROC(PARM) OPTIONS(MAIN) REORDER;

        DCL  PARM      CHAR(00100) VARYING;
        DCL  REC       CHAR(05120) STATIC VARYING;
        DCL  OLDREC    CHAR(05120) STATIC VARYING;
        DCL  I_EOF_IND CHAR(01) STATIC INIT('N');
        DCL  REWRITE_REC  CHAR(01) STATIC INIT('N');

        DCL  ADDED#    FIXED BIN(31) STATIC INIT(0);
        DCL  DELETED#  FIXED BIN(31) STATIC INIT(0);
        DCL  DUPS#     FIXED BIN(31) STATIC INIT(0);
        DCL  READ#     FIXED BIN(31) STATIC INIT(0);
        DCL  UPDATED#  FIXED BIN(31) STATIC INIT(0);
        DCL  WRITTEN#  FIXED BIN(31) STATIC INIT(0);
        DCL  DATE      BUILTIN;
        DCL  TIME      BUILTIN;


        ON ENDFILE(I) I_EOF_IND = 'Y';

        CALL INITIALIZATION;

        DO WHILE ( I_EOF_IND  = 'N' );
           CALL READ_WRITE_LOOP;
        END;

        CALL TERMINATION;


 PROCESS: PROC;
        /***   ...   ***/
        END PROCESS;

 INITIALIZATION: PROC;
        READ FILE(I) INTO(REC);
        END INITIALIZATION;

 READ_WRITE_LOOP: PROC;
        REWRITE_REC = 'Y';
        READ# = READ# + 1;
        CALL PROCESS;
        IF REWRITE_REC = 'Y'
           THEN DO;
              WRITE FILE(O) FROM(REC);
              WRITTEN# = WRITTEN# + 1;
           END;
           ELSE DO;
              DELETED# = DELETED# + 1;
           END;
        READ FILE(I) INTO(REC);
        END READ_WRITE_LOOP;

 TERMINATION: PROC;
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  ', DATE, ' STATISTICS ', TIME, '  ****' )
                 ( SKIP(1), A, A, A, A, A );
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  NUMBER RECORDS READ:    ', READ#    )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS WRITTEN: ', WRITTEN# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS UPDATED: ', UPDATED# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS ADDED:   ', ADDED#   )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS DELETED: ', DELETED# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS DUPS:    ', DUPS#    )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF LENGTH(PARM) > 0
           THEN DO;
              PUT EDIT ( '***  PARM LENGTH/PARM ARE:  ',
                         LENGTH(PARM), '/', PARM )
                       ( SKIP(2), A, P'ZZ9', A, A  );
           END;
        END TERMINATION;

        END BASE;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//L          EXEC     PGM=HEWLKED,COND=(5,LT),
//             PARM='XREF,LIST,LET,DCBS,AMODE=31,RMODE=ANY', <******
//             REGION=4M
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=&&TEMPPGM,DISP=(MOD,PASS),
//             UNIT=SYSDA,SPACE=(CYL,(5,2,44)),
//             DCB=(RECFM=U,BLKSIZE=06144)
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE)
//             DD       *
  SETSSI   99080300
  ENTRY    PLISTART
  NAME     BASE(R)
//***
//G          EXEC     PGM=BASE,REGION=32M,COND=(5,LT),   <******
//             PARM='HEAP(1M,1M,ANYWHERE)'               <******
//STEPLIB      DD       DSN=&&TEMPPGM,DISP=(OLD,PASS)
//I            DD       DISP=SHR,DSN=-YOUR-SYSLIB-(JOB)
//O            DD       SYSOUT=*,
//             DCB=(RECFM=F,LRECL=80,BLKSIZE=80)
//SYSPRINT     DD       SYSOUT=*
//***
