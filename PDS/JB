/** REXX - Jump-Back to Perform Statement (from JT macro) **/
Parse Source opsys . cmdname .
Address ISPEXEC "CONTROL ERRORS RETURN"
Address ISREDIT
'MACRO (PARM) NOPROCESS'
'(MEMBER) = MEMBER'
alpha = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
Call ISPFVGET
if jbnum = '' | jbnum = 0  then jbnum = 1

if parm = '?' then call DispDoc

suff = SUBSTR(alpha,jbnum,1)
jb = '.JB' || suff
'(LINENO) = LINENUM' jb
lrc =rc
If lrc=0 Then Do
  'LOCATE' jb
  jbnum = jbnum - 1
  'RESET FIND'
  Call ISPFVPUT
end
if parm <> '' then do
  if ABBREV('ERASE',parm,1) then do
    Call ISPFVERASE
    Call RESET
  end
  if ABBREV('RESET',parm,1) then do
    jbnum = 1
    Call RESET
  end
end
if parm = '' &  lrc  = 0 then 'RESET LABEL' jb jb
Exit
/**********************************************************************/
/* Reset .JBx ALL labels                                              */
/**********************************************************************/
RESET: Procedure Expose alpha
 do i=1 to LENGTH(alpha)
   suff = SUBSTR(alpha,i,1)
   jb   = '.JB' || suff
   '(LINENO) = LINENUM' jb
   if rc=0 then 'RESET LABEL' jb jb
 end i
Return
/**********************************************************************/
/* Get the highest jb number for this edit member                     */
/**********************************************************************/
ISPFVGET: Procedure Expose JBNUM member
  Address ISPEXEC
  'VGET (FINCL) SHARED'
  If rc> 8 Then Say 'ISPFVGET: error rc='  rc
  where = POS(member,fincl)
  if where>0 Then
  do
    jbnum = SUBSTR(fincl,(where+LENGTH(member)+1),2)
    jbnum = STRIP(jbnum)
  end
  else jbnum = 0
Return
/*----------------------------------------------------------------------
! Save information in SHARED profile for this member                   !
----------------------------------------------------------------------*/
ISPFVPUT: Procedure Expose JBNUM member
  Address ISPEXEC
  'VGET (FINCL) SHARED'
  where = POS(member,fincl)
  if where>0 Then
  do
    fincl = OVERLAY(jbnum,fincl,(where+LENGTH(member)+1),2)
  end
  else
  do
    fincl = fincl member || '_' || STRIP(jbnum)
    fincl = SPACE(fincl,1)
  end
  'VPUT (FINCL) SHARED'
  If rc> 8 Then Say 'ISPFVPUT: error rc='  rc
Return
/**********************************************************************/
/* Delete the SHARED profile information for this edit member         */
/**********************************************************************/
ISPFVERASE: Procedure Expose JBNUM member
  Address ISPEXEC
  'VERASE (FINCL) SHARED'
  If rc> 8 Then Say 'ISPFVERASE: error rc='  rc
Return

DispDoc:
   ADDRESS TSO "CLEAR"
   SAY " JB - Jump Back to Perform Statement set by JT         "
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY "    JB    ( ? )                                        "
   SAY
   SAY "       positions display with Perform statement set by "
   SAY "       JT macro.                                       "
   SAY
   Return
