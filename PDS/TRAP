/**  REXX -- accept any TSO command (including clists and other  **/
/*           Rexx procedures)that output to the terminal and      */
/*           trap and display the captured results using View     */
/* Syntax:    %TRAP tso-command options                           */
/* -------------------------------------------------------------- */
x = msg("off")
arg command
if length(command) = 0 then do
   say "Error:  Use of TRAP requires the specification of"
   say "        a TSO command whose results will be captured"
   say "        and displayed using ISPF Browse."
   say "        (e.g.  %TRAP LISTC)"
   exit 12
   end
x = outtrap("trap.","*")
command
x= outtrap("off")
dd = "TP"random()
da = "DA"random()
"ALLOC DD("dd") NEW CYL SP(5,2) DA("da")"
"EXECIO * DISKW" dd "(FINIS STEM trap."
Address ISPEXEC "VIEW DATASET("da")"
"FREE DD("dd")"
"DELETE" da
