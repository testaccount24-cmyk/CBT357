/**  REXX -- Edit every member of a PDS from outside Edit  **/

   arg dsn exec
   if dsn = "?" | dsn = "" then signal DispDoc

   x = outtrap("lm.","*")
   address tso "LISTDS" dsn "MEMBERS"
   rcsave = rc
   if rcsave <> 0 then do
       say 'RC =' rcsave 'from LISTDS...'
       exit 16
   end
   x = outtrap("off")

   do i = 1 to lm.0
       if lm.i = "--MEMBERS--" then leave
   end

   do j = i+1 to lm.0
       parse value lm.j with mem extra
       if mem = exec then iterate
       if substr(dsn,1,1) = "'" | substr(dsn,1,1) = '"' then do
           dsnx = substr(dsn,2,length(dsn)-2)
           Address ISPEXEC "EDIT DATASET('"dsnx"("mem")') MACRO("exec")"
       end
       else do
           Address ISPEXEC "EDIT DATASET("dsn"("mem")) MACRO("exec")"
       end
   end

   Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
   Say "XALLMEM -- Execute macro against all members of a PDS       "
   Say
   Say "   XALLMEM  ( ? )  PDS  Macro                               "
   Say
   Say "       PDS is the name of the Partitioned Data Set which    "
   Say "             will be edited.                                "
   Say
   Say "       Macro is the name of a macro to execute.  If it      "
   Say "             is a program macro, remember to specify the    "
   Say "             exclamation point before the name.             "
   Say
   Say "       A question mark (?) as the first parameter will      "
   Say "       cause this built in documentation to display.        "
   Say
   Exit(1)
