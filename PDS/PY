/** REXX - Put lines from the default clipboard after the cursor **/

   Address ISPEXEC
   'ISREDIT MACRO (PARM1)   PROCESS'
   Parm1 = translate(Parm1)
   If Parm1 = '?' then signal DispDoc

   'ISREDIT (Line,Col) = CURSOR'
   'ISREDIT (ZDest)    = CURSOR'
   If ZDest = 1  then do
       'VGET (ZSCREENC,ZSCREENI) SHARED'
       ii = INDEX(ZScreenI,'* Top of Data *')
       If ( ZScreenC % 80 )  <=  ( ii % 80 )  then ZDest = 0
       Else ZDest = 1
   End

   DATA      = "..."
   CutCount  = -1
   "ISREDIT LINE_AFTER &ZDEST = (DATA)"
   ii = ZDest + 1
   "ISREDIT LABEL" ii "= .HERE"
   If CutCount = -1 then do
      "ISREDIT LINE_AFTER" ii "= (DATA)"
      jj = ii + 1
      "ISREDIT LABEL" jj "= .THERE"
   End
   Address ISREDIT "PASTE AFTER .HERE KEEP"
   If RC = 12 then do
      zedsmsg = 'No "CUT" data'
      zedlmsg = 'There is no data to "PASTE" from the ISPF' ,
                'Default Clipboard.'
      Address ISPEXEC "SETMSG MSG(ISRZ001)"  /* msg - with alarm */
      Exit 12
   End
   If CutCount = -1 then do
      "ISREDIT (ii) = LINENUM .HERE"
      "ISREDIT (jj) = LINENUM .THERE"
      CutCount = jj - ii - 1
      "ISREDIT DELETE .THERE .THERE"
   End
   "ISREDIT DELETE .HERE .HERE"

   zedsmsg = CutCount 'lines pasted'
   zedlmsg = CutCount 'lines were pasted into the current file'
   'SETMSG MSG(ISRZ000)'
   If Line = 0 & Col = 0 then Exit(1)
   Else do
      'ISREDIT CURSOR = ' Line Col
      Exit
   End

DispDoc:
   ADDRESS TSO "CLEAR"
   Say
   Say ' PY '
   Say
   Say ' Enter PY on the COMMAND line and position the cursor on   '
   Say ' the line after which the cut data is to be inserted. Assign  '
   Say ' PY to a PF Key and you can just position the cursor and   '
   Say ' press the PF Key, as often as desired and across files.      '
   Say
   Exit(1)
