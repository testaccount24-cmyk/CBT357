/**  REXX -- DISPLAY LINK EDIT DATE OF SPECIFIC LOAD MODULE  **/

   PARSE ARG PARMS
   MSG = MSG(OFF)
   PARMS = TRANSLATE(PARMS)

   IF WORD(PARMS,1) = "" | WORD(PARMS,1) = "?"
      THEN SIGNAL DISPDOC

   TOKEN    = WORD(PARMS,1)
   XX       = INDEX(TOKEN,"(")
   YY       = INDEX(TOKEN,")")
   SEL_LMOD = SUBSTR(TOKEN,XX+1,YY-XX-1)
   IF SUBSTR(TOKEN,1,1) = "'" | SUBSTR(TOKEN,1,1) = '"',
      THEN FULL_DSN = SUBSTR(TOKEN,1,XX-1) || "'"
      ELSE FULL_DSN = "'" || USERID() || ".",
         || SUBSTR(TOKEN,1,XX-1) || "'"

   ADDRESS TSO
   "FREE  DD(SYSLML SYSPRINT SYSIN)"
   "ALLOC DD(SYSLML) DSN("FULL_DSN") SHR"
   "ALLOC DD(SYSPRINT) NEW TRACKS SPACE(1) UNIT(SYSDA)",
        " LRECL(121) BLKSIZE(27951) RECFM(F B M)"
   "ALLOC DD(SYSIN) NEW TRACKS SPACE(1) UNIT(SYSDA)",
        " LRECL(80) BLKSIZE(800) RECFM(F B)"
   QUEUE " LISTIDR  DDN=SYSLML,MEMBER=" || SEL_LMOD
   "EXECIO" QUEUED() "DISKW SYSIN (FINIS"

   "CALL *(AMBLIST)"
   IF RC <> 0 THEN DO
      SAY "*** AMBLIST RETURNED CODE" RC "- PROBABLY NO IDR DATA FOUND"
      EXIT(1)
   END
   ELSE DO
      "EXECIO 0 DISKR" SYSPRINT "(OPEN"
      "EXECIO 1 DISKR" SYSPRINT
      RETCODE = RC
      DO WHILE RETCODE = 0
         PULL PRTLINE
         IF INDEX(PRTLINE,"WAS PRODUCED BY") <> 0 THEN DO
            XX   = INDEX(PRTLINE,"ON DAY ")
            DDD  = SUBSTR(PRTLINE,XX+7,3)
            XX   = INDEX(PRTLINE,"OF YEAR ")
            YYYY = SUBSTR(PRTLINE,XX+8,4)
            TIMP = SUBSTR(PRTLINE,XX+16,8)
            RETCODE = 666
         END
         ELSE "EXECIO 1 DISKR" SYSPRINT
      END
      "EXECIO 0 DISKR" SYSPRINT "(FINIS"
      "FREE  DD(SYSLML SYSPRINT SYSIN)"
      YYYYDDD = YYYY || DDD
      SPARMS = DATE(S,SUBSTR(YYYYDDD,3),J)
      IF SUBSTR(SPARMS,8,1) = '1'         THEN XX = 'st'
         ELSE IF SUBSTR(SPARMS,8,1) = '2' THEN XX = 'nd'
         ELSE IF SUBSTR(SPARMS,8,1) = '3' THEN XX = 'rd'
         ELSE XX = 'th'
      DD = SUBSTR(SPARMS,7,2)
      IF SUBSTR(DD,1,1) = '0'             THEN DD = SUBSTR(DD,2)
      SAY SEL_LMOD "in" FULL_DSN "was linked"
      IF TIMP <> " ",
         THEN SAY "   " DATE(W,SPARMS,S) DATE(M,SPARMS,S) DD||XX',',
            YYYY "(day" DDD") at" TIMP
         ELSE SAY "   " DATE(W,SPARMS,S) DATE(M,SPARMS,S) DD||XX',',
            YYYY "(day" DDD")"
      EXIT(0)
   end

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "                                                             "
   SAY "  LKDT          DISPLAYS THE DATE THE SPECIFIED LOAD MODULE  "
   SAY "                WAS LINK EDITED.                             "
   SAY "                                                             "
   SAY "  SYNTAX:   LKDT      LOAD-LIBRARY-NAME(MEMBER)              "
   SAY
   SAY "             LOAD-LIBRARY-NAME IS THE LOAD MODULE LIBRARY    "
   SAY "             WHICH CONTAINS THE MODULE TO BE DATED.          "
   SAY
   SAY "             MEMBER IS THE SPECIFIC LOAD MODULE TO BE DATED. "
   SAY "             IT MAY ALSO BE ENTERED ON ANY MEMBER LIST OF A  "
   SAY "             LOAD MODULE LIBRARY (BROWSE, EDIT OR VIEW).     "
   SAY
   SAY "  EXAMPLE:   LKDT 'SYS1.LINKLIB(IEHPROGM)'                   "
   SAY
   SAY "             LKDT MY.LOADLIB(TESTMOD)                        "
   SAY
   EXIT(1)

