/** REXX -- SPLITON -- SPLIT COLUMNS BASED ON ONE OR MORE CHARACTERS **/

   ADDRESS ISREDIT
   "MACRO (STRING INDENT) NOPROCESS"
   IF STRING = "" | STRING = "?" THEN SIGNAL DISPDOC
   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      TOP = 1
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END
   IF SUBSTR(STRING,1,1) = '"' | SUBSTR(STRING,1,1) = "'",
      THEN STRING = TRANSLATE(SUBSTR(STRING,2,LENGTH(STRING)-2))
      ELSE STRING = TRANSLATE(STRING)
   IF INDENT = "" | Â¬DATATYPE(INDENT,'W') THEN INDENT = 4

   DO NN = BOT TO TOP BY -1
      "(REC) = LINE" NN
      DO JJ = 8 TO 72 WHILE(SUBSTR(REC,JJ,1) = " ")
      END JJ
      II = POS(STRING,REC)
      IF II > 0 & SUBSTR(REC,7,1) <> "*",
            & SUBSTR(REC,7,1) <> "/",
            & SUBSTR(REC,7,1) <> "D" THEN DO
               SPLITREC = COPIES(" ",JJ+INDENT-1) || SUBSTR(REC,II)
               "LINE_AFTER (NN) = (SPLITREC)"
               REC = SUBSTR(REC,1,II-1)
               "LINE " NN " = (REC)"
            END
   END

   "LOCATE" TOP
   RETURN(0)

DISPDOC:
   SAY "SPLITON - SPLIT COLUMNS AT STRING                      "
   SAY
   SAY " FORMAT "
   SAY "    SPLITON  STRING  ( INDENT )                       "
   SAY "       WILL SPLIT THE SPECIFIED LINES AT THE STRING   "
   SAY "       PROVIDED. A RANGE OF LINES MAY BE SPECIFIED BY "
   SAY "       C/CC/C## OR THE ENTIRE FILE WILL BE PROCESSED. "
   SAY "       IF NO STRING IS SPECIFIED, THIS HELP WILL BE   "
   SAY "       SHOWN. QUOTES MAY BE USED AROUND THE STRING.   "
   SAY "       INDENT DEFAULTS TO 4.                          "
   SAY
   SAY " EXAMPLE "
   SAY "    SPLITON  ' TO '                            "
   SAY "       ON THE FOLLOWING:                       "
   SAY "          MOVE      7   TO WS-SHORT-FIELD      "
   SAY "          MOVE     16   TO WS-MIDDLE-FIELD     "
   SAY "          MOVE    427   TO WS-REAL-BIG-FIELD   "
   SAY "       WILL RESULT IN:                         "
   SAY "          MOVE      7                          "
   SAY "              TO WS-SHORT-FIELD                "
   SAY "          MOVE     16                          "
   SAY "              TO WS-MIDDLE-FIELD               "
   SAY "          MOVE    427                          "
   SAY "              TO WS-REAL-BIG-FIELD             "
   EXIT(1)

