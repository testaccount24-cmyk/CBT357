/**  REXX -- Invoke a macro against all members in the PDS being   **/
/*       edited; Edit the first member, then invoke ALLMEM          */
/*------------------------------------------------------------------*/
   Address 'ISPEXEC'
   'ISREDIT MACRO (WORKMAC,PREFIX)'
   If WORKMAC = '' then signal DispDoc
   'ISREDIT (DATA1)   = DATAID'
   'ISREDIT (THISONE) = MEMBER '
   Address 'ISPEXEC' 'LMOPEN DATAID('data1') OPTION(INPUT)'
   parse upper var prefix prefix .
   member1 = ''
   Do Until lmrc \= 0
      Address 'ISPEXEC' 'LMMLIST DATAID('data1') OPTION(LIST)',
                        'MEMBER(MEMBER1) STATS(YES)'
      lmrc = rc
      If lmrc = 0              ,/* If member name returned          */
        & member1 \= thisone   ,/* and it isn't this member         */
        & (                    ,/* and prefix check is ok...        */
            prefix = ''        ,/*    No prefix specified           */
            | substr(member1,1,length(prefix))=prefix,/* | pref mtch*/
          ) Then Do      /* Invoke edit with specified initial macro*/
           Address 'ISPEXEC' 'CONTROL ERRORS CANCEL'
           Address 'ISPEXEC' 'EDIT DATAID('data1')',
                   'MEMBER('member1') MACRO('workmac')'
           Address 'ISPEXEC' 'CONTROL ERRORS CANCEL'
        End
   End
   Address 'ISPEXEC' 'LMMLIST DATAID('data1') OPTION(FREE)'
   Address 'ISPEXEC' 'LMCLOSE DATAID('data1')'
   'ISREDIT DEFINE 'workmac' MACRO CMD'
   If prefix=''                              ,/* No prefix specified */
      | substr(thisone,1,length(prefix))=prefix ,/* or prefix match  */
     then 'ISREDIT 'workmac        /* perform macro for this member  */
   Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
   SAY "ALLMEM -- Execute macro against all members of a PDS        "
   SAY
   SAY "    ALLMEM Macro Prefix                                     "
   SAY
   SAY "       Macro is the name of a macro to execute.  If it      "
   SAY "             is a program macro, remember to specify the    "
   SAY "             exclamation point before the name.             "
   SAY
   SAY "       Prefix is an optional prefix to use when selecting   "
   SAY "              members to process.  for example, Y2K will    "
   SAY "              process all members starting with Y2K.        "
   SAY
   SAY " Edit any member of the PDS and invoke this macro. Note     "
   SAY " that the macro which this calls can have an ISREDIT END or "
   SAY " ISREDIT CANCEL in it to avoid the display. See the sample  "
   SAY " ALLMEMC and ALLMEMF macros.                                "
   SAY
   EXIT(1)
