/**  REXX -- SPLIT LINES AT SELECTED COLUMNS  **/
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'SPLTCOLS MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (PARMS)"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY SPLTCOLS 'EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG = ''

   PARMS   = TRANSLATE(PARMS)
   LBL1    = ""
   LBL2    = ""
   SCOL.   = ""
   SCNT    = 0
   DO II = 1 TO 50;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LBL1 = "" THEN LBL1 = TOKEN
         ELSE IF LBL2 = "" THEN LBL2 = TOKEN
            ELSE DO
               ZEDSMSG = ".LABEL RC=8"
               ZEDLMSG = "TOO MANY LABELS --" LBL1 LBL2 TOKEN
               ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
               EXIT 8
            END
      END
      ELSE DO
         IF DATATYPE(TOKEN,'W')  THEN DO
            SCNT = SCNT + 1
            SCOL.SCNT = TOKEN
         END
         ELSE DO
            ZEDSMSG = "BAD PARM"
            ZEDLMSG = "UNRECOGNIZED PARM --" TOKEN
            ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
            EXIT 8
         END
      END
   END II
   IF LBL1 <> ""  &  LBL2 = ""  THEN LBL2 = LBL1

   IF SCNT = 0 THEN DO
      MESG = "NO COLUMNS SPECIFIED!"
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   DO II = 1 TO SCNT
      IF SCOL.II > LRECL THEN DO
      MESG = "COLUMN" SCOL.II "IS GREATER THAN LRECL OF FILE!"
         SIGNAL ERROR
      END
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
      IF RC <> 0 THEN DO
         MESG = "LABEL" LBL1 "NOT FOUND"
         SIGNAL ERROR
      END
   END
   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
      IF RC <> 0 THEN DO
         MESG = "LABEL" LBL2 "NOT FOUND"
         SIGNAL ERROR
      END
   END

   "ISREDIT (END) = LINENUM .ZLAST"
   START = 1
   IF LBL1 <> '' THEN "ISREDIT (START) = LINENUM &LBL1"
   IF LBL2 <> '' THEN "ISREDIT (END)   = LINENUM &LBL2"

   IF START > END THEN DO
      TEMP  = START
      START = END
      END   = TEMP
   END

   TOT = 0
   DO PNTR =  END  TO  START  BY -1
      "ISREDIT (RECORD) = LINE &PNTR"
      DO II = SCNT TO 1 BY -1
         TOT = TOT + 1
         NEW = SUBSTR(RECORD,SCOL.II)
         "ISREDIT LINE_AFTER &PNTR = (NEW)"
         RECORD = SUBSTR(RECORD,1,SCOL.II-1)
      END
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END

   STR = TOT 'LINES ADDED'
   CALL SETMSG '* NO ' STR '$CREATED NEW LINES'
   "ISREDIT LOCATE" START
   "ISREDIT AUTONUM =" ANUM
    SIGNAL EOJ

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   CALL DISPDOC

EOJ:
   X = MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "SPLTCOLS - SPLIT LINES AT SPECIFIED COLUMNS               "
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY "    SPLTCOLS COL-1 ( COL-2 ... )  ( .A .B )               "
   SAY
   SAY "       AT LEAST ONE SPLIT COLUMN IS REQUIRED. THE LABEL   "
   SAY "       RANGES ARE OPTIONAL AND MAY BE INTERMIXED.         "
   SAY
   SAY " EXAMPLES "
   SAY
   SAY "    SPLTCOLS  21  41  61                             "
   SAY "       WILL SPLIT EACH LINE AT COLUMNS 21, 41 AND 61,"
   SAY "       CREATING FOUR LINES FROM EACH ONE IN THE FILE."
   SAY
   SAY "    SPLTCOLS  .A .B  41                              "
   SAY "       WILL SPLIT LINES IN THE RANGE .A THRU .B AT   "
   SAY "       COLUMN 41.                                    "
   SAY
   EXIT(1)
