/**  REXX -- Place lines from the user PROFILE pool into the file    **/

   Address ISPEXEC
   'ISREDIT MACRO (PARM1) NOPROCESS'
   Parm1 = translate(Parm1)
   If Parm1 = '?' then signal DispDoc

   If Parm1 <> ' ' & Parm1 <> 'D' & Parm1 <> 'DELETE' then do
       zedsmsg = 'Invalid parameter'
       zedlmsg = 'Use ? for help, D or DELETE to cause lines to delete'
       'SETMSG MSG(ISRZ001)'
       Exit(12)
   End

   'CONTROL ERRORS RETURN'
   'ISREDIT PROCESS DEST'
   Select
       When rc = 0 then
           'ISREDIT (ZDEST) = LINENUM .ZDEST'
       When rc <= 8 then do
            zedsmsg = 'Enter "A"/"B" line cmd'
            zedlmsg = 'PASTEX requires an "A" or "B" line command'
            'SETMSG MSG(ISRZ001)'
            Exit(12)
       End
       When rc < 20 then
            Exit(12)
       When rc = 20 then
            ZDest = 0
       Otherwise Exit(12)
   End
   'CONTROL ERRORS CANCEL'

   'VGET (CUTCNT) PROFILE'
   If rc Â¬= 0 | CutCnt <= 0 then do
       zedsmsg = 'Use CUTX before PASTEX'
       zedlmsg = 'Use CUTX first before attempting PASTEX'
       'SETMSG MSG(ISRZ001)'
       Exit(12)
   End

   TruncCnt = 0
   CutCount = CutCnt
   Do i = CutCnt to 1 by -1
     'VGET (CL'i') PROFILE'
     'ISREDIT LINE_AFTER' ZDest '= DATALINE (CL'i')'
     If rc = 4 then TruncCnt = TruncCnt + 1
   End

   If Parm1 = 'D' | Parm1 = 'DELETE' then do
       Do i = 1 to CutCnt by 1
         'VERASE (CL'i') PROFILE'
       End
       CutCnt = 0
       'VPUT (CUTCNT) PROFILE'
     End

   If TruncCnt > 0 then do
       zedsmsg = TruncCnt 'lines truncated'
       msg     = 'Current record length shorter than origin'
       zedlmsg = msg '-' TruncCnt 'of' CutCount 'records truncated'
       'SETMSG MSG(ISRZ001)'
   End
   Else do
       zedsmsg = CutCount 'lines pasted'
       zedlmsg = CutCount 'lines were pasted into the current file'
       'SETMSG MSG(ISRZ000)'
   End

   Line1 = ZDest + 1
   'ISREDIT CURSOR =' Line1 0
   Exit(0)

DispDoc:
   ADDRESS TSO "CLEAR"
Say
Say ' PASTEX will pull previously cut lines from the PROFILE pool   '
Say ' into the current file.                                        '
Say
Say '     PASTEX  ( D | DELETE )                                    '
Say
Say ' Enter PASTEX on the COMMAND line and use the A or B line      '
Say ' command to specify where the lines are to be pasted.          '
Say
Say ' A parameter of D or DELETE can be specified to cause the macro'
Say ' to set the profile variables to null after the lines have been'
Say ' pasted into the file. If line truncation occurs, the profile  '
Say ' will be erased and a message issued in the upper right corner.'
Say
Exit(1)
