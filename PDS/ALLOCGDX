/**  REXX -- ALLOCATE GDG BY RELATIVE NUMBER  **/

   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT "ISREDIT MACRO ("PARMS")"
      IF RC = 0 THEN
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
   END
   IF DSN = 'DSN' THEN ARG PARMS

   IF PARMS = "" | PARMS = "?" THEN SIGNAL DISPDOC
   DO II = 1 TO LENGTH(PARMS) BY 1 WHILE( SUBSTR(PARMS,II,1) <> '(' )
   END II
   DO JJ = II+1 TO LENGTH(PARMS) BY 1 WHILE( SUBSTR(PARMS,JJ,1) <> ')' )
   END JJ
   IF SUBSTR(PARMS,1,1) = "'",
      THEN DO
         QUOTED = "YES"
         RELGDG = SUBSTR(PARMS,2,II-2) || ".G*V*"
         ALLOCPARMS = SUBSTR(PARMS,JJ+3)
      END
      ELSE DO
         QUOTED = "NO"
         RELGDG = SUBSTR(PARMS,1,II-1) || ".G*V*"
         ALLOCPARMS = SUBSTR(PARMS,JJ+2)
      END
   RELOFF = SUBSTR(PARMS,II+1,JJ-II-1)

/* "READ" LOOP */
   "ISPEXEC LMDINIT LISTID(DSLIST) LEVEL(&RELGDG)"
   NN = 0
   DO WHILE RC = 0
      "ISPEXEC LMDLIST LISTID(&DSLIST) DATASET(DSNVAR) OPTION(LIST)"
      DSNVAR  = STRIP(LEFT(DSNVAR,44),"T")
      IF RC > 0 THEN LEAVE
      NN = NN + 1
      ABSDSN.NN = DSNVAR
   END
   "ISPEXEC LMDLIST LISTID(&DSLIST)" "OPTION(FREE)"

/* GENERATE ALLOCATE */
   QQ = 0
   DO XX = NN TO 1 BY -1
      IF RELOFF = QQ THEN DO
         IF QUOTED = 'YES' THEN
            ADDRESS TSO "ALLOC DSN('"ABSDSN.XX"')" ALLOCPARMS
         ELSE
            ADDRESS TSO "ALLOC DSN("ABSDSN.XX")" ALLOCPARMS
         LEAVE
      END
      QQ = QQ - 1
   END XX
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "ALLOCGDG - ALLOCATE A RELATIVE GDG AS ABSOLUTE GDG        "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    ALLOCGDG  DSN-QUALIFIER | ?  ( OTHER ALLOC PARMS )    "
   SAY
   SAY "       DO EQUIVALENT OF ALLOCATE FOR A RELATIVE GDG ENTRY "
   SAY "       TRANSLATING THE RELATIVE NUMBERED DATA SET INTO A  "
   SAY "       FULLY QUALIFIED G....V00 NAME. FOR EXAMPLE:        "
   SAY "          ALLOCGDG 'RUBBER.DUCKY(-1)' DD(...) ...         "
   SAY "       WOULD ALLOCATE THE -1 FILE AS THE FULLY NAMED      "
   SAY "       'RUBBER.DUCKY.G0011V00' (IF APPROPRIATE).          "
   SAY
   SAY "       ANY ADDITIONAL NORMAL ALLOCATE PARAMETERS ARE      "
   SAY "       PASSED AS IS. THE DSN-QUALIFIER MAY BE QUOTED AS   "
   SAY "       SHOWN OR NOT.                                      "
   SAY
   SAY "       USE NO PARAMETERS OR A STANDALONE ? TO GET THIS    "
   SAY "       HELP.                                              "
   EXIT(1)

