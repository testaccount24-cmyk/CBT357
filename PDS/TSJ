/**  REXX - SPLIT OR JOIN LINES - STRIPS BLANKS ON JOIN  **/
   ADDRESS ISREDIT
   "ISREDIT MACRO (PARMS)"
   IF PARMS = '?' THEN SIGNAL DISPDOC
   "ISREDIT (LINE,COL) = CURSOR"
   "ISREDIT (CURRENT)  = LINE "LINE
   "ISREDIT (END)      = LINENUM .ZLAST"
   IF LINE < END
       THEN "ISREDIT (NEXT) = LINE "LINE+1
       ELSE NEXT = "   "
   IF SUBSTR(CURRENT,COL) = " "
      THEN DO
         CURRENT = SUBSTR(CURRENT,1,COL-1) || STRIP(NEXT,'L')
         "ISREDIT LINE &LINE = (CURRENT)"
         DELLINE = LINE + 1
         "ISREDIT DELETE &DELLINE"
      END
      ELSE DO
         BLANKCOLS = VERIFY(CURRENT," ") - 1
         IF BLANKCOLS < 0 THEN BLANKCOLS = 0
         NEW = COPIES(" ",BLANKCOLS) || SUBSTR(CURRENT,COL)
         CURRENT = SUBSTR(CURRENT,1,COL-1)
         "ISREDIT LINE &LINE = (CURRENT)"
         "ISREDIT LINE_AFTER &LINE = (NEW)"
      END
   "ISREDIT CURSOR = " LINE COL
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "TSJ - TEXT SPLIT OR JOIN MACRO"
   SAY
   SAY " FORMAT "
   SAY
   SAY "    SPLIT LINE WHERE CURSOR IS POSITIONED IF ANY NON-    "
   SAY "    BLANK DATA TO RIGHT. DATA WILL BE ALIGNED WITH       "
   SAY "    DATA ON LINE WITH CURSOR.                            "
   SAY
   SAY "    IF ONLY BLANKS TO RIGHT OF CURSOR POSITION, JOIN     "
   SAY "    DATA FROM NEXT LINE, DELETING THAT LINE. WILL        "
   SAY "    ATTACH ONLY NON BLANK DATA.                          "
   SAY
   EXIT(1)

