/**  REXX -- CREATE DISPLAYS AFTER EACH PARAGRAPH  **/
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'DISPARA EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      ADDRESS ISREDIT "ISREDIT MACRO (PARMS) NOPROCESS"
      PARMS = TRANSLATE(PARMS)
      IF PARMS = "?" THEN SIGNAL DISPDOC
   END
   ELSE DO
      SAY 'DISPARA EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT!'
      EXIT(12)
   END

   ADDRESS ISREDIT
   OVLAY = "           DISPLAY '~DEBUG~ "
   SEEK "' PROC'" FIRST 7
   "(CURLINE) = LINENUM .ZCSR"
   CURLINE = CURLINE + 1
   "LABEL (CURLINE) = .A"
   EXCLUDE ALL
   "FIND FIRST P' ¬' 7 .A .ZL"
   "(CURLINE) = LINENUM .ZCSR"
   RET_CODE = 0
   DO WHILE RET_CODE = 0
      "(DISPLINE) = LINE (CURLINE)"
      DO II = 8 TO 11 BY 1 WHILE (SUBSTR(DISPLINE,II,1) = " ")
      END II
      XX = POS(" EXIT.",DISPLINE)
      IF XX = 0 THEN DO
         DO JJ = 43 TO II BY -1 WHILE (SUBSTR(DISPLINE,JJ,1) = " ")
         END JJ
         DISPLINE = OVLAY || SUBSTR(DISPLINE,II,JJ-II) || "'"
         "LINE_AFTER (CURLINE) = (DISPLINE)"
      END
      ELSE DO
         EXITLINE = SUBSTR(DISPLINE,1,XX-1)
         "LINE (CURLINE) = (EXITLINE)"
         EXITLINE = COPIES(" ",XX-1) || SUBSTR(DISPLINE,XX)
         "LINE_AFTER (CURLINE)= (EXITLINE)"
         DO JJ = XX TO II BY -1 WHILE (SUBSTR(DISPLINE,JJ,1) = " ")
         END JJ
         DISPLINE = OVLAY || SUBSTR(DISPLINE,II,JJ-II) || "'"
         "LINE_AFTER (CURLINE) = (DISPLINE)"
      END
      "FIND NEXT P' ¬' 7"
      RET_CODE = RC
      IF RET_CODE = 0 THEN "(CURLINE) = LINENUM .ZCSR"
   END
   RESET
   SEEK "' PROC'" FIRST 7
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "DISPARA - CREATE DISPLAYS OF PARAGRAPH NAMES AFTER EACH   "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    DISPARA  ( ? )                                        "
   SAY
   SAY "       CREATES A DISPLAY OF THE NAME OF EACH PARAGRAPH    "
   SAY "       AFTER EACH PARAGRAPH NAME.                         "
   SAY
   SAY "       USE DELPARA TO REMOVE THESE DISPLAYS IF NEEDED.    "
   SAY
   EXIT(1)

