//-YOUR-USERID-G JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  RETRIEVE DATA SET NAME AND MEMBER (IF ANY) FOR PASSED COBOL
//***  FILE NAME; RETURNS BLANKS IF CAN'T RETRIEVE
//***
//***  SIMPLY "CALL 'GETDSN' USING FILE-NAME, RET-DSN, RET-MEM"
//***
//A          EXEC     PGM=ASMA90,REGION=4M,
//             PARM='RENT'
//SYSLIB       DD       DISP=SHR,DSN=SYS1.MACLIB
//             DD       DISP=SHR,DSN=-YOUR-SYSLIB-

*  R3 - INPUT POINTER TO DCB AREA
*  R4 - RETURNED DATA SET NAME
*  R5 - RETURNED MEMBER NAME (IF THE DATA SET IS A PDS)

         PRINT NOGEN

GETDSN   AMODE 31
GETDSN   RMODE ANY

         USING WKAREA,R13
GETDSN   RENTER  SALEN=WKAREAL

         LM    R3,R5,0(R1)        LOAD PARAMETER ADDRESSES

         MVI   0(R4),C' '         BLANK OUTPUT FIELDS
         MVC   1(43,R4),0(R4)
         MVC   0(8,R5),0(R4)
         MVC   RDJFCB(RDJFCBL),XRDJFCB
         LA    R14,JFCB           GET READY FOR READ JFCB
         ST    R14,EXLST
         MVI   EXLST,X'87'
         LA    R14,EXLST
         ST    R14,EXADDR
         MVC   DUMMYDCB(256),0(R3)  GET DCB OF FILE
         LA    R10,DUMMYDCB
         USING IHADCB,R10
         MVC   DCBEXLSA,EXADDR+1

         RDJFCB  DUMMYDCB,MF=(E,RDJFCB)  GET DSN & VOLSER

         LTR   R15,R15
         BNZ   RETURN
         MVC   0(44,R4),JFCBDSNM
         TM    JFCBIND1,JFCPDS    CHECK IF PICK UP MEMBER NAME
         BZ    RETURN
         MVC   0(8,R5),JFCBELNM

RETURN   EQU   *
         REXIT

XRDJFCB  RDJFCB  (0),MF=L

WKAREA   DSECT
SAVEAREA DS    9D
DUMMYDCB DS    CL512
EXADDR   DS    A
EXLST    DS    A
JFCB     DS   0CL176
         IEFJFCBN  ,  LIST=YES
RDJFCB   RDJFCB  (DUMMYDCB),MF=L
RDJFCBL  EQU   *-RDJFCB
WKAREAL  EQU   *-WKAREA

         DCBD  DSORG=(PS,PO),DEVD=DA

         END   GETDSN
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*,
//             DCB=(RECFM=FM,LRECL=121,BLKSIZE=121)
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(2960,(111,12)),DCB=BLKSIZE=2960
//***
//L          EXEC     PGM=IEWL,
//             PARM='RENT,AMODE(31),RMODE(ANY)',
//             REGION=4M,COND=(5,LE)
//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLMOD      DD       DSN=-YOUR-LOADLIB-,
//             DISP=SHR
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//             DD       *
  SETSSI   99092400
  ENTRY    GETDSN
  NAME     GETDSN(R)
//***
