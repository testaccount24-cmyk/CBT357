/**  REXX -- GDG FILES INSERTION MACRO - IN REVERSE ORDER  **/

   "ISREDIT MACRO (QUAL,OPTION)"
   IF QUAL = '' | QUAL = '?' THEN SIGNAL DISPDOC
   QUAL   = TRANSLATE(QUAL)
   OPTION = TRANSLATE(OPTION)
   IF OPTION = '' | OPTION <> 'STATS' THEN SIGNAL NOSTATS

/* HEADER ET AL FOR STATISTICS */
   DSNVAR  = 'DATA.SET.NAME................................'
   ZDLVOL  = 'VOLUME'
   ZDLSIZE = '.TRACKS.'
   "ISREDIT (LINE) = CURSOR"
   IF LINE = 1  THEN DO
       ADDRESS ISPEXEC 'VGET (ZSCREENC,ZSCREENI) SHARED'
       II = INDEX(ZSCREENI,'* Top of Data *')
       IF ( ZSCREENC % 80 )  <=  ( II % 80 )  THEN LINE = 0
   END
   "ISREDIT LINE_AFTER &LINE = < 1 '&DSNVAR &ZDLVOL &ZDLSIZE' >"
   DSNVAR  = '$$ NONE FOUND $$'
   TOTTRKS = 0

/* PRIMING "READ" */
   "ISPEXEC LMDINIT LISTID(DSLIST) LEVEL(&QUAL)"
   "ISPEXEC LMDLIST LISTID(&DSLIST)",
          "DATASET(DSNVAR) OPTION(LIST) STATS(YES)"

/* "READ" LOOP */
   DO WHILE RC = 0
      "ISPEXEC LMDLIST LISTID(&DSLIST)",
               "DATASET(DSNVAR) OPTION(LIST) STATS(YES)"
      IF RC > 0 THEN LEAVE
      IF ZDLMIGR = 'YES' THEN ZDLVOL = 'MIGRAT'
      DSNVAR  = STRIP(LEFT(DSNVAR,44),"T")
      DSNVAR  = DSNVAR || '='
      ZDLSIZE = RIGHT(ZDLSIZE,8)
      LINE    = LINE + 1
      "ISREDIT LINE_AFTER &LINE = < 1 '&DSNVAR &ZDLVOL &ZDLSIZE' >"
      IF DATATYPE(ZDLSIZE,N) THEN TOTTRKS = TOTTRKS + ZDLSIZE
   END
   LINE = LINE + 1
   "ISREDIT LINE_AFTER &LINE = ",
   "< 1 '..................................TOTAL TRACKS = &TOTTRKS' >"
   "ISPEXEC LMDLIST LISTID(&DSLIST)" "OPTION(FREE)"
   EXIT(0)

NOSTATS:                /* NO STATISTICS JUST NAMES */
   DSNVAR = '$$ NONE FOUND $$'
   IF OPTION = 'DD' THEN LINE_UP = 2
                    ELSE LINE_UP = 1
   "ISREDIT (LINE) = CURSOR"
   IF LINE = 1  THEN DO
       ADDRESS ISPEXEC 'VGET (ZSCREENC,ZSCREENI) SHARED'
       II = INDEX(ZSCREENI,'* Top of Data *')
       IF ( ZSCREENC % 80 )  <=  ( II % 80 )  THEN LINE = 0
   END

/* PRIMING "READ" */
   "ISPEXEC LMDINIT LISTID(DSLIST) LEVEL(&QUAL)"
   "ISPEXEC LMDLIST LISTID(&DSLIST)",
          "DATASET(DSNVAR) OPTION(LIST)"

/* "READ" LOOP */
   DO WHILE RC = 0
      "ISPEXEC LMDLIST LISTID(&DSLIST)",
               "DATASET(DSNVAR) OPTION(LIST)"
      DSNVAR  = STRIP(LEFT(DSNVAR,44),"T")
      IF RC > 0 THEN LEAVE
      IF OPTION = 'DD' THEN DO
         DSNVAR = '//             DSN=' || DSNVAR
         "ISREDIT LINE_AFTER &LINE = < 1 '&DSNVAR' >"
         DDVAR  = '//             DD       DISP=SHR,'
         "ISREDIT LINE_AFTER &LINE = < 1 '&DDVAR' >"
      END
      ELSE "ISREDIT LINE_AFTER &LINE = < 1 '&DSNVAR' >"
      LINE = LINE + LINE_UP
   END
   "ISPEXEC LMDLIST LISTID(&DSLIST)" "OPTION(FREE)"
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "GETGDGS - PLACE LIST OF GDG DATA SETS AFTER CURSOR        "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    GETGDGS  ( WILDCARD-LIST | ? )  ( STATS | DD )        "
   SAY
   SAY "       DO EQUIVALENT OF DS LIST SCAN FOR DATA SETS WITHIN "
   SAY "       THE SUPPLIED GENERATION DATA GROUP AND PLACE THEM  "
   SAY "       AFTER WHERE THE CURSOR IS POSITIONED. FOR EXAMPLE: "
   SAY "          GETGDGS  SYS4.CATOOL.BACKUP                     "
   SAY "       WOULD GATHER THE CATALOGED DATA SET(S) MATCHING    "
   SAY "       THE WILDCARD AND PASTE THEM INTO THE EDITED FILE   "
   SAY "       AFTER WHERE THE CURSOR IS POSITIONED. THESE WILL   "
   SAY "       ALWAYS BE IN REVERSE ORDER, E.G. GENERATION 3, THEN"
   SAY "       2, THEN 1 TO FACILITATE OLDEST FIRST USE. SPECIFY  "
   SAY "       'STATS' TO GET DATA SET STATISTICS, 'DD' TO CAUSE  "
   SAY "       JCL DD CARD IMAGES TO BE GENERATED AND '?' TO GET  "
   SAY "       THIS HELP.                                         "
   EXIT(1)



