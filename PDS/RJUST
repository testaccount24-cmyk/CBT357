/**  REXX -- RIGHT JUSTIFIES DATA USING CC/C## RANGE OR DEFAULT TO **/
/*        FILE; WILL ACCEPT BOUNDARIES AS PARMS OR USE ISPF BOUNDS  */

   SAVEMSG = MSG()
   X = MSG("OFF")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY "RJUST MUST BE RUN UNDER ISPF EDIT!"
      SIGNAL QUIT
      END
   SUBCOM ISREDIT
   UMODE = "ISREDIT"
   ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
   ADDRESS ISREDIT
   "MACRO (LEFT,RIGHT) NOPROCESS"
   IF LEFT = '?' THEN SIGNAL DISPDOC

   IF LEFT <> "" AND RIGHT = "" THEN DO
      "ISREDIT (L2,RIGHT) = BOUNDS"
      END

   "PROCESS RANGE C"
   IF RC > 0 THEN DO
      "(TOP) = LINENUM .ZFIRST"
      "(BOT) = LINENUM .ZLAST"
   END
   ELSE DO
      "(TOP) = LINENUM .ZFRANGE"
      "(BOT) = LINENUM .ZLRANGE"
   END

   "ISREDIT("LB","RB") = BOUNDS"
   IF LEFT  <> "" THEN LB = LEFT
   IF RIGHT <> "" THEN RB = RIGHT

   DO II = TOP TO BOT BY 1
      "ISREDIT (RECORD) = LINE &II"
      DO JJ = RB TO LB BY -1 WHILE(SUBSTR(RECORD,JJ,1) = ' ')
         END
      IF RB > JJ THEN DO
         RECORD = SUBSTR(RECORD,1,LB-1)  || SUBSTR(RECORD,JJ+1,RB-JJ),
            || SUBSTR(RECORD,LB,JJ-LB+1) || SUBSTR(RECORD,RB+1)
         "ISREDIT LINE &II = &STR(RECORD)"
         END
      END

QUIT:
   ADDRESS ISPEXEC
   X = MSG(SAVEMSG)
   ADDRESS ISREDIT "LOCATE" TOP
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "RJUST - RIGHT JUSTIFY TEXT W/IN BOUNDS                   "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    RJUST   ( LEFT-COLUMN  RIGHT-COLUMN )                "
   SAY
   SAY "       WILL CAUSE THE MARKED (VIA C/CC/C##) RANGE OF     "
   SAY "       LINES TO BE RIGHT JUSTIFIED WITHIN THE BOUNDS THEN"
   SAY "       IN FORCE.  IF NO RANGE IS SPECIFIED, THE ENTIRE   "
   SAY "       FILE IS PROCESSED.  THE FIRST AND THE LAST        "
   SAY "       NON-BLANK CHARACTERS WITHIN THE BOUNDS DETERMINE  "
   SAY "       THE SIZE OF THE DATA TO BE RIGHT JUSTIFIED.       "
   SAY "       SPECIFYING A LEFT COLUMN AND RIGHT COLUMN WILL    "
   SAY "       CAUSE ONLY THE DATA IN THOSE COLUMNS TO BE        "
   SAY "       JUSTIFIED.                                        "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    RJUST  1 50                                          "
   SAY "       WILL CAUSE THE TEST IN COLUMNS 1 THRU 50 TO BE    "
   SAY "       SHIFTED RIGHT SO THAT THE LAST NON BLANK IN THOSE "
   SAY "       COLUMNS WILL BE IN POSITION 50.                   "
   SAY
   EXIT(1)

