/**  REXX -- PK COLUMNS ONTO SAME OR OTHER COLUMNS  **/
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'PK MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (FRMLEFT,FRMRIGHT,PARMS)"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY PK 'EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT',
         'WITH NO PENDING EDIT COMMANDS!'
      SIGNAL EOJ
   END
   ADDRESS ISREDIT "CAPS OFF"

   ERRORMSG = ''
   MESG     = ''
   IF FRMLEFT = ''  |  FRMRIGHT = ''  THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMLEFT,'W')  |  FRMLEFT = 0  THEN DO
      MESG = "'"FRMLEFT"' VALUE OF FROM-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMRIGHT,'W')  |  FRMRIGHT = 0  THEN DO
      MESG = "'"FRMRIGHT"' VALUE OF FROM-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF FRMLEFT > FRMRIGHT THEN DO
      MESG = "FROM-LEFT-COL "FRMLEFT" IS > THAN FROM-RIGHT-COL "FRMRIGHT
      SIGNAL ERROR
   END

   PARMS   = TRANSLATE(PARMS)
   LBL1    = ""
   LBL2    = ""
   TOLEFT  = ""
   TORIGHT = ""
   MINUS   = 0
   DO II = 1 TO 7;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LBL1 = "" THEN LBL1 = TOKEN
         ELSE IF LBL2 = "" THEN LBL2 = TOKEN
            ELSE DO
               ZEDSMSG = ".LABEL RC=8"
               ZEDLMSG = "TOO MANY LABELS --" LBL1 LBL2 TOKEN
               ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
               EXIT 8
            END
      END
      ELSE IF SUBSTR(TOKEN,1,1) = "-" THEN DO
         MINUS = SUBSTR(TOKEN,2)
      END
      ELSE DO
         IF DATATYPE(TOKEN,'W')  THEN DO
            IF TOLEFT = "" THEN TOLEFT = TOKEN
            ELSE IF TORIGHT = "" THEN TORIGHT = TOKEN
               ELSE DO
                  ZEDSMSG = ".LABEL RC=8"
                  ZEDLMSG = "TOO MANY COLUMNS --" TOLEFT TORIGHT TOKEN
                  ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
                  EXIT 8
               END
         END
      END
   END II
   IF LBL1 <> ""  &  LBL2 = ""  THEN LBL2 = LBL1
   IF TOLEFT <> "" AND TORIGHT = "" THEN TORIGHT = TOLEFT
   IF TOLEFT = "" THEN DO
      TOLEFT  = FRMLEFT
      TORIGHT = FRMRIGHT
   END

   IF ¬DATATYPE(TOLEFT,'W')  |  TOLEFT=0  THEN DO
      MESG = "'"TOLEFT"' VALUE OF TO-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TORIGHT,'W')  |  TORIGHT=0  THEN DO
      MESG = "'"TORIGHT"' VALUE OF TO-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   LEN = FRMRIGHT - FRMLEFT + 1
   IF TOLEFT > TORIGHT THEN DO
      MESG = "TO-LEFT-COL "TOLEFT" IS GREATER THAN TO-RIGHT-COL "TORIGHT
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   IF TORIGHT > LRECL THEN DO
      MESG = "TO-RIGHT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
      IF RC <> 0 THEN DO
         MESG = "LABEL" LBL1 "NOT FOUND"
         SIGNAL ERROR
      END
   END
   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
      IF RC <> 0 THEN DO
         MESG = "LABEL" LBL2 "NOT FOUND"
         SIGNAL ERROR
      END
   END
   "ISREDIT ("LRECL") = LRECL "
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR "
   "ISREDIT ("ANUM") = AUTONUM "
   "ISREDIT (END) = LINENUM .ZLAST"

   START = 1
   IF LBL1 <> '' THEN
       "ISREDIT (START) = LINENUM &LBL1"

   IF LBL2 <> '' THEN
        "ISREDIT (END) = LINENUM &LBL2"
   ELSE
        LBL2 = LBL1

   IF START > END THEN DO
      WST = START
      START = END
      END = WST
   END

   HEX_ZEROS  = '000000000000000000000000000000'X
   GOOD_NUM   = '0123456789'
   GOOD_SIGN  = '{ABCDEFGHI}JKLMNOPQR0123456789'
   FROM_SIGN  = '{ABCDEFGHI}JKLMNOPQR0123456789'
   TO_SIGN    = '0C1C2C3C4C5C6C7C8C9C'X,
             || '0D1D2D3D4D5D6D7D8D9D'X,
             || '0F1F2F3F4F5F6F7F8F9F'X
   TO_MINUS   = '0D1D2D3D4D5D6D7D8D9D'X,
             || '0D1D2D3D4D5D6D7D8D9D'X,
             || '0D1D2D3D4D5D6D7D8D9D'X
   TO_PLUS    = '0C1C2C3C4C5C6C7C8C9C'X,
             || '0C1C2C3C4C5C6C7C8C9C'X,
             || '0C1C2C3C4C5C6C7C8C9C'X

   TOT  = 0
   OK   = 0
   OVFL = 0
   DO PNTR =  START  TO  END  BY 1
      TOT = TOT + 1
      "ISREDIT (RECORD) = LINE &PNTR"
      IF VERIFY(SUBSTR(RECORD,FRMLEFT,FRMRIGHT-FRMLEFT),GOOD_NUM) = 0,
            & VERIFY(SUBSTR(RECORD,FRMRIGHT,1),GOOD_SIGN) = 0
         THEN DO
            OK = OK + 1
            USTR = X2C(SUBSTR(RECORD,FRMLEFT,FRMRIGHT-FRMLEFT))
            IF MINUS = 0,
               THEN DO
                  ZSTR = SUBSTR(RECORD,FRMRIGHT,1)
                  ZSTR = TRANSLATE(ZSTR,TO_SIGN,FROM_SIGN)
               END
               ELSE DO
                  ZSTR = SUBSTR(RECORD,FRMRIGHT,1)
                  IF SUBSTR(RECORD,MINUS,1) = '-',
                     THEN ZSTR = TRANSLATE(ZSTR,TO_MINUS,FROM_SIGN)
                     ELSE ZSTR = TRANSLATE(ZSTR,TO_PLUS,FROM_SIGN)
               END
            USTR = USTR || ZSTR
            IF LENGTH(USTR) > TORIGHT - TOLEFT + 1,
               THEN DO
                  RECORD = SUBSTR(RECORD,1,TOLEFT-1),
                     || SUBSTR(USTR,LENGTH(USTR)-TORIGHT+TOLEFT),
                     || SUBSTR(RECORD,TORIGHT+1)
                  USTRO = LENGTH(USTR) - TORIGHT + TOLEFT - 1
                  ZEROS = COPIES('0',USTRO)
                  IF SUBSTR(RECORD,FRMLEFT,USTRO) <> ZEROS,
                     THEN OVFL = OVFL + 1
               END
               ELSE DO
                  USTR0  = TORIGHT - TOLEFT + 1 - LENGTH(USTR)
                  ZEROS  = SUBSTR(HEX_ZEROS,1,USTR0)
                  ZEROS  = ZEROS || USTR
                  RECORD = SUBSTR(RECORD,1,TOLEFT-1),
                     || ZEROS || SUBSTR(RECORD,TORIGHT+1)
               END
         END
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END

   STR = OK || '/' || OVFL || '/' || TOT
   CALL SETMSG '* NO ' STR 'PKED $VALID OVER OVERFLOW OVER TOTAL'
   "ISREDIT LOCATE" START
   "ISREDIT AUTONUM =" ANUM
    SIGNAL EOJ

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   CALL DISPDOC

EOJ:
   SAY "PLEASE NOTE, CAPS IS NOW OFF..."
   X = MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "PK - PACK DECIMALS BY COLUMNS "
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY
   SAY "    PK  FROM-COL1 FROM-COL2  (TO-COL1 TO-COL2) "
   SAY "              (-MINUS-COL)  (.A .B)            "
   SAY
   SAY "       FROM-COLUMNS ARE REQUIRED. TO-COLUMNS WILL DEFAULT "
   SAY "       TO FROM COLUMNS.  THE MINUS COLUMN AND THE LABEL   "
   SAY "       RANGES ARE OPTIONAL.  THE CAPS OPTION WILL BE      "
   SAY "       TURNED OFF TO PROTECT THE NEW DATA.                "
   SAY
   SAY " EXAMPLE "
   SAY
   SAY "    PK  1 8  .A .B  -9                               "
   SAY "       WILL PK THE CONTENTS OF COLUMNS 1 THRU 8 ON   "
   SAY "       TOP OF THOSE SAME COLUMNS AND PLACE THE + OR  "
   SAY "       MINUS FROM COLUMN 9 IN THE SIGN PORTION OF    "
   SAY "       COLUMN 8 IN THE RANGE OF .A THROUGH .B.       "
   SAY
   EXIT(1)
