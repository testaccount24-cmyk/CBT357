/**  REXX -- CONVERT BINARY TO NUMERIC DISPLAY  **/
   NUMERIC DIGITS 30
   RTN = 0
   SAVEMSG = MSG()
   IF SAVEMSG = 'OFF' THEN
      X = MSG("ON")
   X = ADDRESS()
   SUBCOM ISPEXEC
   IF RC <> 0 THEN DO
      SAY 'CVD MUST BE RUN UNDER ISPF EDIT!'
      SIGNAL EOJ
   END
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT,
         "ISREDIT MACRO (FRMLEFT,FRMRIGHT,PARMS)"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      SAY CVD 'EDIT MACRO CAN ONLY BE RUN UNDER ISPF EDIT',
         'WITH NO PENDING EDIT COMMANDS!'
      SIGNAL EOJ
   END

   ERRORMSG = ''
   MESG = ''
   IF FRMLEFT = '' THEN DO
     SIGNAL ERROR
   END
   IF FRMLEFT = ''  |  FRMRIGHT = ''  THEN DO
      MESG = 'PARAMETERS ARE MISSING!'
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMLEFT,'W')  |  FRMLEFT = 0  THEN DO
      MESG = "'"FRMLEFT"' VALUE OF FROM-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(FRMRIGHT,'W')  |  FRMRIGHT = 0  THEN DO
      MESG = "'"FRMRIGHT"' VALUE OF FROM-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF FRMLEFT > FRMRIGHT THEN DO
      MESG = "FROM-LEFT-COL "FRMLEFT" IS > THAN FROM-RIGHT-COL "FRMRIGHT
      SIGNAL ERROR
   END

   PARMS   = TRANSLATE(PARMS)
   LBL1    = ""
   LBL2    = ""
   TOLEFT  = ""
   TORIGHT = ""
   MINUS   = 0
   DO II = 1 TO 5;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LBL1 = "" THEN LBL1 = TOKEN
         ELSE IF LBL2 = "" THEN LBL2 = TOKEN
            ELSE DO
               ZEDSMSG = ".LABEL RC=8"
               ZEDLMSG = "TOO MANY LABELS --" LBL1 LBL2 TOKEN
               ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
               EXIT 8
            END
      END
      ELSE IF SUBSTR(TOKEN,1,1) = "-" THEN DO
         MINUS = SUBSTR(TOKEN,2)
      END
      ELSE DO
         IF DATATYPE(TOKEN,'W')  THEN DO
            IF TOLEFT = "" THEN TOLEFT = TOKEN
            ELSE IF TORIGHT = "" THEN TORIGHT = TOKEN
               ELSE DO
                  ZEDSMSG = ".LABEL RC=8"
                  ZEDLMSG = "TOO MANY COLUMNS --" TOLEFT TORIGHT TOKEN
                  ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
                  EXIT 8
               END
         END
      END
   END II
   IF LBL1 <> ""  &  LBL2 = ""  THEN LBL2 = LBL1
   IF TOLEFT <> "" AND TORIGHT = "" THEN TORIGHT = TOLEFT
   IF TOLEFT = "" THEN DO
      TOLEFT  = FRMLEFT
      TORIGHT = FRMRIGHT
   END

   IF ¬DATATYPE(TOLEFT,'W')  |  TOLEFT=0  THEN DO
      MESG = "'"TOLEFT"' VALUE OF TO-LEFT-COL IS INVALID!"
      SIGNAL ERROR
   END
   IF ¬DATATYPE(TORIGHT,'W')  |  TORIGHT=0  THEN DO
      MESG = "'"TORIGHT"' VALUE OF TO-RIGHT-COL IS INVALID!"
      SIGNAL ERROR
   END
   LEN = FRMRIGHT - FRMLEFT + 1
   IF TOLEFT > TORIGHT THEN DO
      MESG = "TO-LEFT-COL "TOLEFT" IS GREATER THAN TO-RIGHT-COL "TORIGHT
      SIGNAL ERROR
   END
   "ISREDIT ("LRECL") = LRECL "
   IF TORIGHT > LRECL THEN DO
      MESG = "TO-RIGHT COLUMN IS GREATER THAN RECORD SIZE ("LRECL")"
      SIGNAL ERROR
   END

   IF LBL1 <> '' THEN DO
      "ISREDIT L &LBL1"
      IF RC <> 0 THEN DO
         MESG = "LABEL" LBL1 "NOT FOUND"
         SIGNAL ERROR
      END
   END
   IF LBL2 <> '' THEN DO
      "ISREDIT L &LBL2"
      IF RC <> 0 THEN DO
         MESG = "LABEL" LBL2 "NOT FOUND"
         SIGNAL ERROR
      END
   END
   "ISREDIT ("LRECL") = LRECL "
   "ISREDIT ("SAVLINE","SAVCOL") = CURSOR "
   "ISREDIT ("ANUM") = AUTONUM "
   "ISREDIT (END) = LINENUM .ZLAST"

   START = 1
   IF LBL1 <> '' THEN
       "ISREDIT (START) = LINENUM &LBL1"

   IF LBL2 <> '' THEN
        "ISREDIT (END) = LINENUM &LBL2"
   ELSE
        LBL2 = LBL1

   IF START > END THEN DO
      WST = START
      START = END
      END = WST
   END

   TOT  = 0
   OVFL = 0
   DO PNTR =  START  TO  END  BY 1
      TOT = TOT + 1
      "ISREDIT (RECORD) = LINE &PNTR"
      N  = FRMRIGHT-FRMLEFT+1
      USTR = X2D(C2X(SUBSTR(RECORD,FRMLEFT,N)),(2*N))
      IF MINUS = 0,
         THEN DO
            IF SUBSTR(USTR,1,1) = '-'
               THEN USTR = SUBSTR(USTR,2,LENGTH(USTR)-2),
                     || TRANSLATE(SUBSTR(USTR,LENGTH(USTR),1),
                           ,'}JKLMNOPQR','0123456789')
         END
         ELSE DO
            IF SUBSTR(USTR,1,1) = '-'
               THEN DO
                  RECORD = SUBSTR(RECORD,1,MINUS-1),
                     || '-',
                     || SUBSTR(RECORD,MINUS+1)
                  USTR = SUBSTR(USTR,2)
               END
               ELSE RECORD = SUBSTR(RECORD,1,MINUS-1),
                     || '+',
                     || SUBSTR(RECORD,MINUS+1)
         END
      IF LENGTH(USTR) > TORIGHT - TOLEFT + 1,
         THEN DO
            RECORD = SUBSTR(RECORD,1,TOLEFT-1),
               || SUBSTR(USTR,LENGTH(USTR)-TORIGHT+TOLEFT),
               || SUBSTR(RECORD,TORIGHT+1)
            USTRO = LENGTH(USTR) - TORIGHT + TOLEFT - 1
            ZEROS  = COPIES('0',USTRO)
            IF SUBSTR(USTR,1,USTRO) <> ZEROS THEN OVFL = OVFL + 1
         END
         ELSE DO
            ZEROS  = COPIES('0',TORIGHT-TOLEFT+1-LENGTH(USTR))
            ZEROS  = ZEROS || USTR
            RECORD = SUBSTR(RECORD,1,TOLEFT-1),
               || ZEROS || SUBSTR(RECORD,TORIGHT+1)
         END
      "ISREDIT LINE &PNTR = &STR(RECORD)"
   END

   STR = OVFL || '/' || TOT
   CALL SETMSG '* NO ' STR 'CVD $OVERFLOW OVER TOTAL'
   "ISREDIT LOCATE" START
   "ISREDIT AUTONUM =" ANUM
    SIGNAL EOJ

SETMSG:
   PARSE ARG ZERRHM ZERRALRM ZERRSM'$'ZERRLM
   ADDRESS ISPEXEC 'SETMSG MSG(ISRZ002) COND'
   RETURN

ERROR:
   IF MESG <> '' THEN DO
      RTN = 20
      ERRORMSG = '===>' MESG
   END
   CALL DISPDOC

EOJ:
   X = MSG(SAVEMSG)
   RETURN RTN

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "CVD - BINARY CONVERSION TO DECIMAL BY COLUMNS"
   SAY
   SAY ERRORMSG
   SAY
   SAY " FORMAT "
   SAY "    CVD     FROM-COL1 FROM-COL2 (TO-COL1 TO-COL2)         "
   SAY "              (-MINUS-COL)  (.A .B)                       "
   SAY
   SAY "       FROM-COLUMNS ARE REQUIRED. TO-COLUMNS WILL DEFAULT "
   SAY "       TO FROM-COLUMNS.  THE MINUS COLUMN AND THE LABEL   "
   SAY "       RANGES ARE OPTIONAL.                               "
   SAY
   SAY " EXAMPLE "
   SAY "    CVD       15 18  73 80  .A .B  -72                    "
   SAY "       WILL CONVERT THE BINARY VALUE IN COLS THRU 18 TO   "
   SAY "       DISPLAY NUMERICS IN COLUMNS 73 THROUGH 80 WITHIN   "
   SAY "       LINE RANGE .A THRU .B AND PLACE THE SIGN IN COLUMN "
   SAY "       72.                                                "
   SAY
   EXIT(1)
