/**  REXX -- SHOW NUMBER OF UNEXCLUDED AND EXCLUDED LINES  **/
   ADDRESS "ISREDIT"
   "MACRO (PARMS) NOPROCESS"

   EXCLUDED   = 0
   UNEXCLUDED = 0
   LABF       = ""
   LABL       = ""

   PARMS = TRANSLATE(PARMS)
   IF PARMS = '?' THEN SIGNAL DISPDOC
   DO II = 1 TO 10;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LABF = "" THEN LABF = TOKEN
         ELSE IF LABL = "" THEN LABL = TOKEN
         ELSE DO
            ZEDSMSG = ".LABEL RC=8"
            ZEDLMSG = "TOO MANY LABELS --" LABF LABL TOKEN
            ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
            EXIT 8
         END
      END
   END II
   IF LABF <> ""  &  LABL = ""  THEN LABL = LABF

   IF LABL = "" THEN DO
      "PROCESS RANGE C"
      IF RC = 0 THEN DO
         LABF = ".ZFRANGE"
         LABL = ".ZLRANGE"
      END
      ELSE DO
         LABF = ".ZFIRST"
         LABL = ".ZLAST"
      END
   END

   "(LABFNO) = LINENUM" LABF
   "(LABLNO) = LINENUM" LABL

   DO II = LABFNO TO LABLNO
      "(LINESTAT) = XSTATUS" II
      IF LINESTAT = "NX" THEN UNEXCLUDED = UNEXCLUDED + 1
                         ELSE   EXCLUDED = EXCLUDED   + 1
   END

   ZEDSMSG = UNEXCLUDED "NX /" EXCLUDED "X LINES"
   ZEDLMSG = "THERE ARE" UNEXCLUDED "DISPLAYED LINES & ",
      || EXCLUDED "EXCLUDED LINES"
   ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "COUNTXNX - SHOW NUMBERS OF DISPLAYED/EXCLUDED LINES         "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    COUNTXNX  ( ? )  ( .A .B )                              "
   SAY
   SAY "       SHOWS THE COUNT OF UNEXCLUDED AND EXCLUDED LINES IN  "
   SAY "       UPPER RIGHT HAND CORNER OF SCREEN IN VIEW OR EDIT.   "
   SAY "       THE RANGE TO BE PROCESSED MAY BE SPECIFIED VIA       "
   SAY "       LABELS OR C/CC/C# PREFIX COMMANDS. A SINGLE QUESTION "
   SAY "       MARK CAUSES THIS HELP TO BE DISPLAYED.               "
   SAY
   SAY " EXAMPLE                                                    "
   SAY
   SAY "    COUNTXNX                                                "
   SAY
   SAY "       SHOWS THE NUMBER OF DISPLAYED AND EXCLUDED LINES     "
   SAY "       WITHIN THE ENTIRE FILE.                              "
   SAY
   EXIT(1)

