/**  REXX -- EXCLUDE LINES BEFORE/AFTER SHOWN LINES  **/
   ADDRESS "ISREDIT"
   "MACRO (PARMS) NOPROCESS"

   AFTER  = 1
   BEFORE = 1
   LABF   = ""
   LABL   = ""
   FIRST  = -1

   PARMS = TRANSLATE(PARMS)
   IF PARMS = '?' THEN SIGNAL DISPDOC
   DO II = 1 TO 10;
      TOKEN = WORD(PARMS,II)
      IF TOKEN = "" THEN LEAVE
      IF SUBSTR(TOKEN,1,6) = "AFTER(" THEN DO;
         PARSE VAR TOKEN  "AFTER(" AFTER ")" RIGHT
         ITERATE II;
      END
      IF "BEFORE(" = SUBSTR(TOKEN,1,7)  THEN DO;
         PARSE VAR TOKEN  "BEFORE(" BEFORE ")" RIGHT
         ITERATE II;
      END
      IF SUBSTR(TOKEN,1,1) = "." THEN DO
         IF LABF = "" THEN LABF = TOKEN
         ELSE IF LABL = "" THEN LABL = TOKEN
         ELSE DO
            ZEDSMSG = ".LABEL RC=8"
            ZEDLMSG = "TOO MANY LABELS --" LABF LABL TOKEN
            ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
            EXIT 8
         END
      END
   END II
   IF LABF <> ""  &  LABL = ""  THEN LABL = LABF

   IF LABL = "" THEN DO
      "PROCESS RANGE C"
      IF RC = 0 THEN DO
         LABF = ".ZFRANGE"
         LABL = ".ZLRANGE"
      END
      ELSE DO
         LABF = ".ZFIRST"
         LABL = ".ZLAST"
      END
   END

   "(LABFNO) = LINENUM" LABF
   "(LABLNO) = LINENUM" LABL

   DO II = LABFNO TO LABLNO
      "(LINESTAT) = XSTATUS" II
      IF LINESTAT = "NX" THEN DO
         RR = -1
         DO JJ = (II+1) TO LABLNO
            "(LINESTAT) = XSTATUS" JJ
            IF LINESTAT = "X" THEN DO
               RR = JJ
               IF (JJ-II) >= BEFORE THEN DO
                  DO KK = (JJ-1) TO (JJ-BEFORE) BY -1
                     "XSTATUS" KK "= X";
                     IF FIRST = -1 THEN FIRST = KK
                  END
                  JJ = LABLNO + 1
               END
            END
         END
         IF RR = -1 THEN II = LABLNO + 1
                    ELSE II = RR + 1
      END
   END

   DO II = LABFNO TO LABLNO
      "(LINESTAT) = XSTATUS" II
      IF LINESTAT = "X" THEN DO
         RR = -1
         DO JJ = (II+1) TO LABLNO
            "(LINESTAT) = XSTATUS" JJ
            IF LINESTAT = "NX" THEN DO
               DO KK = JJ TO LABLNO
                  "(LINESTAT) = XSTATUS" KK
                  IF (KK-JJ) >= AFTER  |  LINESTAT = "X"  THEN DO
                     RR = KK
                     JJ = LABLNO + 1
                     KK = LABLNO + 1
                  END
                  ELSE DO
                     "XSTATUS" KK "= X"
                  END
               END
            END
         END
         IF RR = -1 THEN II = LABLNO + 1
                    ELSE II = RR
      END
   END

   IF FIRST = -1 THEN DO
       "LOCATE" 0
   END
   ELSE DO
      IF FIRST > 1 THEN FIRST = FIRST - 1
      "(LINESTAT) = XSTATUS" FIRST
      "LOCATE" FIRST
      "XSTATUS" FIRST "=" LINESTAT
   END

   ZEDSMSG = "LESS" BEFORE"/"AFTER
   ZEDLMSG = "LESS COMPLETED BEFORE="BEFORE "AND AFTER="AFTER
   ADDRESS "ISPEXEC" "SETMSG MSG(ISRZ000)"
   EXIT(1)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "LESS - EXCLUDE LINE(S) BEFORE/AFTER CURRENTLY EXCLUDED LINES"
   SAY
   SAY " FORMAT "
   SAY
   SAY "    LESS    ( BEFORE(#) )   ( AFTER(#) )   ( .A .B )        "
   SAY
   SAY "       EXCLUDES THE NUMBER OF LINES SPECIFIED IN BEFORE OR  "
   SAY "       AFTER AROUND EACH GROUP OF CURRENTLY EXCLUDED LINES, "
   SAY "       DEFAULTING TO 1 BEFORE AND AFTER. THE RANGE TO BE    "
   SAY "       PROCESSED MAY BE SPECIFIED VIA LABELS OR C/CC/C#     "
   SAY "       PREFIX COMMANDS.                                     "
   SAY
   SAY " EXAMPLE                                                    "
   SAY
   SAY "    LESS  AFTER(0)                                          "
   SAY
   SAY "       WILL CAUSE 1 LINE TO BE EXCLUDED BEFORE EACH EXCLUDED"
   SAY "       GROUP, THE DEFAULT FOR BEFORE, AND NO LINES AFTER    "
   SAY "       WITHIN THE ENTIRE FILE.                              "
   SAY
   EXIT(1)

