//-YOUR-USERID-D JOB  (-YOUR-ACCT-INFO-),'-YOUR-NAME-',
//             CLASS=2,MSGCLASS=X,NOTIFY=-YOUR-USERID-
//***
//***  BASIC PL/I TO DYNAMICALLY ALLOCATE A SEQUENTIAL FILE,
//***  READ AND WRITE IT
//***
//C          EXEC     PGM=IEL0AA,REGION=4M,
//             PARM=('AG,A,NCT,ESD,F(I),NOFLOW,NIS,NOLIST,M,NMI,NEST',
//             'MAP,OF,OPT(TIME),OP,SZ(MAX),S,STMT,STG,X(F)')
//STEPLIB      DD       DISP=SHR,DSN=-YOUR-PLI-LIB-

 BASE:  PROC(PARM) OPTIONS(MAIN) REORDER;

        DECLARE DYNA   ENTRY(CHAR(44),      /* DSNAME            */
                             CHAR(8),       /* MEMBER            */
                             CHAR(8),       /* DDNAME            */
                             FIXED BIN(15), /* DYNALLOC R15 R/C  */
                             FIXED BIN(15), /* INFO  ERROR  CODE */
                             FIXED BIN(15)) /* INFO  REASON CODE */
                       OPTIONS(ASM);

        DECLARE DYNF   ENTRY(CHAR(08) )     /* DDNAME            */
                       OPTIONS(ASM);

        DCL  PARM      CHAR(00100) VARYING;
        DCL  REC       CHAR(05120) STATIC VARYING;
        DCL  OLDREC    CHAR(05120) STATIC VARYING;
        DCL  I_EOF_IND CHAR(01) STATIC INIT('N');
        DCL  REWRITE_REC  CHAR(01) STATIC INIT('N');

        DCL  (II,JJ)   FIXED BIN(31) STATIC INIT(0);

        DCL  ADDED#    FIXED BIN(31) STATIC INIT(0);
        DCL  DELETED#  FIXED BIN(31) STATIC INIT(0);
        DCL  DUPS#     FIXED BIN(31) STATIC INIT(0);
        DCL  READ#     FIXED BIN(31) STATIC INIT(0);
        DCL  UPDATED#  FIXED BIN(31) STATIC INIT(0);
        DCL  WRITTEN#  FIXED BIN(31) STATIC INIT(0);
        DCL  DATE      BUILTIN;
        DCL  TIME      BUILTIN;

        DCL  DSNAME    CHAR(44) STATIC
                       INIT(' ');
        DCL  MEMBER    CHAR(08) STATIC INIT(' ');
        DCL  DDNAME    CHAR(08) STATIC INIT('I');
        DCL  DYNA_R15  FIXED BIN(15) STATIC INIT(0);
        DCL  DYNA_RC   FIXED BIN(15) STATIC INIT(0);
        DCL  DYNA_WHY  FIXED BIN(15) STATIC INIT(0);


        IF INDEX(PARM,'(') = 0
           THEN DO;
              DSNAME = PARM;
              MEMBER = ' ';
              END;
           ELSE DO;
              II     = INDEX(PARM,'(');
              DSNAME = SUBSTR(PARM,1,II-1);
              JJ     = INDEX(PARM,')');
              MEMBER = SUBSTR(PARM,II+1,JJ-II-1);
              END;
        CALL DYNA(DSNAME,MEMBER,DDNAME,DYNA_R15,DYNA_RC,DYNA_WHY);

        IF DYNA_R15 = 0
           THEN DO;
              OPEN FILE(I) INPUT SEQL RECORD TITLE(DDNAME);
              ON ENDFILE(I) I_EOF_IND = 'Y';

              CALL INITIALIZATION;

              DO WHILE ( I_EOF_IND  = 'N' );
                 CALL READ_WRITE_LOOP;
                 END;

              CALL TERMINATION;
              CALL DYNF(DDNAME);
              CALL PLIRETC(DYNA_R15);
              END;
           ELSE DO;
              PUT EDIT ( '***  DSNAME:                 ', DSNAME   )
                       ( SKIP(2), A, A );
              PUT EDIT ( '***  MEMBER:                 ', MEMBER   )
                       ( SKIP(2), A, A );
              PUT EDIT ( '***  DDNAME:                 ', DDNAME   )
                       ( SKIP(2), A, A );
              PUT EDIT ( '**************************************' )
                       ( SKIP(1), A );
              PUT EDIT ( '***  DYNALLOC R15 RET VALUE: ', DYNA_R15 )
                       ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '***  DYNA ERROR:             ', DYNA_RC  )
                       ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '***  DYNA REASON:            ', DYNA_WHY )
                       ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
              PUT EDIT ( '**************************************' )
                       ( SKIP(1), A );
              CALL PLIRETC(DYNA_R15);
              END;


 PROCESS: PROC;
        /***   ...   ***/
        END PROCESS;

 INITIALIZATION: PROC;
        READ FILE(I) INTO(REC);
        END INITIALIZATION;

 READ_WRITE_LOOP: PROC;
        REWRITE_REC = 'Y';
        READ# = READ# + 1;
        CALL PROCESS;
        IF REWRITE_REC = 'Y'
           THEN DO;
              WRITE FILE(O) FROM(REC);
              WRITTEN# = WRITTEN# + 1;
              END;
           ELSE DO;
              DELETED# = DELETED# + 1;
              END;
        READ FILE(I) INTO(REC);
        END READ_WRITE_LOOP;

 TERMINATION: PROC;
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  ', DATE, ' STATISTICS ', TIME, '  ****' )
                 ( SKIP(1), A, A, A, A, A );
        PUT EDIT ( '**************************************' )
                 ( SKIP(1), A );
        PUT EDIT ( '***  NUMBER RECORDS READ:    ', READ#    )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS WRITTEN: ', WRITTEN# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS UPDATED: ', UPDATED# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS ADDED:   ', ADDED#   )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS DELETED: ', DELETED# )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        PUT EDIT ( '***  NUMBER RECORDS DUPS:    ', DUPS#    )
                 ( SKIP(2), A, P'Z,ZZZ,ZZ9' );
        IF LENGTH(PARM) > 0
           THEN DO;
              PUT EDIT ( '***  PARM LENGTH/PARM ARE:  ',
                         LENGTH(PARM), '/', PARM )
                       ( SKIP(2), A, P'ZZ9', A, A  );
              END;
        END TERMINATION;

        END BASE;

//SYSUT1       DD       UNIT=VIO,
//             SPACE=(CYL,(05,02),,CONTIG)
//SYSPRINT     DD       SYSOUT=*
//SYSLIN       DD       DSN=&&LOADSET,DISP=(MOD,PASS),
//             UNIT=VIO,SPACE=(CYL,(05,02))
//***
//GO         EXEC     PGM=LOADER,
//             PARM='MAP,PRINT/NOSPIE,NOSTAE/-YOUR-SYSLIB-(TESTIP)',
//             REGION=4M,COND=(5,LT)
//SYSLIB       DD       DISP=SHR,DSN=-YOUR-SCEELLED-
//             DD       DISP=SHR,DSN=-YOUR-LOADLIB-
//SYSLIN       DD       DSN=&&LOADSET,DISP=(OLD,DELETE,DELETE)
//SYSLOUT      DD       SYSOUT=*
//O            DD       SYSOUT=*,
//             DCB=(RECFM=F,LRECL=80,BLKSIZE=80)
//SYSPRINT     DD       SYSOUT=*
//SYSUDUMP     DD       SYSOUT=*
//***
