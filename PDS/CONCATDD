/**  REXX -- CONCATENATE A DATA SET TO A DDNAME                    **/
/*                                                                  */
/* SYNTAX FOR INVOKING THIS CLIST IS:                               */
/*    CONCATDD DDNAME DATA.SET.NAME   (TO PUT IT AT THE BEGINNING)  */
/*    CONCATDD DDNAME DATA.SET.NAME B (TO PUT IT AT THE END)        */
/*       NOTE NO QUOTES ON THE DATA SET NAME!                       */
/********************************************************************/
   PARSE  UPPER  ARG  DDNAME  DSNAME  B
   X = MSG("OFF")
   IF SUBSTR(DDNAME,1,1) = '?' THEN SIGNAL DISPDOC
   IF SYSDSN("'"DSNAME"'") Â¬= 'OK' THEN DO
       SAY  DSNAME 'DOES NOT EXIST OR ISN''T CATALOGED'
       EXIT
    END
   X = OUTTRAP("TRAP.","*")
   "LISTALC STATUS"
   X= OUTTRAP("OFF")

   FOUND = 'NO'
   DO  II  =  1  TO  TRAP.0  WHILE  FOUND <> 'YES'
      DSN = TRAP.II
      IF SUBSTR(DSN,1,10) = '  ' || DDNAME THEN DO
         FOUND   = 'YES'
         JJ  = II - 1
         DSN = TRAP.JJ
         IF DSN <> DSNAME THEN CONCAT = "'" || DSN || "'"
      END
   END

   IF  FOUND = 'YES' THEN DO
      DO JJ = (II+1) TO (TRAP.0) BY 2
         DSN = TRAP.JJ
         IF SUBSTR(DSN,3,8) = ' ' THEN DO
            KK = JJ - 1
            DSN = TRAP.KK
            IF DSN <> DSNAME THEN CONCAT = CONCAT || " '" || DSN || "'"
         END
         ELSE JJ = TRAP.0 +100
      END
   END
   ELSE DO
      SAY 'DDNAME' DDNAME 'NOT CURRENTLY ALLOCATED'
      EXIT
   END

   DSNBOT = ''
   DSNTOP = ''
   IF B = 'B' THEN DSNBOT = "'" || DSNAME || "'"
              ELSE DSNTOP = "'" || DSNAME || "'"
   "ALLOC DD("DDNAME") DSN("DSNTOP CONCAT DSNBOT") SHR REU"
   EXIT(0)

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "CONCATDD - CONCATENATE A DATA SET TO A DD NAME            "
   SAY
   SAY " FORMAT "
   SAY
   SAY "    CONCATDD  DDNAME  DSNAME  ( B | T )                   "
   SAY
   SAY "       WILL ADD THE DATA SET TO THE TOP OR BOTTOM OF THE  "
   SAY "       DD NAME'S CONCATENATION. THE DATA SET NAME MUST BE "
   SAY "       IN SINGLES QUOTES. 'T' FOR TOP IS THE DEFAULT      "
   SAY
   SAY "       WILL CHECK IF THE DATA SET EXISTS AND WHETHER THE  "
   SAY "       DD NAME IS ALLOCATED. WILL MOVE THE DATA SET TO    "
   SAY "       THE TOP OR BOTTOM, AS SPECIFIED, IF ALREADY IN THE "
   SAY "       CONCATENATION.                                     "
   SAY
   EXIT(1)

