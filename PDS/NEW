/**  REXX - ALLOCATE NEW SEQUENTIAL DATA SET  **/
   SAVEMSG = MSG("OFF")
   CALLED = SYSVAR(SYSNEST)
   X = ADDRESS()
   SUBCOM ISREDIT
   IF RC = 0 THEN DO
      UMODE = 'ISREDIT'
      ADDRESS ISPEXEC "CONTROL ERRORS RETURN"
      ADDRESS ISREDIT "ISREDIT MACRO ("DSN SP1 SP2 RF LR")"
      IF RC = 0 THEN DO
         ADDRESS ISREDIT "ISREDIT ("DSN") = DATASET"
      END
   END
   IF DSN = 'DSN' THEN DO
      UMODE = 'ISPEXEC'
      ARG DSN SP1 SP2 RF LR
   END
   ELSE DO
      ADDRESS ISREDIT "ISREDIT ("LIN","COL") = CURSOR"
   END
   IF DSN = '?' | DSN = '' THEN SIGNAL DISPDOC

   DSN = TRANSLATE(DSN)
   RF  = TRANSLATE(RF)
   SELECT
      WHEN RF = 'FB' THEN DO
         EXPRF = 'F B'
         BS = ( 27998 % LR ) * LR
         END
      WHEN RF = 'FBA' THEN DO
         EXPRF = 'F B A'
         BS = ( 27998 % LR ) * LR
         END
      WHEN RF = 'VB' THEN DO
         EXPRF = 'V B'
         BS = 27998
         END
      WHEN RF = 'VBA' THEN DO
         EXPRF = 'V B A'
         BS = 27998
         END
      WHEN RF = 'F'  THEN DO
         EXPRF = 'F'
         BS = LR
         END
      WHEN RF = 'FA'  THEN DO
         EXPRF = 'F A'
         BS = LR
         END
      WHEN RF = 'V'  THEN DO
         EXPRF = 'V'
         BS = LR + 4
         END
      WHEN RF = 'VA'  THEN DO
         EXPRF = 'V A'
         BS = LR + 4
         END
      WHEN RF = 'U'  THEN DO
         EXPRF = 'U'
         BS = LR
         LR = 0
         END
      OTHERWISE DO
         SAY 'UNEXPECTED RECORD FORMAT FOUND: ' RF
         EXIT(12)
         END
   END  /* END SELECT */

   ADDRESS TSO "ALLOCATE DSN("DSN") NEW CYL SP("SP1" "SP2")",
               "RECFM("EXPRF") LRECL("LR") BLKSIZE("BS")"
   RETCODE = RC
   IF RETCODE = 0 THEN DO
      ANSW = "NEW DATA SET" DSN "SUCCESSFULLY ALLOCATED"
      ADDRESS TSO "FREE DSN("DSN")"
   END
   ELSE DO
      ANSW = DSN "NOT ALLOCATED - RETURN CODE =" RETCODE
   END

   SAY ANSW
   EXIT  0

DISPDOC:
   ADDRESS TSO "CLEAR"
   SAY "NEW - ALLOCATE NEW SEQUENTIAL DATA SET                 "
   SAY
   SAY " NEW  DSN SPACE1 SPACE2 RECFM LRECL                    "
   SAY
   SAY "    DSN     - NEW DATA SET NAME                        "
   SAY
   SAY "    SPACE1  - PRIMARY SPACE (IN CYLINDERS)             "
   SAY
   SAY "    SPACE2  - SECONDARY SPACE (IN CYLINDERS)           "
   SAY
   SAY "    RECFM   - RECORD FORMAT, E.G. FB                   "
   SAY
   SAY "    LRECL   - LOGICAL RECORD LENGTH                    "
   SAY
   SAY "  THE BLOCK SIZE WILL BE SET AS CLOSE AS POSSIBLE      "
   SAY "  TO HALF TRACK BLOCKING, I.E. <= 27,998, UNLESS       "
   SAY "  IT'S SET TO 27,998 FOR VB FORMAT.                    "
   SAY
   EXIT(255)
